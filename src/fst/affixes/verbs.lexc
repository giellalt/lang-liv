!! Verb inflection
! ---------------
!! The LIVONIAN language verbs inflect in persons.

LEXICON V_
!! CONJUGATION TYPE MISSING
: # ;

LEXICON TV_
!! CONJUGATION TYPE MISSING
+TV: K ;

LEXICON V-AUX_LǞʼDÕ
!! 1 lǟʼdõ:lǟʼ
+Aux: K ;

LEXICON IV_LǞʼDÕ
!! 1 lǟʼdõ:lǟʼ
+IV: K ;

LEXICON TV_TǬʼDÕ
!! 2 tǭʼdõ:tǭʼ
+TV: V-01_TǬʼDÕ ;

LEXICON V-AUX_VĪDÕ
!! 3 vīdõ:vī
+Aux: V-01_VĪDÕ ;

LEXICON IV_VĪDÕ
!! 3 vīdõ:vī
+IV: V-01_VĪDÕ ;

LEXICON TV_VĪDÕ
!! 3 vīdõ:vī
+TV: V-01_VĪDÕ ;

!LEXICON IV_NǞʼDÕ
!! 4 nǟʼdõ:nǟʼ
!+IV: V-01_NǞʼDÕ ;

LEXICON TV_NǞʼDÕ
!! 4 nǟʼdõ:nǟʼ
!! * Yaml: __naeaeqdw__
+TV: V-01_NǞʼDÕ ;

LEXICON IV_KǞʼDÕ
!! 5 kǟʼdõ:kǟʼ
+IV: K ;

LEXICON TV_TĪʼEDÕ
!! 6 tīʼedõ:tīʼe
+TV: K ;


LEXICON V-AUX_SĪEDÕ
!! 7 sīedõ:sīe
+Aux: V-01_SĪEDÕ ;

LEXICON IV_SĪEDÕ
!! 7 sīedõ:sīe
+IV: V-01_SĪEDÕ ;

LEXICON TV_SĪEDÕ
!! 7 sīedõ:sīe
+TV: V-01_SĪEDÕ ;

LEXICON IV_SǬDÕ
!! 8 sǭdõ:s
+IV: V-01_SǬDÕ ;

LEXICON TV_SǬDÕ
!! 8 sǭdõ:s
+TV: V-01_SǬDÕ ;

LEXICON V-AUX_SǬDÕ
!! 8 sǭdõ:s
+Aux: V-01_SǬDÕ ;

!LEXICON IV_JŪODÕ
!! 9 jūodõ:jūo
!+IV: V-01_JŪODÕ ;

LEXICON TV_JŪODÕ
!! 9 jūodõ:jūo
+TV: V-01_JŪODÕ ;

LEXICON V-AUX_VȰLDA
!! 10 vȱlda:
+Aux: K ;

LEXICON IV_VȰLDA
!! 10 vȱlda:
+IV: K ;

LEXICON IV_TŪLDA
!! 11 tūlda:
+IV: K ;

LEXICON V-AUX_PĀNDA
!! 12 pānda:
+Aux: K ;

LEXICON IV_PĀNDA
!! 12 pānda:
+IV: K ;

LEXICON TV_PĀNDA
!! 12 pānda:
+TV: K ;

LEXICON IV_JEʼLLÕ
!! 13 jeʼllõ:jeʼlā
+IV: V-01_JEʼLLÕ ;

LEXICON TV_JEʼLLÕ
!! 13 jeʼllõ:jeʼllõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_ASTÕ
!! 18 astõ:astõ
+IV: V-01_JEʼLLÕ ;

LEXICON TV_ASTÕ
!! 18 astõ:astõ
+TV: V-01_JEʼLLÕ ;

LEXICON TV_VÕTTÕ
!! 19 võttõ:võttõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_VIEʼDDÕ
!! 24 vieʼddõ:vieʼddõ
+IV: V-01_JEʼLLÕ ;

LEXICON TV_VIEʼDDÕ
!! 24 vieʼddõ:vieʼddõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_MAKSÕ
!! 25 maksõ:maksõ
+IV: V-01_JEʼLLÕ ;

LEXICON TV_MAKSÕ
!! 25 maksõ:maksõ
+TV: V-01_JEʼLLÕ ;

LEXICON TV_TAPPÕ
!! 26 tappõ:tappõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_MÄNGÕ
!! 14 mängõ:mǟnga
+IV: V-01_MÄNGÕ ;

LEXICON TV_KILLÕ
!! 15 killõ:kīla
+TV: V-01_MÄNGÕ ;

LEXICON TV_PALLÕ
!! 16 pallõ:pǭla
+TV: V-01_MÄNGÕ ;

LEXICON TV_LOULÕ
!! 17 loulõ:lōla
+TV: V-01_MÄNGÕ ;

LEXICON IV_LAITÕ
!! 20 laittõ:lāita
+IV: V-01_MÄNGÕ ;

LEXICON TV_LAITÕ
!! 20 laittõ:lāita
+TV: V-01_MÄNGÕ ;

LEXICON IV_TÄUTÕ
!! 21 täutõ:tǟta
+IV: V-01_MÄNGÕ ;

LEXICON TV_TÄUTÕ
!! 21 täutõ:tǟta
+TV: V-01_MÄNGÕ ;

!LEXICON IV_PȮĻTÕ
!! 22 pȯļtõ:pūoļta
!+IV: V-01_MÄNGÕ ;

LEXICON TV_PȮĻTÕ
!! 22 pȯļtõ:pūoļta
+TV: V-01_MÄNGÕ ;

!LEXICON IV_MȮISTÕ
!! 23 mȯistõ:mūošta
!+IV: V-01_MÄNGÕ ;

LEXICON TV_MȮISTÕ
!! 23 mȯistõ:mūošta
+TV: V-01_MÄNGÕ ;

LEXICON IV_ANDÕ
!! 27 andõ:ānda
+IV: V-01_MÄNGÕ ;

LEXICON TV_ANDÕ
!! 27 andõ:ānda
+TV: V-01_MÄNGÕ ;

LEXICON IV_TIEUDÕ
!! 28 tieudõ:tīeda
+IV: V-01_MÄNGÕ ;

LEXICON TV_TIEUDÕ
!! 28 tieudõ:tīeda
+TV: V-01_MÄNGÕ ;

!! 29-48 follow same pattern
LEXICON IV_LUʼGGÕ
!! luʼggõ:luʼggõ 29
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_LUʼGGÕ
!! luʼggõ:lugū 29
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_MUʼDŽÕ
!! muʼdžõ:mudžū 30
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_MUʼDŽÕ
!! muʼdžõ:mudžū 30
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_VAKȚÕ
!! vakțõ:vakțū 31
+IV:     V-01_LUʼGGÕ      ;

LEXICON TV_VAKȚÕ
!! vakțõ:vakțū 31
+TV:	V-01_LUʼGGÕ	;

LEXICON IV_KITTÕ
!! kittõ:kitū 32
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_KITTÕ
!! kittõ:kitū 32
+TV:     V-01_LUʼGGÕ ;

LEXICON V-AUX_RIʼDDÕ
!! riʼddõ:ridū 33
+Aux:     V-01_LUʼGGÕ ;

LEXICON IV_RIʼDDÕ
!! riʼddõ:ridū 33
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_RIʼDDÕ
!! riʼddõ:ridū 33
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_KUTSÕ
!! kutsõ:kutsū 34
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_KUTSÕ
!! kutsõ:kutsū 34
+TV:     V-01_LUʼGGÕ ;

LEXICON V-AUX_LASKÕ
!! laskõ:laskū 35
+Aux:     V-01_LUʼGGÕ ;

LEXICON IV_LASKÕ
!! laskõ:laskū 35
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_LASKÕ
!! laskõ:laskū 35
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_AKKÕ
!! akkõ:akū 36
!! Should ss be s and šš be š? 2013-02-19
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_AKKÕ
!! akkõ:akū 36
+TV:     V-01_LUʼGGÕ ;


LEXICON V-AUX_AIGÕ
!! aigõ:āigõ 37
+Aux:	V-01_AIGÕ ;

LEXICON IV_AIGÕ
!! aigõ:āigõ 37
+IV:  V-01_AIGÕ ;

LEXICON TV_AIGÕ
!! aigõ:āigõ 37
+TV:  V-01_AIGÕ ;

LEXICON TV_KUOŖŖÕ
!! kuoŗŗõ:kūoŗõ 38
+TV:     V-01_AIGÕ       ;

LEXICON TV_VANNÕ
!! vannõ:vǭnõ 39
+TV:     V-01_AIGÕ       ;


LEXICON IV_PȮĻĻÕ
!! pȯļļõ:pūoļõ 40
+IV:     V-01_AIGÕ       ;


LEXICON IV_PȮIMÕ
!! pȯimõ:pūoimõ 41
+IV:     V-01_AIGÕ       ;

LEXICON TV_PȮIMÕ
!! pȯimõ:pūoimõ 41
+TV:     V-01_AIGÕ       ;

LEXICON IV_OUŖÕ
!! ouŗõ:ōŗõ 42
+IV:     V-01_AIGÕ       ;

LEXICON IV_KEIJÕ
!! keijõ:kējõ 43
+IV:     V-01_AIGÕ       ;

LEXICON TV_KEIJÕ
!! keijõ:kējõ 43
+TV:     V-01_AIGÕ       ;


LEXICON IV_AŖŠTÕ
!! aŗštõ:āŗštõ 44
+IV:     V-01_AIGÕ       ;

LEXICON TV_AŖŠTÕ
!! aŗštõ:āŗštõ 44
+TV:	V-01_AIGÕ	;

LEXICON TV_PȮRTÕ
!! pȯrtõ:pūortõ 45
+TV:     V-01_AIGÕ       ;

LEXICON TV_OUTÕ
!! outõ:ōtõ 46
+TV:     V-01_AIGÕ       ;

LEXICON V-AUX_TUNDÕ
!! tundõ:tūndõ 47
+Aux:    V-01_AIGÕ       ;

LEXICON IV_TUNDÕ
!! tundõ:tūndõ 47
+IV:	V-01_AIGÕ	;

LEXICON TV_TUNDÕ
!! tundõ:tūndõ 47
+TV:     V-01_AIGÕ       ;

LEXICON TV_OUDÕ
!! oudõ:ōdõ 48
+TV:	V-01_AIGÕ	;


LEXICON IV_KŪLÕ
!! kūlõ:kūlõ 49
+IV: V-01_KŪLÕ ;

LEXICON TV_KŪLÕ
!! kūlõ:kūlõ 49
+TV: V-01_KŪLÕ ;

LEXICON IV_ARRÕ
!! arrõ:arrõ 50
+IV: V-01_KŪLÕ ;

LEXICON TV_ARRÕ
!! arrõ:arrõ 50
+TV: V-01_KŪLÕ ;

LEXICON IV_AʼILÕ
!! aʼilõ:aʼilõ 51
+IV: V-01_AʼILÕ ;

LEXICON TV_AʼILÕ
!! aʼilõ:aʼilõ 51
+TV: V-01_AʼILÕ ;

LEXICON TV_SVAʼRRÕ
!! svaʼrrõ:svaʼrrõ 52
+TV: V-01_KŪLÕ ;

LEXICON V-AUX_KĪTÕ
!! kītõ:kīt 53
+Aux: V-01_KŪLÕ ;

LEXICON IV_KĪTÕ
!! kītõ:kīt 53
!! ~701
+IV: V-01_KŪLÕ ;

LEXICON TV_KĪTÕ
!! kītõ:kīt 53
+TV: V-01_KŪLÕ ;


LEXICON IV_ÄʼBȚÕ
!! äʼbțõ:äʼbț 54
+IV: V-01_KŪLÕ ;

LEXICON TV_ÄʼBȚÕ
!! äʼbțõ:äʼbț 54
+TV: V-01_KŪLÕ ;

!LEXICON V_KŪLDÕ
!! kūldõ:kūld 55
! V-01_KŪLÕ ;

LEXICON V-AUX_KŪLDÕ
!! kūldõ:kūld 55
+Aux: V-01_KŪLÕ ;

LEXICON IV_KŪLDÕ
!! kūldõ:kūld 55
+IV: V-01_KŪLÕ ;

LEXICON TV_KŪLDÕ
!! kūldõ:kūld 55
+TV: V-01_KŪLÕ ;

LEXICON TV_KĪSKÕ
!! kīskõ:kīsk 56
+TV: V-01_KŪLÕ ;

LEXICON V-AUX_ĪʼEDÕ
!! īʼedõ:īed 57
+Aux: V-01_KŪLÕ ;

LEXICON IV_ĪʼEDÕ
!! īʼedõ:īed 57
+IV: V-01_KŪLÕ ;

LEXICON TV_ĪʼEDÕ
!! īʼedõ:īed 57
+TV: V-01_KŪLÕ ;

!LEXICON V_UMBLÕ
!! umblõ:umblõ 58
! V-01_UMBLÕ ;

LEXICON IV_UMBLÕ
!! umblõ: 58
+IV: V-01_UMBLÕ ;

LEXICON TV_UMBLÕ
!! umblõ: 58
+TV: V-01_UMBLÕ ;

LEXICON V-AUX_MÕTLÕ
!! mõtlõ: 59
+Aux: V-01_UMBLÕ ;

LEXICON IV_MÕTLÕ
!! mõtlõ: 59
+IV: V-01_UMBLÕ ;

LEXICON TV_MÕTLÕ
!! mõtlõ: 59
+TV: V-01_UMBLÕ ;

LEXICON IV_MǞʼDLÕ
!! mǟʼdlõ: 60
+IV: V-01_UMBLÕ ;

LEXICON TV_MǞʼDLÕ
!! mǟʼdlõ: 60
+TV: V-01_UMBLÕ ;

LEXICON IV_NAʼGRÕ
!! naʼgrõ: 60
+IV: V-01_UMBLÕ ;

LEXICON TV_NAʼGRÕ
!! naʼgrõ: 60
+TV: V-01_UMBLÕ ;

!LEXICON V-AUX_ALĀ
!! 62 alā:alā
!+Aux: K ;

LEXICON V-AUX_ÄʼB
!! 62 äʼb:ä
+Neg: K ;

!LEXICON V-AUX_IʼZ
!! 62 iʼz:ä
!+Neg: K ;

LEXICON TV_SÄ
!! 63 sä:sä
+TV: K ;

LEXICON V-AUX_PIḐĪKS
!! 64 piḑīks:piḑī
+Aux: K ;

!! !!After transitive, intransitive, auxiliary and such tags have been added
!! 1

!! 2
LEXICON V-01_TǬʼDÕ
 V-01_VĪDÕ ;

!! 3
LEXICON V-01_VĪDÕ
!! This is mutual for 3: 
!! vīdõ:vī
:d   GER_sõ   ;
   INF_dõ	  ;
   SUP-STEM_mõ	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
 INDPRT_ž    ;
! Cond
: COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
:%^VowsRM PSSPRSPRC ;
:%^VowsRM	PSSPRFPRCSG    ;


LEXICON V-01_NǞʼDÕ !!= @CODE@ 4
!! This is mutual for ??: 
!! 4 nǟʼdõ:nǟʼ
:d   GER_sõ   ;
   INF_dõ	  ;
:%^StodRM   SUP-STEM_mõ	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
 INDPRT_ž    ;
! Cond
: COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
:%^VowsRM PSSPRSPRC ;
:%^VowsRM	PSSPRFPRCSG    ;
!!5

LEXICON V-01_SĪEDÕ !!= @CODE@ : 7 sīedõ:sīe
!! * Yaml: __siiedw__
:d   GER_sõ   ;
   INF_dõ	  ;
   SUP_mõ       ;
   INDPRSPL1    ;
   INDPRSPL2_tõ    ;
   INDPRSPL3    ;
   INDCONNEGPL1 ;
   INDCONNEGPL2 ;
   INDCONNEGPL3 ;
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
: INDPRT_i    ;
! Cond
 COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
 PSSPRSPRC ;
 PSSPRFPRCSG    ;

+Act+PrfPrc:n NMN_SĪEND	   ;
!! 8
LEXICON V-01_SǬDÕ
!! sǭdõ:s
:ǭd   GER_sõ   ;
:ǭ   INF_dõ	  ;
:ǭ   SUP-STEM_mõ	  ; 
:ǭ   INDPRSSG1-STEM    ;
:ǭ   INDCONNEGSG    ;
!! Prt
:a INDPRT_i    ;
! Cond
:ǭ COND ;
!! Imprt
:ǭ	IMPRTSG2    ;
:ǭ	IMPRTPL1	;
:ǭ	IMPRTPL2	;
:ǭ	IMPRTCONNEG ;

!! Jus
:ǭ JUS_gõ	;
!! Qvo
:ǭ QUO-STEM_ji	;

!! participles
:ǭ ACTPRSPRC ;
:ǭ ACTPRFPRC_nd ;
:ǭ PSSPRSPRC ;
:ǭ PSSPRFPRCSG    ;
!! 9
!! 9
LEXICON V-01_JŪODÕ
!! jūodõ:vī
:d   GER_sõ   ;
   INF_dõ	  ;
   SUP-STEM_mõ	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
 INDPRT_i    ;
! Cond
: COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
 PSSPRSPRC ;
 PSSPRFPRCSG    ;
!! 10

!!
LEXICON V-01_JEʼLLÕ
!! 13 jeʼllõ, 18 astõ, 19 võttõ, 24 vieʼddõ, 25 maksõ, 26 tappõ
:%^Stress2to1 GER_s     ;
:%^Stress2to1 INF_ZERO      ;
:%^StodRM%^ConsSh%^VowsRM%>ā SUP-STEM_m        ;
:%^StodRM%^ConsSh%^VowsRM%>ā   INDPRSSG1-STEM    ;
:%^ConsSh%^VowsRM   INDCONNEGSG    ;
:%^StodRM%^ConsSh%^VowsRM%>ī INDPRT_z ;
!+Ind+Sg+ConNeg:	K    ;

!! Cond
:%^StodRM%^ConsSh%^VowsRM%>ā COND ;
!! Imprt
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTSG2    ;
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTPL1        ;
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTPL2        ;
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTCONNEG ;
       IMPRTCONNEGSG2 ;
!! Jus
:%^StodRM%^ConsSh%^VowsRM%>ā JUS_g ;
!! Qvo
:%^StodRM%^ConsSh%^VowsRM%>ā QUO-STEM_ji    ;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_n ;
:%^ConsSh%^VowsRM PSSPRSPRC ;
:%^ConsSh%^VowsRM PSSPRFPRCSG    ;

!:%^Stress2to1%^VowsRM%^ConsRM	PSSPRFPRCSG    ;
!+Pss+PrfPrc:tõd			NMN_TAPTÕD ;

!!
LEXICON V-01_MÄNGÕ
!! 14 mängõ, 15 killõ, 16 pallõ, 17 loulõ, 20 laitõ, 21 täutõ, 22 pȯļtõ, 23 mȯistõ, 27 āndõ, 28 tīeudõ
 GER_s     ;
:%^Stress2to1 INF_ZERO      ;
:%^Stress2to1 SUP-STEM_m        ;
:   INDPRSSG1-STEM    ;
:%^ConsSh   INDCONNEGSG    ;
:%^VowsRM%>ī INDPRT_z ;
!+Ind+Sg+ConNeg:	K    ;

!+Cond+Sg3:õks	K    ;
 COND ;

!+Imprt+Sg2:  	K    ;
!+Imprt+ConNeg:	K    ;
!+Imprt+Pl2:õgid	K    ;
!+Jus+Sg3:õg		K    ;
!+Quo+Sg:iji		K    ;
!+Act+PrfPrc+Sg:õn	K    ;
:%^Stress2to1%^VowsRM%^ConsRM	PSSPRFPRCSG    ;

!!
!! 29
LEXICON	V-01_LUʼGGÕ !!= @CODE@ luʼggõ:luʼggõ 29
!! This is mutual for 29-36: 
!! luʼggõ, muʼdžõ, vakțõ, kittõ, riʼddõ, kutsõ, laskõ, akkõ
 GER_s   ;
 INF_ZERO	  ;
 SUP-STEM_m	  ; 
:%^StodRM%^ConsSh%^VowsRM%>ū INDPRSSG1-STEM    ;
:%^ConsSh%^VowsRM INDCONNEGSG    ;
!! Prt
:%^StodRM%^ConsSh%^VowsRM%>ī INDPRT_z    ;
! Cond
 COND ;
!! ImprtI
:%^ConsSh%^VowsRM 	IMPRTSG2    ;
:%^Stress2to1 IMPRTPL1	;
:%^Stress2to1 IMPRTPL2	;
:%^ConsSh%^VowsRM IMPRTCONNEG ;

+Imprt+Pl2:%^Stress2to1%>gid	K    ;
!! Jus
 JUS_g	;
!! Kvo
:%^VowsRM QUO-STEM_iji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_n ;
:%^ConsSh%^VowsRM PSSPRSPRC ;
:%^ConsSh%^VowsRM	PSSPRFPRCSG    ;

!!

LEXICON V-01_AIGÕ
!! This is mutual for 37-48
:%^Stress1to2 GER_s   ;
:%^Stress1to2 INF_ZERO	  ;
:%^Stress1to2 SUP_m	  ; 
 INDPRSSG1-STEM    ;
:%^Stress1to2 INDPRSPL1    ;
:%^Stress1to2 INDPRSPL2    ;
 INDPRSPL3    ;
:%^Stress1to2%^VowsRM	INDCONNEGSG    ;
:%^Stress1to2    INDCONNEGPL1 ;
:%^Stress1to2    INDCONNEGPL2 ;
:%^Stress1to2    INDCONNEGPL3 ;

!! Prt
:%^VowsRM%>i INDPRT_z    ;

!! participles
:%^Stress1to2 ACTPRSPRC ;
:%^Stress1to2 ACTPRFPRC_n ;
:%^Stress1to2%^VowsRM%^ConsRM PSSPRSPRC ;
:%^Stress1to2%^VowsRM%^ConsRM	PSSPRFPRCSG    ;

:%^Stress1to2 COND ;
:%^Stress1to2%^VowsRM IMPRTSG2 ;
:%^Stress1to2 IMPRTPL1 ;
:%^Stress1to2 IMPRTPL2 ;
 IMPRTCONNEG ;
:%^Stress1to2 JUS_g	;
:%^Stress1to2%^VowsRM QUO-STEM_iji	;		

!!
LEXICON V-01_KŪLÕ
! This is mutual for 49-50, 52-57
 GER_s   ;
 INF_ZERO         ;
 SUP-STEM_m       ;
: INDPRSSG1-STEM    ;
:%^ConsSh       INDCONNEGSG    ;
!! Prt
:%^VowsRM%>i INDPRT_z    ;
!! +Act+PrfPrc
+Act+PrfPrc:n    NMN_KŪOLÕN      ;

!!
LEXICON V-01_AʼILÕ
! This is mutual for 51
 GER_s   ;
!! Ger, Ger_Ine

 INF_ZERO	  ; !!≈ * __@CODE@__ aʼilõ
!! Inf

 SUP-STEM_m	  ; !!≈ * __@CODE@__ aʼilõ-
!! Sup, Sup+Abe, Sup+Ela, Sup+Deb, Sup+Ill, Sup+Ine, Sup+Tra
!! Ind+Prs+Pl1, Ind+Prs+Pl2, Ind+Prs+Pl3, Ind+ConNeg+Pl1
!! Ind+ConNeg+Pl2, Ind+ConNeg+Pl3

: INDPRSSG1-STEM    ;	!!≈ * __@CODE@__ aʼilõ-
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Ind+Prs+Sg3

:%^VowsRM	INDCONNEGSG    ; !!≈ * __@CODE@__ aʼil-
!! Ind+ConNeg+Sg

:%^VowsRM%>i INDPRT_z    ; !!≈ * __@CODE@__ aʼili-
!! Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Ind+Prt+Pl3

 COND		; !!≈ * __@CODE@__ aʼilõ-
!! Cond...
:%^VowsRM IMPRTSG2/IMPRTCONNEGSG2 ;
!! Imprt+Sg2, Imprt+ConNeg+Sg2

IMPRT-ELSE ;
!! Imprt+Pl1, Imprt+Pl2, Imprt+ConNeg

JUS_g ;
!! Jus+Sg3, Jus+Pl3

:%^VowsRM QUO-STEM_iji ;
!! Quo+Sg3, Quo+Pl3, Der/VN -mi

 ACTPRFPRC_n ; !!≈ * __@CODE@__ aʼilõ%>n
!! +Act+PrfPrc

:%^VowsRM PSSPRSPRC ;	!!≈ * __@CODE@__ aʼil-
:%^VowsRM PSSPRFPRCSG ; !!≈ * __@CODE@__ aʼil-


!!
!! 58, 
LEXICON V-01_UMBLÕ
!! This is mutual for 58-61: 
!! umblõ, mõtlõ, mǟʼdlõ, naʼgrõ
   GER_s   ;
   INF_ZERO	  ;
   SUP-STEM_m	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
:%^VowsRM%>i INDPRT_z    ;
! Cond
: COND ;
!! Imprt
:%^VowsMetath%^VowsRM	IMPRTSG2    ;
: IMPRTPL1	;
: IMPRTPL2	;
:%^VowsMetath%^VowsRM IMPRTCONNEG ;

!! Jus
 JUS_g	;
!! Qvo
:%^VowsRM QUO-STEM_iji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_n ;
:%^VowsRM PSSPRSPRC ;
:%^VowsRM	PSSPRFPRCSG    ;




!LEXICON VTag
!	Persons	;

!LEXICON Persons
!+1sg:en   #   ;
!+2sg:et   #   ;
!+3sg:e    #   ;
!+1pl:ien  #   ;
!+2pl:iet  #   ;
!+3pl:iet  #   ;
!+inf:0    #   ;
!!#:0   Nouns   ;

!! Nonfinites
LEXICON GER_s
+Ger:%>s   K ;
+Ger+Ine:%>s   K ;

LEXICON GER_sõ
+Ger:%>sõ   K ;
+Ger+Ine:%>sõ   K ;

LEXICON INF_ZERO
+Inf:	K	;

LEXICON INF_dõ
+Inf:%>dõ    K   ;

LEXICON INF_da
+Inf:%>da    K   ;

LEXICON SUP-STEM_m
   SUP_m	;
   INDPRSPL1    ;
   INDPRSPL2    ;
   INDPRSPL3    ;
   INDCONNEGPL1	;
   INDCONNEGPL2	;
   INDCONNEGPL3	;

LEXICON SUP_m
+Sup:%>m K   ;
+Sup+Abe:%>mõt K   ;
+Sup+Ela:%>mõst K   ;
+Sup+Deb:%>mõst K   ;
+Sup+Ill:%>m K   ;
+Sup+Ine:%>mõs K   ;
+Sup+Tra:%>mõks K   ;

LEXICON	SUP-STEM_mõ
   SUP_mõ	;
   INDPRSPL1    ;
   INDPRSPL2    ;
   INDPRSPL3    ;
   INDCONNEGPL1 ;
   INDCONNEGPL2 ;
   INDCONNEGPL3 ;

LEXICON SUP_mõ
+Sup:%>mõ K   ;
+Sup+Abe:%>mõt K   ;
+Sup+Ela:%>mõst K   ;
+Sup+Deb:%>mõst K   ;
+Sup+Ill:%>mõ K   ;
+Sup+Ine:%>mõs K   ;
+Sup+Tra:%>mõks K   ;

LEXICON ACTPRSPRC
+Act+PrsPrc+Sg:%>b	K    ;
+Act+PrsPrc+Pl:%>bõd	K    ;
+Act+PrsPrc+Der/A:%>b	A_SIDĀM    ;

LEXICON	ACTPRFPRC_n
+Use/NG+Act+PrfPrc:%>n	K    ;
+Act+PrfPrc+Sg:%>n	K    ;
+Act+PrfPrc+Pl:%>nd	K    ;
+Act+PrfPrc:%>n  NMN_KŪOLÕN      ; 

LEXICON ACTPRFPRC_nd
+Act+PrfPrc+Sg:%>nd      K    ;
+Act+PrfPrc+Pl:%>nd     K    ; !! Are the singular and plural homonyms?



LEXICON PSSPRSPRC
+Pss+PrsPrc+Sg:%>dõb        K    ;
+Pss+PrsPrc+Pl:%>dõbõd      K    ;

LEXICON PSSPRFPRCSG
+Use/NG+Pss+PrfPrc:%>dõd    K    ;
+Pss+PrfPrc+Sg:%>dõd    K    ;
+Pss+PrfPrc:%>dõd	   NMN_TAPTÕD ; !! 2014-08-21

!! Finites

!LEXICON INDPRS
!! Indicative present
!  INDPRSSG1-STEM	;
!  INDPRSPL1	;
!  INDPRSPL2	;
!  INDPRSPL3	;

LEXICON INDPRS_tõ
!! Indicative present
  INDPRSSG1-STEM	;
  INDPRSPL1	;
  INDPRSPL2_tõ	;
  INDPRSPL3	;

LEXICON INDPRT_i
!! Indicative preterite in i
 INDPRTSG1_i    ;
 INDPRTSG2_i    ;
 INDPRTSG3_i    ;
 INDPRTPL1_i    ;
 INDPRTPL2_i    ;
 INDPRTPL3_i    ;

LEXICON INDPRT_z
!! Indicative preterite in z
 INDPRTSG1_z    ;
 INDPRTSG2_z    ;
 INDPRTSG3_z    ;
 INDPRTPL1_z    ;
 INDPRTPL2_z    ;
 INDPRTPL3_z    ;

LEXICON INDPRT_ž
!! Indicative preterite in ž
 INDPRTSG1_ž    ;
 INDPRTSG2_ž    ;
 INDPRTSG3_ž    ;
 INDPRTPL1_ž    ;
 INDPRTPL2_ž    ;
 INDPRTPL3_ž    ;

LEXICON INDPRTSG3-STEM_tõ
 INDPRTSG3    ;
 INDPRTPL2_tõ    ;
 INDPRTPL3_tõ    ;


LEXICON COND
!! Conditional present
  CONDSG1	;
  CONDSG2	;
  CONDSG3	;
  CONDPL1	;
  CONDPL2	;
  CONDPL3	;


 ! Indicative present
LEXICON INDPRSSG1-STEM
	INDPRSSG1	;
	INDPRSSG2	;
	INDPRSSG3	;

LEXICON INDPRSSG1
 +Ind+Prs+Sg1:%>b    K	;

LEXICON INDPRSSG2
 +Ind+Prs+Sg2:%>d    K	;

LEXICON INDPRSSG3
 +Ind+Prs+Sg3:%>b    K	;

LEXICON INDPRSPL1
 +Ind+Prs+Pl1:%>m    K	;

LEXICON INDPRSPL2
 +Ind+Prs+Pl2:%>t    K	;

LEXICON INDPRSPL2_tõ
 +Ind+Prs+Pl2:%>tõ    K	;

LEXICON INDPRSPL3
 +Ind+Prs+Pl3:%>bõd    K	;

 ! Indicative preterite
LEXICON INDPRTSG1_i
 +Ind+Prt+Sg1:%>i    K	;

LEXICON INDPRTSG1_z
 +Ind+Prt+Sg1:%>z    K	;

LEXICON INDPRTSG1_ž
 +Ind+Prt+Sg1:%>ž    K	;

LEXICON INDPRTSG2_i
+Ind+Prt+Sg2:%>id    K ;

LEXICON INDPRTSG2_z
+Ind+Prt+Sg2:%>zt    K	;

LEXICON INDPRTSG2_ž
 +Ind+Prt+Sg2:%>žt    K	;

LEXICON INDPRTSG3
 +Ind+Prt+Sg3:    K	;

LEXICON INDPRTSG3_i
 +Ind+Prt+Sg3:%>i    K	;

LEXICON INDPRTSG3_z
 +Ind+Prt+Sg3:%>z    K	;

LEXICON INDPRTSG3_ž
 +Ind+Prt+Sg3:%>ž    K	;

LEXICON INDPRTPL1_i
 +Ind+Prt+Pl1:%>imõ    K	;

LEXICON INDPRTPL1_z
 +Ind+Prt+Pl1:%>zmõ    K	;

LEXICON INDPRTPL1_ž
 +Ind+Prt+Pl1:%>žmõ    K	;

LEXICON INDPRTPL2_i
 +Ind+Prt+Pl2:%>itõ    K	;

LEXICON INDPRTPL2_z
 +Ind+Prt+Pl2:%>zt    K	;
 +Ind+Prt+Pl2:%>ztõ    K	;

LEXICON INDPRTPL2_ž
 +Ind+Prt+Pl2:%>žt    K	;
 +Ind+Prt+Pl2:%>žtõ    K	;

LEXICON INDPRTPL2_tõ
 +Ind+Prt+Pl2:%>tõ    K	;

LEXICON INDPRTPL2_tā
 +Ind+Prt+Pl2:%>tā    K	;

LEXICON INDPRTPL3_i
 +Ind+Prt+Pl3:%>itõ    K	;

LEXICON INDPRTPL3_z
 +Ind+Prt+Pl3:%>zt    K	;
 +Ind+Prt+Pl3:%>ztõ    K	;

LEXICON INDPRTPL3_ž
 +Ind+Prt+Pl3:%>žt    K	;
 +Ind+Prt+Pl3:%>žtõ    K	;

LEXICON INDPRTPL3_tõ
 +Ind+Prt+Pl3:%>tõ    K	;

LEXICON INDPRTPL3_tā
 +Ind+Prt+Pl3:%>tā    K	;


LEXICON INDCONNEGSG
+Ind+ConNeg+Sg:    K	;

LEXICON INDCONNEGPL1
 +Ind+ConNeg+Pl1:%>m    K	;

LEXICON INDCONNEGPL2
 +Ind+ConNeg+Pl2:%>t    K	;

LEXICON INDCONNEGPL3
 +Ind+ConNeg+Pl3:%>t    K	;

!! Conditional
LEXICON CONDSG1
 +Cond+Sg1:%>ks    K	;

LEXICON CONDSG2
 +Cond+Sg2:%>kst    K	;

LEXICON CONDSG3
 +Cond+Sg3:%>ks    K	;

LEXICON CONDPL1
 +Cond+Pl1:%>ksmõ    K	;

LEXICON CONDPL2
 +Cond+Pl2:%>kstõ    K	;

LEXICON CONDPL3
 +Cond+Pl3:%>kstõ    K	;

!! Imperative
LEXICON IMPRT-ELSE
IMPRTPL1 ;
IMPRTPL2 ;
IMPRTCONNEG ;

LEXICON IMPRTSG2/IMPRTCONNEGSG2
IMPRTSG2 ;
IMPRTCONNEGSG2 ;

LEXICON IMPRTSG2
+Imprt+Sg2: K ;

LEXICON IMPRTPL1
+Imprt+Pl1:%>gõm K ;

LEXICON IMPRTPL2
+Imprt+Pl2:%>gid K ;

LEXICON IMPRTCONNEG
+Imprt+ConNeg:      K    ;

LEXICON IMPRTCONNEGSG2
+Imprt+ConNeg+Sg2:      K    ;

!! Jussative
LEXICON JUS_g
+Jus+Sg3:%>g             K    ;
+Jus+Pl3:%>gõd             K    ;

LEXICON JUS_gõ
+Jus+Sg3:%>gõ             K    ;
+Jus+Pl3:%>gõd             K    ;

!! Quotative
LEXICON QUO-STEM_ji
	QUO_ji	;
+Der/VN:mi		N_AʼBLI	;

LEXICON QUO-STEM_iji
	QUO_iji	;
+Der/VN:imi		N_AʼBLI	;

LEXICON QUO_ji
+Quo+Sg3:%>ji           K    ;
+Quo+Pl3:%>jid           K    ;

LEXICON QUO_iji
+Quo+Sg3:%>iji           K    ;
+Quo+Pl3:%>ijid           K    ;

! vim: set ft=xfst-lexc:

