!! # Livonian Verb inflection
! ---------------
!! This file documents the verb inflection of Livonian.


!! ## Verb stem classes
LEXICON V_
!! **LEXICON @LEXNAME@ = CONJUGATION TYPE MISSING
: # ;

LEXICON TV_
!!  **LEXICON @LEXNAME@ = CONJUGATION TYPE MISSING
+TV: K ;

LEXICON V-AUX_VȰLDA
!!  **LEXICON @LEXNAME@ =  10 vȱlda:ZERO
+Ind+Prs+Sg1:uʼm K ;
+Ind+Prs+Sg2:ūod K ;
+Ind+Prs+Sg3:uʼm K ;
+Ind+Prs+Pl1:ūomõ K ;
+Ind+Prs+Pl1:ūom K ;
+Ind+Prs+Pl2:ūotõ K ;
+Ind+Prs+Pl3:ātõ K ;
+Ind+Prs+Pl3:attõ K ;
+Ind+Prs+Pl3:āt K ;
+Ind+Prs+Pl3:at K ;
+Ind+Conneg+Sg:ūo K ;
+Ind+Conneg+Pl1:ūomõ K ;
+Ind+Conneg+Pl2:ūotõ K ;
+Ind+Conneg+Pl3:ūotõ K ;
+Ind+Prt+Sg1:vȯʼļ K ;
+Ind+Prt+Sg2:vȯʼļd K ;
+Ind+Prt+Sg3:vȯʼļ K ;
+Ind+Prt+Pl1:vȯʼļmõ K ;
+Ind+Prt+Pl2:vȯʼļtõ K ;
+Ind+Prt+Pl2:vȯʼļțõ K ; ! variant 2023_08_9
+Ind+Prt+Pl3:vȯʼļtõ K ;
+Ind+Prt+Pl3:vȯʼļțõ K ; ! variant 2023_08_9
+Cnd+Conneg:vȯlks K ;
+Cnd+Sg1:vȯlks K ;
+Cnd+Sg2:vȯlkst K ;
+Cnd+Sg3:vȯlks K ;
+Cnd+Pl1:vȯlksmõ K ;
+Cnd+Pl2:vȯlkstõ K ;
+Cnd+Pl3:vȯlkstõ K ;
+Jus+Sg:vȯlkõ K ;
+Jus+Conneg+Sg:vȯlkõ K ;
+Jus+Pl:vȯlkõ K ;
+Jus+Conneg+Pl:vȯlkõ K ;
+Quo+Sg:vȯʼļļi K ;
+Quo+Pl:vȯʼļļi K ;
+Quo+Pl:vȯʼļļid K ;
+Ind+PrcPrs+Sg:vȯnd K ;
+Ind+PrcPrs+Pl:vȯnnõd K ;
+Imprt+Sg2:vȯʼl K ;
+Imprt+Pl2:vȯʼlgid K ;


LEXICON IV_VȰLDA
!!  **LEXICON @LEXNAME@ = 10 vȱlda: goes to **K**
+IV: K ;

LEXICON V-AUX_LǞʼDÕ
!!  **LEXICON @LEXNAME@ = 1 lǟʼdõ:lǟʼ
: K ;

LEXICON IV_LǞʼDÕ
!!  **LEXICON @LEXNAME@ = 1 lǟʼdõ:lǟʼ
+IV: K ;

LEXICON TV_TǬʼDÕ
!!  **LEXICON @LEXNAME@ = 2 tǭʼdõ:tǭʼ
+TV: V-01_TǬʼDÕ ;

LEXICON V-AUX_VĪDÕ
!!  **LEXICON @LEXNAME@ = 3 vīdõ:vī
: V-01_VĪDÕ ;

LEXICON IV_VĪDÕ
!!  **LEXICON @LEXNAME@ = 3 vīdõ:vī
+IV: V-01_VĪDÕ ;

LEXICON TV_VĪDÕ
!!  **LEXICON @LEXNAME@ = 3 vīdõ:vī
+TV: V-01_VĪDÕ ;

!LEXICON IV_NǞʼDÕ
! 4 nǟʼdõ:nǟʼ
!+IV: V-01_NǞʼDÕ ;

LEXICON TV_NǞʼDÕ
!!  **LEXICON @LEXNAME@ = 4 nǟʼdõ:nǟʼ
!! * Yaml: **naeaeqdw**
+TV: V-01_NǞʼDÕ ;

LEXICON IV_KǞʼDÕ
!!  **LEXICON @LEXNAME@ = 5 kǟʼdõ:kǟʼ
+IV: K ;

LEXICON TV_TĪʼEDÕ
!!  **LEXICON @LEXNAME@ = 6 tīʼedõ:tīʼe
+TV: K ;


LEXICON V-AUX_SĪEDÕ
!!  **LEXICON @LEXNAME@ = 7 sīedõ:sīe
: V-01_SĪEDÕ ;

LEXICON IV_SĪEDÕ
!!  **LEXICON @LEXNAME@ = 7 sīedõ:sīe
+IV: V-01_SĪEDÕ ;

LEXICON TV_SĪEDÕ
!!  **LEXICON @LEXNAME@ = 7 sīedõ:sīe
+TV: V-01_SĪEDÕ ;

LEXICON IV_SǬDÕ
!!  **LEXICON @LEXNAME@ = 8 sǭdõ:s
+IV: V-01_SǬDÕ ;

LEXICON TV_SǬDÕ
!!  **LEXICON @LEXNAME@ = 8 sǭdõ:s
+TV: V-01_SǬDÕ ;

LEXICON V-AUX_SǬDÕ
!!  **LEXICON @LEXNAME@ = 8 sǭdõ:s
: V-01_SǬDÕ ;

!LEXICON IV_JŪODÕ
! 9 jūodõ:jūo
!+IV: V-01_JŪODÕ ;

LEXICON TV_JŪODÕ
!!  **LEXICON @LEXNAME@ = 9 jūodõ:jūo
+TV: V-01_JŪODÕ ;

LEXICON IV_TŪLDA
!!  **LEXICON @LEXNAME@ = 11 tūlda:
+IV: K ;

LEXICON V-AUX_PĀNDA
!!  **LEXICON @LEXNAME@ = 12 pānda:
: K ;

LEXICON IV_PĀNDA
!!  **LEXICON @LEXNAME@ = 12 pānda:
+IV: K ;

LEXICON TV_PĀNDA
!!  **LEXICON @LEXNAME@ = 12 pānda:
+TV: K ;

LEXICON IV_JEʼLLÕ
!!  **LEXICON @LEXNAME@ = 13 jeʼllõ:jeʼlā
+IV: V-01_JEʼLLÕ ;

LEXICON TV_JEʼLLÕ
!! **LEXICON @LEXNAME@ =  13 jeʼllõ:jeʼllõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_ASTÕ
!!  **LEXICON @LEXNAME@ = 18 astõ:astõ
+IV: V-01_JEʼLLÕ ;

LEXICON TV_ASTÕ
!! **LEXICON @LEXNAME@ =  18 astõ:astõ
+TV: V-01_JEʼLLÕ ;

LEXICON TV_VÕTTÕ
!!  **LEXICON @LEXNAME@ = 19 võttõ:võttõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_VIEʼDDÕ
!!  **LEXICON @LEXNAME@ = 24 vieʼddõ:vieʼddõ
+IV: V-01_JEʼLLÕ ;

LEXICON TV_VIEʼDDÕ
!!  **LEXICON @LEXNAME@ = 24 vieʼddõ:vieʼddõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_MAKSÕ
!!  **LEXICON @LEXNAME@ = 25 maksõ:maksõ
+IV: V-01_JEʼLLÕ ;

LEXICON TV_MAKSÕ
!!  **LEXICON @LEXNAME@ = 25 maksõ:maksõ
+TV: V-01_JEʼLLÕ ;

LEXICON TV_TAPPÕ
!!  **LEXICON @LEXNAME@ = 26 tappõ:tappõ
+TV: V-01_JEʼLLÕ ;

LEXICON IV_MÄNGÕ
!!  **LEXICON @LEXNAME@ = 14 mängõ:mǟnga
+IV: V-01_MÄNGÕ ;

LEXICON TV_KILLÕ
!!  **LEXICON @LEXNAME@ = 15 killõ:kīla
+TV: V-01_MÄNGÕ ;

LEXICON TV_PALLÕ
!!  **LEXICON @LEXNAME@ = 16 pallõ:pǭla
+TV: V-01_MÄNGÕ ;

LEXICON TV_LOULÕ
!!  **LEXICON @LEXNAME@ = 17 loulõ:lōla
+TV: V-01_MÄNGÕ ;

LEXICON IV_LAITÕ
!!  **LEXICON @LEXNAME@ = 20 laittõ:lāita
+IV: V-01_MÄNGÕ ;

LEXICON TV_LAITÕ
!!  **LEXICON @LEXNAME@ = 20 laittõ:lāita
+TV: V-01_MÄNGÕ ;

LEXICON IV_TÄUTÕ
!!  **LEXICON @LEXNAME@ = 21 täutõ:tǟta
+IV: V-01_MÄNGÕ ;

LEXICON TV_TÄUTÕ
!!  **LEXICON @LEXNAME@ = 21 täutõ:tǟta
+TV: V-01_MÄNGÕ ;

!LEXICON IV_PȮĻTÕ
! 22 pȯļtõ:pūoļta
!+IV: V-01_MÄNGÕ ;

LEXICON TV_PȮĻTÕ
!!  **LEXICON @LEXNAME@ = 22 pȯļtõ:pūoļta
+TV: V-01_MÄNGÕ ;

!LEXICON IV_MȮISTÕ
! 23 mȯistõ:mūošta
!+IV: V-01_MÄNGÕ ;

LEXICON TV_MȮISTÕ
!!  **LEXICON @LEXNAME@ = 23 mȯistõ:mūošta
+TV: V-01_MÄNGÕ ;

LEXICON IV_ANDÕ
!!  **LEXICON @LEXNAME@ = 27 andõ:ānda
+IV: V-01_MÄNGÕ ;

LEXICON TV_ANDÕ
!!  **LEXICON @LEXNAME@ = 27 andõ:ānda
+TV: V-01_MÄNGÕ ;

LEXICON IV_TIEUDÕ
!!  **LEXICON @LEXNAME@ = 28 tieudõ:tīeda
+IV: V-01_MÄNGÕ ;

LEXICON TV_TIEUDÕ
!!  **LEXICON @LEXNAME@ = 28 tieudõ:tīeda
+TV: V-01_MÄNGÕ ;

!! 29-48 follow same pattern

LEXICON IV_LUʼGGÕ
!!  **LEXICON @LEXNAME@ = luʼggõ:luʼggõ 29
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_LUʼGGÕ
!!  **LEXICON @LEXNAME@ = luʼggõ:lugū 29
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_MUʼDŽÕ
!!  **LEXICON @LEXNAME@ = muʼdžõ:mudžū 30
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_MUʼDŽÕ
!!  **LEXICON @LEXNAME@ = muʼdžõ:mudžū 30
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_VAKȚÕ
!!  **LEXICON @LEXNAME@ = vakțõ:vakțū 31
+IV:     V-01_LUʼGGÕ      ;

LEXICON TV_VAKȚÕ
!! **LEXICON @LEXNAME@ =  vakțõ:vakțū 31
+TV:	V-01_LUʼGGÕ	;

LEXICON IV_KITTÕ
!!  **LEXICON @LEXNAME@ = kittõ:kitū 32
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_KITTÕ
!!  **LEXICON @LEXNAME@ = kittõ:kitū 32
+TV:     V-01_LUʼGGÕ ;

LEXICON V-AUX_RIʼDDÕ
!!  **LEXICON @LEXNAME@ = riʼddõ:ridū 33
:     V-01_LUʼGGÕ ;

LEXICON IV_RIʼDDÕ
!!  **LEXICON @LEXNAME@ = riʼddõ:ridū 33
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_RIʼDDÕ
!!  **LEXICON @LEXNAME@ = riʼddõ:ridū 33
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_KUTSÕ
!!  **LEXICON @LEXNAME@ = kutsõ:kutsū 34
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_KUTSÕ
!!  **LEXICON @LEXNAME@ = kutsõ:kutsū 34
+TV:     V-01_LUʼGGÕ ;

LEXICON V-AUX_LASKÕ
!!  **LEXICON @LEXNAME@ = laskõ:laskū 35
:     V-01_LUʼGGÕ ;

LEXICON IV_LASKÕ
!!  **LEXICON @LEXNAME@ = laskõ:laskū 35
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_LASKÕ
!!  **LEXICON @LEXNAME@ = laskõ:laskū 35
+TV:     V-01_LUʼGGÕ ;

LEXICON IV_AKKÕ
!!  **LEXICON @LEXNAME@ = akkõ:akū 36
!! Should ss be s and šš be š? 2013-02-19
+IV:     V-01_LUʼGGÕ ;

LEXICON TV_AKKÕ
!!  **LEXICON @LEXNAME@ = akkõ:akū 36
+TV:     V-01_LUʼGGÕ ;


LEXICON V-AUX_AIGÕ
!!  **LEXICON @LEXNAME@ = aigõ:āigõ 37
:	V-01_AIGÕ ;

LEXICON IV_AIGÕ
!!  **LEXICON @LEXNAME@ = aigõ:āigõ 37
+IV:  V-01_AIGÕ ;

LEXICON TV_AIGÕ
!!  **LEXICON @LEXNAME@ = aigõ:āigõ 37
+TV:  V-01_AIGÕ ;

LEXICON TV_KUOŖŖÕ
!!  **LEXICON @LEXNAME@ = kuoŗŗõ:kūoŗõ 38
+TV:     V-01_AIGÕ       ;

LEXICON TV_VANNÕ
!!  **LEXICON @LEXNAME@ = vannõ:vǭnõ 39
+TV:     V-01_AIGÕ       ;


LEXICON IV_PȮĻĻÕ
!!  **LEXICON @LEXNAME@ = pȯļļõ:pūoļõ 40
+IV:     V-01_AIGÕ       ;


LEXICON IV_PȮIMÕ
!!  **LEXICON @LEXNAME@ = pȯimõ:pūoimõ 41
+IV:     V-01_AIGÕ       ;

LEXICON TV_PȮIMÕ
!!  **LEXICON @LEXNAME@ = pȯimõ:pūoimõ 41
+TV:     V-01_AIGÕ       ;

LEXICON IV_OUŖÕ
!!  **LEXICON @LEXNAME@ = ouŗõ:ōŗõ 42
+IV:     V-01_AIGÕ       ;

LEXICON IV_KEIJÕ
!!  **LEXICON @LEXNAME@ = keijõ:kējõ 43
+IV:     V-01_AIGÕ       ;

LEXICON TV_KEIJÕ
!!  **LEXICON @LEXNAME@ = keijõ:kējõ 43
+TV:     V-01_AIGÕ       ;


LEXICON IV_AŖŠTÕ
!!  **LEXICON @LEXNAME@ = aŗštõ:āŗštõ 44
+IV:     V-01_AIGÕ       ;

LEXICON TV_AŖŠTÕ
!!  **LEXICON @LEXNAME@ = aŗštõ:āŗštõ 44
+TV:	V-01_AIGÕ	;

LEXICON TV_PȮRTÕ
!!  **LEXICON @LEXNAME@ = pȯrtõ:pūortõ 45
+TV:     V-01_AIGÕ       ;

LEXICON TV_OUTÕ
!!  **LEXICON @LEXNAME@ = outõ:ōtõ 46
+TV:     V-01_AIGÕ       ;

LEXICON V-AUX_TUNDÕ
!!  **LEXICON @LEXNAME@ = tundõ:tūndõ 47
:    V-01_AIGÕ       ;

LEXICON IV_TUNDÕ
!!  **LEXICON @LEXNAME@ = tundõ:tūndõ 47
+IV:	V-01_AIGÕ	;

LEXICON TV_TUNDÕ
!!  **LEXICON @LEXNAME@ = tundõ:tūndõ 47
+TV:     V-01_AIGÕ       ;

LEXICON TV_OUDÕ
!!  **LEXICON @LEXNAME@ = oudõ:ōdõ 48
+TV:	V-01_AIGÕ	;


LEXICON IV_KŪLÕ
!!  **LEXICON @LEXNAME@ = kūlõ:kūlõ 49
+IV: V-01_KŪLÕ ;

LEXICON TV_KŪLÕ
!!  **LEXICON @LEXNAME@ = kūlõ:kūlõ 49
+TV: V-01_KŪLÕ ;

LEXICON IV_ARRÕ
!!  **LEXICON @LEXNAME@ = arrõ:arrõ 50
+IV: V-01_KŪLÕ ;

LEXICON TV_ARRÕ
!!  **LEXICON @LEXNAME@ = arrõ:arrõ 50
+TV: V-01_KŪLÕ ;

LEXICON IV_AʼILÕ
!!  **LEXICON @LEXNAME@ = aʼilõ:aʼilõ 51
+IV: V-01_AʼILÕ ;

LEXICON TV_AʼILÕ
!!  **LEXICON @LEXNAME@ = aʼilõ:aʼilõ 51
+TV: V-01_AʼILÕ ;

LEXICON TV_SVAʼRRÕ
!!  **LEXICON @LEXNAME@ = svaʼrrõ:svaʼrrõ 52
+TV: V-01_KŪLÕ ;

LEXICON V-AUX_KĪTÕ
!!  **LEXICON @LEXNAME@ = kītõ:kīt 53
: V-01_KŪLÕ ;

LEXICON IV_KĪTÕ
!!  **LEXICON @LEXNAME@ = kītõ:kīt 53
!! ~701
+IV: V-01_KŪLÕ ;

LEXICON TV_KĪTÕ
!!  **LEXICON @LEXNAME@ = kītõ:kīt 53
+TV: V-01_KŪLÕ ;


LEXICON IV_ÄʼBȚÕ
!!  **LEXICON @LEXNAME@ = äʼbțõ:äʼbț 54
+IV: V-01_KŪLÕ ;

LEXICON TV_ÄʼBȚÕ
!!  **LEXICON @LEXNAME@ = äʼbțõ:äʼbț 54
+TV: V-01_KŪLÕ ;

!LEXICON V_KŪLDÕ
! kūldõ:kūld 55
! V-01_KŪLÕ ;

LEXICON V-AUX_KŪLDÕ
!!  **LEXICON @LEXNAME@ = kūldõ:kūld 55
: V-01_KŪLÕ ;

LEXICON IV_KŪLDÕ
!!  **LEXICON @LEXNAME@ = kūldõ:kūld 55
+IV: V-01_KŪLÕ ;

LEXICON TV_KŪLDÕ
!!  **LEXICON @LEXNAME@ = kūldõ:kūld 55
+TV: V-01_KŪLÕ ;

LEXICON TV_KĪSKÕ
!!  **LEXICON @LEXNAME@ = kīskõ:kīsk 56
+TV: V-01_KŪLÕ ;

LEXICON V-AUX_ĪʼEDÕ
!!  **LEXICON @LEXNAME@ = īʼedõ:īed 57
: V-01_KŪLÕ ;

LEXICON IV_ĪʼEDÕ
!!  **LEXICON @LEXNAME@ = īʼedõ:īed 57
+IV: V-01_KŪLÕ ;

LEXICON TV_ĪʼEDÕ
!!  **LEXICON @LEXNAME@ = īʼedõ:īed 57
+TV: V-01_KŪLÕ ;

!LEXICON V_UMBLÕ
! umblõ:umblõ 58
! V-01_UMBLÕ ;

LEXICON IV_UMBLÕ
!!  **LEXICON @LEXNAME@ = umblõ: 58
+IV: V-01_UMBLÕ ;

LEXICON TV_UMBLÕ
!!  **LEXICON @LEXNAME@ = umblõ: 58
+TV: V-01_UMBLÕ ;

LEXICON V-AUX_MÕTLÕ
!!  **LEXICON @LEXNAME@ = mõtlõ: 59
: V-01_UMBLÕ ;

LEXICON IV_MÕTLÕ
!!  **LEXICON @LEXNAME@ = mõtlõ: 59
+IV: V-01_UMBLÕ ;

LEXICON TV_MÕTLÕ
!!  **LEXICON @LEXNAME@ = mõtlõ: 59
+TV: V-01_UMBLÕ ;

LEXICON IV_MǞʼDLÕ
!!  **LEXICON @LEXNAME@ = mǟʼdlõ: 60
+IV: V-01_UMBLÕ ;

LEXICON TV_MǞʼDLÕ
!!  **LEXICON @LEXNAME@ = mǟʼdlõ: 60
+TV: V-01_UMBLÕ ;

LEXICON IV_NAʼGRÕ
!!  **LEXICON @LEXNAME@ = naʼgrõ: 60
+IV: V-01_UMBLÕ ;

LEXICON TV_NAʼGRÕ
!!  **LEXICON @LEXNAME@ = naʼgrõ: 60
+TV: V-01_UMBLÕ ;

!LEXICON V-AUX_ALĀ
! 62 alā:alā
!: K ;

LEXICON V-AUX_ÄʼB
!!  **LEXICON @LEXNAME@ = 62 äʼb:ä
+Neg: K ;

!LEXICON V-AUX_IʼZ
! 62 iʼz:ä
!+Neg: K ;

LEXICON TV_SÄ
!!  **LEXICON @LEXNAME@ = 63 sä:sä
+TV: K ;

LEXICON V-AUX_PIḐĪKS
!!  **LEXICON @LEXNAME@ = 64 piḑīks:piḑī
: K ;

!! ## After transitive, intransitive, auxiliary and such tags have been added
!! 1

!! 2
LEXICON V-01_TǬʼDÕ
 V-01_VĪDÕ ;

!! 3
LEXICON V-01_VĪDÕ
!!  **LEXICON @LEXNAME@ = This is mutual for 3: 
!! vīdõ:vī
:d   GER_sõ   ;
   INF_dõ	  ;
   SUP-STEM_mõ	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
 INDPRT_ž    ;
! Cond
: COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
:%^VowsRM PSSPRSPRC ;
:%^VowsRM	PSSPRFPRCSG    ;


LEXICON V-01_NǞʼDÕ 
!!  **LEXICON @LEXNAME@ = This is mutual for ??: 
!! 4 nǟʼdõ:nǟʼ
:d   GER_sõ   ;
   INF_dõ	  ;
:%^StodRM   SUP-STEM_mõ	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
 INDPRT_ž    ;
! Cond
: COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
:%^VowsRM PSSPRSPRC ;
:%^VowsRM	PSSPRFPRCSG    ;
!!5

LEXICON V-01_SĪEDÕ 
!!  **LEXICON @LEXNAME@ = : 7 sīedõ:sīe
!! * Yaml: **siiedw**
:d   GER_sõ   ;
   INF_dõ	  ;
   SUP_mõ       ;
   INDPRSPL1    ;
   INDPRSPL2_tõ    ;
   INDPRSPL3    ;
   INDCONNEGPL1 ;
   INDCONNEGPL2 ;
   INDCONNEGPL3 ;
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
: INDPRT_i    ;
! Cond
 COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
 PSSPRSPRC ;
 PSSPRFPRCSG    ;

+Act+PrfPrc:n NMN_SĪEND	   ;
!! 8
LEXICON V-01_SǬDÕ
!! sǭdõ:s
:ǭd   GER_sõ   ;
:ǭ   INF_dõ	  ;
:ǭ   SUP-STEM_mõ	  ; 
:ǭ   INDPRSSG1-STEM    ;
:ǭ   INDCONNEGSG    ;
!! Prt
:a INDPRT_i    ;
! Cond
:ǭ COND ;
!! Imprt
:ǭ	IMPRTSG2    ;
:ǭ	IMPRTPL1	;
:ǭ	IMPRTPL2	;
:ǭ	IMPRTCONNEG ;

!! Jus
:ǭ JUS_gõ	;
!! Qvo
:ǭ QUO-STEM_ji	;

!! participles
:ǭ ACTPRSPRC ;
:ǭ ACTPRFPRC_nd ;
:ǭ PSSPRSPRC ;
:ǭ PSSPRFPRCSG    ;
!! 9
!! 9
LEXICON V-01_JŪODÕ
!! jūodõ:vī
:d   GER_sõ   ;
   INF_dõ	  ;
   SUP-STEM_mõ	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
 INDPRT_i    ;
! Cond
: COND ;
!! Imprt
	IMPRTSG2    ;
	IMPRTPL1	;
	IMPRTPL2	;
	IMPRTCONNEG ;

!! Jus
 JUS_gõ	;
!! Qvo
 QUO-STEM_ji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_nd ;
 PSSPRSPRC ;
 PSSPRFPRCSG    ;
!! 10

!!
LEXICON V-01_JEʼLLÕ
!!  **LEXICON @LEXNAME@ = 13 jeʼllõ, 18 astõ, 19 võttõ, 24 vieʼddõ, 25 maksõ, 26 tappõ
:%^Stress2to1 GER_s     ;
:%^Stress2to1 INF_ZERO      ;
:%^StodRM%^ConsSh%^VowsRM%>ā SUP-STEM_m        ;
:%^StodRM%^ConsSh%^VowsRM%>ā   INDPRSSG1-STEM    ;
:%^ConsSh%^VowsRM   INDCONNEGSG    ;
:%^StodRM%^ConsSh%^VowsRM%>ī INDPRT_z ;
!+Ind+Sg+ConNeg:	K    ;

!! Cond
:%^StodRM%^ConsSh%^VowsRM%>ā COND ;
!! Imprt
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTSG2    ;
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTPL1        ;
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTPL2        ;
:%^StodRM%^ConsSh%^VowsRM%>ā        IMPRTCONNEG ;
       IMPRTCONNEGSG2 ;
!! Jus
:%^StodRM%^ConsSh%^VowsRM%>ā JUS_g ;
!! Qvo
:%^StodRM%^ConsSh%^VowsRM%>ā QUO-STEM_ji    ;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_n ;
:%^ConsSh%^VowsRM PSSPRSPRC ;
:%^ConsSh%^VowsRM PSSPRFPRCSG    ;

!:%^Stress2to1%^VowsRM%^ConsRM	PSSPRFPRCSG    ;
!+Pss+PrfPrc:tõd			NMN_TAPTÕD ;

!!
LEXICON V-01_MÄNGÕ
!!  **LEXICON @LEXNAME@ = 14 mängõ, 15 killõ, 16 pallõ, 17 loulõ, 20 laitõ, 21 täutõ, 22 pȯļtõ, 23 mȯistõ, 27 āndõ, 28 tīeudõ
 GER_s     ;
:%^Stress2to1 INF_ZERO      ;
:%^Stress2to1 SUP-STEM_m        ;
:   INDPRSSG1-STEM    ;
:%^ConsSh   INDCONNEGSG    ;
:%^VowsRM%>ī INDPRT_z ;
!+Ind+Sg+ConNeg:	K    ;

!+Cond+Sg3:õks	K    ;
 COND ;

!+Imprt+Sg2:  	K    ;
!+Imprt+ConNeg:	K    ;
!+Imprt+Pl2:õgid	K    ;
!+Jus+Sg3:õg		K    ;
!+Quo+Sg:iji		K    ;
!+Act+PrfPrc+Sg:õn	K    ;
:%^Stress2to1%^VowsRM%^ConsRM	PSSPRFPRCSG    ;

!!
!! 29
LEXICON	V-01_LUʼGGÕ !!= @CODE@ luʼggõ:luʼggõ 29
!! This is mutual for 29-36: 
!! luʼggõ, muʼdžõ, vakțõ, kittõ, riʼddõ, kutsõ, laskõ, akkõ
 GER_s   ;
 INF_ZERO	  ;
 SUP-STEM_m	  ; 
:%^StodRM%^ConsSh%^VowsRM%>ū INDPRSSG1-STEM    ;
:%^ConsSh%^VowsRM INDCONNEGSG    ;
!! Prt
:%^StodRM%^ConsSh%^VowsRM%>ī INDPRT_z    ;
! Cond
 COND ;
!! ImprtI
:%^ConsSh%^VowsRM 	IMPRTSG2    ;
:%^Stress2to1 IMPRTPL1	;
:%^Stress2to1 IMPRTPL2	;
:%^ConsSh%^VowsRM IMPRTCONNEG ;

+Imprt+Pl2:%^Stress2to1%>gid	K    ;
!! Jus
 JUS_g	;
!! Kvo
:%^VowsRM QUO-STEM_iji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_n ;
:%^ConsSh%^VowsRM PSSPRSPRC ;
:%^ConsSh%^VowsRM	PSSPRFPRCSG    ;

!!

LEXICON V-01_AIGÕ
!! This is mutual for 37-48
:%^Stress1to2 GER_s   ;
:%^Stress1to2 INF_ZERO	  ;
:%^Stress1to2 SUP_m	  ; 
 INDPRSSG1-STEM    ;
:%^Stress1to2 INDPRSPL1    ;
:%^Stress1to2 INDPRSPL2    ;
 INDPRSPL3    ;
:%^Stress1to2%^VowsRM	INDCONNEGSG    ;
:%^Stress1to2    INDCONNEGPL1 ;
:%^Stress1to2    INDCONNEGPL2 ;
:%^Stress1to2    INDCONNEGPL3 ;

!! Prt
:%^VowsRM%>i INDPRT_z    ;

!! participles
:%^Stress1to2 ACTPRSPRC ;
:%^Stress1to2 ACTPRFPRC_n ;
:%^Stress1to2%^VowsRM%^ConsRM PSSPRSPRC ;
:%^Stress1to2%^VowsRM%^ConsRM	PSSPRFPRCSG    ;

:%^Stress1to2 COND ;
:%^Stress1to2%^VowsRM IMPRTSG2 ;
:%^Stress1to2 IMPRTPL1 ;
:%^Stress1to2 IMPRTPL2 ;
 IMPRTCONNEG ;
:%^Stress1to2 JUS_g	;
:%^Stress1to2%^VowsRM QUO-STEM_iji	;		


LEXICON V-01_KŪLÕ
!! **LEXICON @LEXNAME@ =  This is mutual for 49-50, 52-57
 GER_s   ;
 INF_ZERO         ;
 SUP-STEM_m       ;
: INDPRSSG1-STEM    ;
:%^ConsSh%^VowsRM       INDCONNEGSG    ;
!:%^ConsSh       INDCONNEGSG    ;
!! Prt
:%^VowsRM%>i INDPRT_z    ;
!! +Act+PrfPrc
+Act+PrfPrc:n    NMN_KŪOLÕN      ;
!! Cond
 COND ;
:%^ConsSh%^VowsRM IMPRTSG2/IMPRTCONNEGSG2 ;
 IMPRT-ELSE ;
 JUS_g ;
:%^VowsRM QUO_iji ; 


LEXICON V-01_AʼILÕ
!! **LEXICON @LEXNAME@ =  This is mutual for 51
 GER_s   ;
!! Ger, Ger_Ine

 INF_ZERO	  ; !!≈ * **@CODE@** aʼilõ
!! Inf

 SUP-STEM_m	  ; !!≈ * **@CODE@** aʼilõ-
!! Sup, Sup+Abe, Sup+Ela, Sup+Deb, Sup+Ill, Sup+Ine, Sup+Tra
!! Ind+Prs+Pl1, Ind+Prs+Pl2, Ind+Prs+Pl3, Ind+ConNeg+Pl1
!! Ind+ConNeg+Pl2, Ind+ConNeg+Pl3

: INDPRSSG1-STEM    ;	!!≈ * **@CODE@** aʼilõ-
!! Ind+Prs+Sg1, Ind+Prs+Sg2, Ind+Prs+Sg3

:%^VowsRM	INDCONNEGSG    ; !!≈ * **@CODE@** aʼil-
!! Ind+ConNeg+Sg

:%^VowsRM%>i INDPRT_z    ; !!≈ * **@CODE@** aʼili-
!! Ind+Prt+Sg1, Ind+Prt+Sg2, Ind+Prt+Sg3, Ind+Prt+Pl1, Ind+Prt+Pl2, Ind+Prt+Pl3

 COND		; !!≈ * **@CODE@** aʼilõ-
!! Cond...
:%^VowsRM IMPRTSG2/IMPRTCONNEGSG2 ;
!! Imprt+Sg2, Imprt+ConNeg+Sg2

IMPRT-ELSE ;
!! Imprt+Pl1, Imprt+Pl2, Imprt+ConNeg

JUS_g ;
!! Jus+Sg3, Jus+Pl3

:%^VowsRM QUO-STEM_iji ;
!! Quo+Sg3, Quo+Pl3, +NomAct -mi

 ACTPRFPRC_n ; !!≈ * **@CODE@** aʼilõ%>n
!! +Act+PrfPrc

:%^VowsRM PSSPRSPRC ;	!!≈ * **@CODE@** aʼil-
:%^VowsRM PSSPRFPRCSG ; !!≈ * **@CODE@** aʼil-



LEXICON V-01_UMBLÕ
!!  **LEXICON @LEXNAME@ = This is mutual for 58-61: 
!! umblõ, mõtlõ, mǟʼdlõ, naʼgrõ
   GER_s   ;
   INF_ZERO	  ;
   SUP-STEM_m	  ; 
   INDPRSSG1-STEM    ;
   INDCONNEGSG    ;
!! Prt
:%^VowsRM%>i INDPRT_z    ;
! Cond
: COND ;
!! Imprt
:%^VowsMetath%^VowsRM	IMPRTSG2    ;
: IMPRTPL1	;
: IMPRTPL2	;
:%^VowsMetath%^VowsRM IMPRTCONNEG ;

!! Jus
 JUS_g	;
!! Qvo
:%^VowsRM QUO-STEM_iji	;

!! participles
 ACTPRSPRC ;
 ACTPRFPRC_n ;
:%^VowsRM PSSPRSPRC ;
:%^VowsRM	PSSPRFPRCSG    ;




!LEXICON VTag
!	Persons	;

!LEXICON Persons
!+1sg:en   #   ;
!+2sg:et   #   ;
!+3sg:e    #   ;
!+1pl:ien  #   ;
!+2pl:iet  #   ;
!+3pl:iet  #   ;
!+inf:0    #   ;
!!#:0   Nouns   ;

!! ### Nonfinites

LEXICON GER_s
!! **LEXICON @LEXNAME@ = 
+Ger:%>s   K ;
+Ger+Ine:%>s   K ;

LEXICON GER_sõ
!! **LEXICON @LEXNAME@ = 
 +Ger:%>sõ   K ;
+Ger+Ine:%>sõ   K ;

LEXICON INF_ZERO
!! **LEXICON @LEXNAME@ = 
+Inf:	K	;

LEXICON INF_dõ
!! **LEXICON @LEXNAME@ = 
+Inf:%>dõ    K   ;

LEXICON INF_da
!! **LEXICON @LEXNAME@ = 
+Inf:%>da    K   ;

LEXICON SUP-STEM_m
!! **LEXICON @LEXNAME@ = 
   SUP_m	;
   INDPRSPL1    ;
   INDPRSPL2    ;
   INDPRSPL3    ;
   INDCONNEGPL1	;
   INDCONNEGPL2	;
   INDCONNEGPL3	;

LEXICON SUP_m
!! **LEXICON @LEXNAME@ = 
+Sup:%>m K   ;
+Sup+Abe:%>mõt K   ;
+Sup+Ela:%>mõst K   ;
+Sup+Deb:%>mõst K   ;
+Sup+Ill:%>m K   ;
+Sup+Ine:%>mõs K   ;
+Sup+Tra:%>mõks K   ;

LEXICON	SUP-STEM_mõ
!! **LEXICON @LEXNAME@ = 
   SUP_mõ	;
   INDPRSPL1    ;
   INDPRSPL2    ;
   INDPRSPL3    ;
   INDCONNEGPL1 ;
   INDCONNEGPL2 ;
   INDCONNEGPL3 ;

LEXICON SUP_mõ
!! **LEXICON @LEXNAME@ = 
+Sup:%>mõ K   ;
+Sup+Abe:%>mõt K   ;
+Sup+Ela:%>mõst K   ;
+Sup+Deb:%>mõst K   ;
+Sup+Ill:%>mõ K   ;
+Sup+Ine:%>mõs K   ;
+Sup+Tra:%>mõks K   ;

LEXICON ACTPRSPRC
!!  **LEXICON @LEXNAME@ = 
+Act+PrsPrc+Sg:%>b	K    ;
+Act+PrsPrc+Pl:%>bõd	K    ;
+Act+PrsPrc+Der/A:%>b	A_SIDĀM    ;

LEXICON	ACTPRFPRC_n
!! **LEXICON @LEXNAME@ = 
+Use/NG+Act+PrfPrc:%>n	K    ;
+Act+PrfPrc+Sg:%>n	K    ;
+Act+PrfPrc+Pl:%>nd	K    ;
+Act+PrfPrc:%>n  NMN_KŪOLÕN      ; 

LEXICON ACTPRFPRC_nd
!! **LEXICON @LEXNAME@ = 
+Act+PrfPrc+Sg:%>nd      K    ;
+Act+PrfPrc+Pl:%>nd     K    ; !! Are the singular and plural homonyms?

LEXICON ACTPRFPRC_SG-nd/PL-nõd
!! **LEXICON @LEXNAME@ = 
+Act+PrfPrc+Sg:%>nd      K    ;
+Act+PrfPrc+Pl:%>nõd     K    ; !! Are the singular and plural homonyms?


LEXICON PSSPRSPRC
!! **LEXICON @LEXNAME@ = 
+Pss+PrsPrc+Sg:%>dõb        K    ;
+Pss+PrsPrc+Pl:%>dõbõd      K    ;

LEXICON PSSPRFPRCSG
!! **LEXICON @LEXNAME@ = 
+Use/NG+Pss+PrfPrc:%>dõd    K    ;
+Pss+PrfPrc+Sg:%>dõd    K    ;
+Pss+PrfPrc:%>dõd	   NMN_TAPTÕD ; !! 2014-08-21

!! ### Finites

!LEXICON INDPRS
! Indicative present
!  INDPRSSG1-STEM	;
!  INDPRSPL1	;
!  INDPRSPL2	;
!  INDPRSPL3	;

LEXICON INDPRS_tõ
!!  **LEXICON @LEXNAME@ = Indicative present
  INDPRSSG1-STEM	;
  INDPRSPL1	;
  INDPRSPL2_tõ	;
  INDPRSPL3	;

LEXICON INDPRT_i
!!  **LEXICON @LEXNAME@ = Indicative preterite in i
 INDPRTSG1_i    ;
 INDPRTSG2_i    ;
 INDPRTSG3_i    ;
 INDPRTPL1_i    ;
 INDPRTPL2_i    ;
 INDPRTPL3_i    ;

LEXICON INDPRT_z
!!  **LEXICON @LEXNAME@ = Indicative preterite in z
 INDPRTSG1_z    ;
 INDPRTSG2_z    ;
 INDPRTSG3_z    ;
 INDPRTPL1_z    ;
 INDPRTPL2_z    ;
 INDPRTPL3_z    ;

LEXICON INDPRT_ž
!!  **LEXICON @LEXNAME@ = Indicative preterite in ž
 INDPRTSG1_ž    ;
 INDPRTSG2_ž    ;
 INDPRTSG3_ž    ;
 INDPRTPL1_ž    ;
 INDPRTPL2_ž    ;
 INDPRTPL3_ž    ;

LEXICON INDPRTSG3-STEM_tõ
!! **LEXICON @LEXNAME@ = 
 INDPRTSG3    ;
 INDPRTPL2_tõ    ;
 INDPRTPL3_tõ    ;


LEXICON COND
!!  **LEXICON @LEXNAME@ = Conditional present
  CONDSG1	;
  CONDSG2	;
  CONDSG3	;
  CONDPL1	;
  CONDPL2	;
  CONDPL3	;


!! ### Indicative present

LEXICON INDPRSSG1-STEM
!!  **LEXICON @LEXNAME@ = 
	INDPRSSG1	; !!≈ * @CODE@ 
	INDPRSSG2	; !!≈ * @CODE@ 
	INDPRSSG3	; !!≈ * @CODE@ these three lexica point to lexica with the actual suffixes.

LEXICON INDPRSSG1
 +Ind+Prs+Sg1:%>b    K	;

LEXICON INDPRSSG2
 +Ind+Prs+Sg2:%>d    K	;

LEXICON INDPRSSG3
 +Ind+Prs+Sg3:%>b    K	;

LEXICON INDPRSPL1
 +Ind+Prs+Pl1:%>m    K	;

LEXICON INDPRSPL2
 +Ind+Prs+Pl2:%>t    K	;

LEXICON INDPRSPL2_tõ
 +Ind+Prs+Pl2:%>tõ    K	;

LEXICON INDPRSPL3
 +Ind+Prs+Pl3:%>bõd    K	;

 ! Indicative preterite
LEXICON INDPRTSG1_i
 +Ind+Prt+Sg1:%>i    K	;

LEXICON INDPRTSG1_z
 +Ind+Prt+Sg1:%>z    K	;

LEXICON INDPRTSG1_ž
 +Ind+Prt+Sg1:%>ž    K	;

LEXICON INDPRTSG2_i
+Ind+Prt+Sg2:%>id    K ;

LEXICON INDPRTSG2_z
+Ind+Prt+Sg2:%>zt    K	;

LEXICON INDPRTSG2_ž
 +Ind+Prt+Sg2:%>žt    K	;

LEXICON INDPRTSG3
 +Ind+Prt+Sg3:    K	;

LEXICON INDPRTSG3_i
 +Ind+Prt+Sg3:%>i    K	;

LEXICON INDPRTSG3_z
 +Ind+Prt+Sg3:%>z    K	;

LEXICON INDPRTSG3_ž
 +Ind+Prt+Sg3:%>ž    K	;

LEXICON INDPRTPL1_i
 +Ind+Prt+Pl1:%>imõ    K	;

LEXICON INDPRTPL1_z
 +Ind+Prt+Pl1:%>zmõ    K	;

LEXICON INDPRTPL1_ž
 +Ind+Prt+Pl1:%>žmõ    K	;

LEXICON INDPRTPL2_i
 +Ind+Prt+Pl2:%>itõ    K	;

LEXICON INDPRTPL2_z
 +Ind+Prt+Pl2:%>zt    K	;
 +Ind+Prt+Pl2:%>ztõ    K	;

LEXICON INDPRTPL2_ž
 +Ind+Prt+Pl2:%>žt    K	;
 +Ind+Prt+Pl2:%>žtõ    K	;

LEXICON INDPRTPL2_tõ
 +Ind+Prt+Pl2:%>tõ    K	;

LEXICON INDPRTPL2_tā
 +Ind+Prt+Pl2:%>tā    K	;

LEXICON INDPRTPL3_i
 +Ind+Prt+Pl3:%>itõ    K	;

LEXICON INDPRTPL3_z
 +Ind+Prt+Pl3:%>zt    K	;
 +Ind+Prt+Pl3:%>ztõ    K	;

LEXICON INDPRTPL3_ž
 +Ind+Prt+Pl3:%>žt    K	;
 +Ind+Prt+Pl3:%>žtõ    K	;

LEXICON INDPRTPL3_tõ
 +Ind+Prt+Pl3:%>tõ    K	;

LEXICON INDPRTPL3_tā
 +Ind+Prt+Pl3:%>tā    K	;


LEXICON INDCONNEGSG
+Ind+ConNeg+Sg:    K	;

LEXICON INDCONNEGPL1
 +Ind+ConNeg+Pl1:%>m    K	;

LEXICON INDCONNEGPL2
 +Ind+ConNeg+Pl2:%>t    K	;

LEXICON INDCONNEGPL3
 +Ind+ConNeg+Pl3:%>t    K	;

!! Conditional
LEXICON CONDSG1
 +Cond+Sg1:%>ks    K	;

LEXICON CONDSG2
 +Cond+Sg2:%>kst    K	;

LEXICON CONDSG3
 +Cond+Sg3:%>ks    K	;

LEXICON CONDPL1
 +Cond+Pl1:%>ksmõ    K	;

LEXICON CONDPL2
 +Cond+Pl2:%>kstõ    K	;

LEXICON CONDPL3
 +Cond+Pl3:%>kstõ    K	;

!! Imperative
LEXICON IMPRT-ELSE
IMPRTPL1 ;
IMPRTPL2 ;
IMPRTCONNEG ;

LEXICON IMPRTSG2/IMPRTCONNEGSG2
IMPRTSG2 ;
IMPRTCONNEGSG2 ;

LEXICON IMPRTSG2
+Imprt+Sg2: K ;

LEXICON IMPRTPL1
+Imprt+Pl1:%>gõm K ;

LEXICON IMPRTPL2
+Imprt+Pl2:%>gid K ;

LEXICON IMPRTCONNEG
+Imprt+ConNeg:      K    ;

LEXICON IMPRTCONNEGSG2
+Imprt+ConNeg+Sg2:      K    ;

!! Jussative
LEXICON JUS_g
+Jus+Sg3:%>g             K    ;
+Jus+Pl3:%>gõd             K    ;

LEXICON JUS_gõ
+Jus+Sg3:%>gõ             K    ;
+Jus+Pl3:%>gõd             K    ;

!! Quotative
LEXICON QUO-STEM_ji
	QUO_ji	;
+NomAct+N:mi		N_AʼBLI	;
+NomAg+N:ji N_SĒJI ; !CHECKME 2021-08-04
!N_AKKIJI vs N_SĒJI
LEXICON QUO-STEM_iji
	QUO_iji	;
+NomAct+N:imi		N_AʼBLI	;
+NomAg+N:iji N_AKKIJI ; !CHECKME 2021-08-04

LEXICON QUO_ji
+Quo+Sg3:%>ji           K    ;
+Quo+Pl3:%>jid           K    ;

LEXICON QUO_iji
+Quo+Sg3:%>iji           K    ;
+Quo+Pl3:%>ijid           K    ;

! vim: set ft=xfst-lexc:

