
!! # Livonian morphophonology

!! This file documents the [phonology.twolc file](http://github.com/giellalt/lang-liv/blob/main/src/fst/phonology.twolc) 

!! We first show alphabet and sets, thereafter rules.


!! ## Alphabet

Alphabet
a b c d e f g h i j k l m n o p q r s t u v w x y z  !!= * @CODE@ 
A B C D E F G H I J K L M N O P Q R S T U V W X Y Z  !!= * @CODE@ 
ḑ ļ ņ ŗ š ț ž									     !!= * @CODE@ 
Ḑ Ļ Ņ Ŗ Š Ț Ž									     !!= * @CODE@ 
õ ä ö ȯ											     !!= * @CODE@ 
Õ Ä Ö Ȯ											     !!= * @CODE@ 
ā ē ī ō ū ǟ ǭ ȭ ȱ								     !!= * @CODE@ 
Ā Ē Ī Ō Ū Ǟ Ǭ Ȭ Ȱ								     !!= * @CODE@ 
ʼ !! * ʼ Stød

! loosening the rule to allow ǭ:ō 2013-06-26
!ǭ:ō
!Ǭ:Ō These are dealt with in filters 2020-10-22

!! ### Literal quotes and angles
!! They must be escaped (cf morpheme boundaries further down):

 »7     !! »7
 «7     !! «7
 %[%>%] !!= @CODE@ - Literal >
 %[%<%] !!= @CODE@ - Literal <

!! ### Archiphonemes for consonant lengthening

%{XC%}:p %{XC%}:t %{XC%}:k		      !!= * @CODE@
%{XC%}:b %{XC%}:d %{XC%}:g		      !!= * @CODE@
%{XC%}:l %{XC%}:ļ				      !!= * @CODE@
%{XC%}:m %{XC%}:n %{XC%}:ņ		      !!= * @CODE@
%{XC%}:r %{XC%}:ŗ				      !!= * @CODE@
%{XC%}:š %{XC%}:v %{XC%}:z %{XC%}:ž   !!= * @CODE@

!! ###  Triggers

 %^PenVV2V:0 !!= * @CODE@ penultimate vowel shortening
 %^Tense:0 !!= * @CODE@ = Tense stem will have stød if proper stem type
 %^D2T:0   !!= * @CODE@ d:t veʼž:veʼd:vietā
 %^PreI:0  !!= * @CODE@ i:0 veʼž:veʼd:vietā
 %^ĪE2Ē:0  !!= * @CODE@ kēļ kīel
K1:k       !!= * @CODE@ this k is not effected by gradation
%^NoGrad:0 !!= * @CODE@  This will be placed after a stem to break Gradation
%^WGStem:0 !!= * @CODE@ this weakens the stem ompel to ommel
%^TS:0     !!= * @CODE@ The ti => si
%^D2Ž:0    !!= * @CODE@ The *ti => *si
%^D2ZERO:0 !!= * @CODE@ The d => 0

%^PEN:0	   !!= * @CODE@ penultimate syllable
%^U2ZERO:0 !!= * @CODE@ The u => 0
%^VV2V:0	 !!= * @CODE@ shorten vowel

!! ###  Vowel raising
%^LowerVows:0 !!= * @CODE@ lower vowel
%^RVows:0     !!= * @CODE@ raise vowel
%^VowsSh1:0   !!= * @CODE@ vowel shortening in first syllable
%^VowsShU1:0  !!= * @CODE@ 
%^VowsShI1:0  !!= * @CODE@
%^VowsShIn1:0 !!= * @CODE@
%^DiphthSh1:0 !!= * @CODE@ 
%^VowsLI1:0   !!= * @CODE@ vowel lengthening that is followed by "i" when short
%^VowsLU1:0   !!= * @CODE@ vowel lengthening that is followed by "u" when short
%^VowsL1:0    !!= * @CODE@ vowel lengthening 
%^DiphthL1:0  !!= * @CODE@ 
%^LongV2Õin2:0   !!= * @CODE@ long vowel to õ in second syllable
%^Vow2Iin2:0   !!= * @CODE@ vowel to i or ī in second syllable

!! ### Vowel metathesis
%^VowsMetath:0   !!= * @CODE@

!! ### VOWEL SHORTENING
 %^VowShIn1:0	!!= * @CODE@ This causes vowel shortening in 1. syll
                !!= * @CODE@ accompanied by coda consonant lengthening
 %^A2ÕIn2:0       !!= * @CODE@ This causes 2. syll a => õ
%^StodRM:0  !!= * @CODE@ 
%^ConsSh:0  !!= * @CODE@ 
%^ConsL:0 !!  * @CODE@ lengthen consonant
%^1Sh2L:0  !!= * @CODE@ 
%^Stress1to2:0  !!= * @CODE@ 
%^Stress2to1:0  !!= * @CODE@ 
%^VowsMRM:0 !!≈ * @CODE@ Vow in middle ētam:eitmõd
%^VowsRM:0  !!= * @CODE@ 
%^ConsRM:0  !!= * @CODE@ 
%^PalatalizeLeft:0 !!  * @CODE@ laps:läpš
%^VowsL1aToǭ:0  !!= * @CODE@ 
%^VowsõToi:0 !! * @CODE@ kīndõr:kīndiriž
%- !!  * @CODE@ Hyphen in  constructions 
%> !!  * @CODE@ morpheme boundary
#   !!  * @CODE@ Word boundary for both lexicalised and dynamic compounds

;


!! ## Sets

Sets

VowBack = a o u A O U ;											   !!≈ * @CODE@ 
VowFront = ä ö y ü Ä Ö Y Ü ;									   !!≈ * @CODE@ 
VowNeutral = e i E I ; 											   !!≈ * @CODE@ 
VowNonHigh = a o ä ö e A O Ä Ö E ;								   !!≈ * @CODE@ 
VowLong  = ā ō ū ǟ ǭ ȭ ȱ ȫ ȳ ǖ ē ī Ā Ō Ū Ǟ Ǭ Ȭ Ȱ Ȫ Ȳ Ǖ Ē Ī ;	   !!≈ * @CODE@ 
VowShort = a o u ä ǫ õ ȯ ö y ü e i A O U Ä Ǫ Õ Ȯ Ö Y Ü E I ;	   !!≈ * @CODE@ 
Vow = VowLong VowShort ;										   !!≈ * @CODE@ 
CnsWithStod = b d g j l ļ m n ņ r ŗ v z ž ;						   !!≈ * @CODE@ 
Cns = b c č d ḑ f g ģ h j k ķ l ļ m n ņ p q r ŗ s š t ț v z ž 	   !!≈ * @CODE@ 
      B C Č D Ḑ F G Ģ H J K Ķ L Ļ M N Ņ P Q R Ŗ S Š T Ț V Z Ž ;	   !!≈   @CODE@ 
Letters = Vow Cns ;												   !!≈ * @CODE@ 
Dummy = %^ConsSh %^ConsL %^LowerVows %^PalatalizeLeft %^PenVV2V %^PEN %^VV2V %^U2ZERO   !!≈ * @CODE@ 
  %^StodRM %^Stress1to2 %^VowsLI1 %^VowsSh1 %^VowShIn1 %^VowsRM ;  !!≈   @CODE@ 

!Definitions
!
!CnsStrengthening = [ b: 0:b | d: 0:d | g: 0:g | j: 0:j | l: 0:l | ļ: 0:ļ | m: 0:m | ņ: 0:ņ | r: 0:r | ŗ: 0:ŗ | v: 0:v | z: 0:z | ž: 0:ž ];


!! # Rule section
Rules

!! ## Vowel rules

!! ### Shortening in first syllable


!! 
"ǟ:ä in first syllable"
!! **Rule: @RULENAME@**
ǟ:ä <=>
# Cns* _ (u:) Cns Cns:* Vow: %^PEN: %^VV2V: ;
# Cns* _ ( i ) Cns Cns:* (Vow:) [ %^VowShIn1:0 ] (%^A2ÕIn2:) [ %> | # ] ;
!!€ tǟutõ^PEN^VV2V
!!€ täutõ00
!!€# kǟnga%^VowShIn1%^A2ÕIn2
!!€0 kängõ00
        # Cns* _ ( u: ) Cns Cns:* (Vow:) %^VowsSh1: ;

"ā:a in first syllable"
!! **Rule: @RULENAME@**
ā:a <=>
# Cns* _  ( i ) Cns:+ Vow: %^PEN: %^VV2V: ;
# Cns* _  ( i ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 | %^VowShIn1:0 ] ;
!!€# āļdža%^VowShIn1%^A2ÕIn2
!!€0 aļdžõ00
     # Cns* _ ( i ) Cns:+ Vow: %^PenVV2V: ;
!!€# āita%^PenVV2V%^VowsRM%>õ
!!€0 ait000%>õ


"ȱ:ȯ"
!! **Rule: @RULENAME@**
ȱ:ȯ <=> # Cns* _ Cns:+ (Vow:) %^VowShIn1: ;
!!€# vȱntsa%^VowShIn1%^VowsRM%>õ
!!€0 vȯnts0000õ

"ā:ī in second syllable plural"
!! **Rule: @RULENAME@**
ā:ī <=>  _ [Dummy: - %^VowsRM:]* %^Vow2Iin2: ; 
!# Cns* Vow [ ʼ: | :ʼ ] [ :Cns | Cns: ] _ (%^ConsSh:) %^Vow2Iin2: ;
!!€# rikkā%^ConsSh%^Vow2Iin2>di
!!€# rik0ī000di

"ū:ī in second syllable plural"
!! **Rule: @RULENAME@**
ū:ī <=>  _ ( %^ConsSh: ) %^Vow2Iin2: ;
!!€# ruzū%^Vow2Iin2>di
!!€# ruzī000di

"a:i in second syllable plural"
!! **Rule: @RULENAME@**
a:i <=>  _ ( %^ConsSh: |%^VowsSh1: %^PalatalizeLeft: ) %^Vow2Iin2: ; 
!!€# āita%^Vow2Iin2>di
!!€# āiti00di

"ǭ:a in first syllable"
!! **Rule: @RULENAME@**
ǭ:a <=> # Cns* _  ( i: ) :Cns+ Vow: [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;
        # Cns* _  Cns %{XC%}: %^VowsSh1:0 ;

"ē:e in first syllable"
!! **Rule: @RULENAME@**
ē:e <=> # Cns* _  ( :i ) :Cns+ Vow: [ %^VowsShIn1:0 | %^Stress1to2:0 | %^VowsSh1:0 ] ;
!# Cns* _  ( :i ) :Cns+ Vow: %^Stress1to2:0  ;
 # Cns* _  ( :i ) :Cns+ Vow: Cns [ %^VowsShIn1:0 | %^Stress1to2:0 | %^VowsSh1:0 ] ;

"ū:u in first syllable"
!! **Rule: @RULENAME@**
ū:u <=> # Cns* _  ( i ) Cns:+ Vow: [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;
    # Cns* _  (ʼ:) Cns:+ (%{XC%}:) %^VowShIn1:0 ;
         # Cns* _ o %^VowShIn1:0 ;
!!€ jūo^VowShIn1>i
!!€ juo0>i

"ū:ȯ in first syllable"
!! **Rule: @RULENAME@**
ū:ȯ <=> # Cns* _   o:  Cns:+ (0:Cns) (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 (%^PalatalizeLeft:)]  ;
!!€# pūol0a%^Stress1to2%^ConsL
!!€0 pȯ0llõ00
!!€# pūoga%^VowsSh1%^PalatalizeLeft%^VowsRM%>õ
!!€0 pȯig00000õ


"ī:i in first syllable"
!! **Rule: @RULENAME@**
ī:i <=> # Cns* _  ( i | e ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;
      # Cns* _ Vow:* Cns:+ (0:Cns|%{XC%}:) (Vow:) %^VowShIn1:0 (%^A2ÕIn2:0) ; 
!!€# tīer0a%^VowShIn1%^A2ÕIn2
!!€0 tierrõ00

"ȭ:õ in first syllable"
!! **Rule: @RULENAME@**
ȭ:õ <=> # Cns* _  ( i | u: ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;
!!€# mȭuka%^VowsSh1%^VowsRM%>õ
!!€0 mõuk0000õ

"ō:o in first syllable"
!! **Rule: @RULENAME@**
ō:o <=> # Cns* _  ( i | 0:u ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 | %^VowsShU1: ]  ;

!! lengthen vowels

"e:ē in first syllable"
!! **Rule: @RULENAME@**
e:ē <=> # Cns* _  ( i: ) :Cns+ (Vow:) %^VowsLI1:0 ;
!!€# leiba%^VowsLI1%>st
!!€0 lē0ba00st
        # Cns* _   i: %^VowsLI1:0 ;
!!€# tei%^VowsLI1%>ḑi
!!€0 tē000ḑi

"u:ū in first syllable"
!! **Rule: @RULENAME@**
u:ū <=> # Cns* _  ( i: | ʼ: | :ʼ ) :Cns+ (Vow:) %^VowsLI1:0 ;
!!€# ul%^VowsLI1>õd
!!€0 ūl00õd


"õ:ȭ in first syllable"
!! **Rule: @RULENAME@**
õ:ȭ <=> # Cns* _  ( u: ) :Cns+ (Vow:) %^VowsLI1:0 ;

"ȯ:ū in first syllable"
!! **Rule: @RULENAME@**
ȯ:ū <=> # Cns* _  0: i:  Cns:+ (Vow:) %^VowsLI1:0  ;

"ä:ǟ in first syllable"
!! **Rule: @RULENAME@**
ä:ǟ <=> # Cns:* _  ( u: ) Cns:+ (Vow:) %^VowsLI1:0 ;

"0:i after shortened vowel or diphthong"
!! **Rule: @RULENAME@**
0:i <=> # Cns* [ ē: | ū: o: ] _ :Cns+ Vow: (Cns:) %^VowsShIn1:0 ;
         _ e ( ʼ: | :ʼ ) Cns: %^PreI: ;
!!€# v0eʼd%^PreI
!!€0 vieʼd0

"0:u after shortened vowel or diphthong"
!! **Rule: @RULENAME@**
0:u <=> # Cns* [ ō: ] _ Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsShU1: ] ;

"0:o after shortened vowel or diphthong"
!! **Rule: @RULENAME@**
0:o <=> # Cns*  ȯ: _ (i:) Cns:+ (Vow:) [ %^VowsLI1:0 ] ;

!! ### VOWEL LENGTHENING

"a:ǭ in first syllable"
!! **Rule: @RULENAME@**
a:ǭ <=> _ :Cns+ %^VowsL1aToǭ:0 ;
!!€# tam%^VowsL1aToǭõd
!!€0 tǭm0õd

"a:ā in first syllable"
!! **Rule: @RULENAME@**
a:ā <=> _ [i [d|g |k|l| m |n|s| z| t s] | :Cns+ ] (Vow:) %^VowsLI1:0 ;
!!€# kand%^VowsLI1õd
!!€0 kāndõd

"i:ī in first syllable"
!! **Rule: @RULENAME@**
i:ī <=> Cns _ Cns:+  (Vow:) %^VowsLI1:0 ;
!!€# niŗț%^VowsLI1%>õ
!!€0 nīŗț00õ
   _ e (ʼ:) Cns: %^VowsLI1:0 ;

"o:ō in first syllable"
!! **Rule: @RULENAME@**
o:ō <=> _ [i g | (u:) :Cns+] (Vow:) %^VowsLI1:0 ;
!!€# ouki%^VowsLI1z
!!€0 ō0ki0z

"o:i"
!! **Rule: @RULENAME@**
 o:i <=> ū: _ g (a:) %^VowsSh1: %^PalatalizeLeft: ;
!!€# pūog%^VowsSh1%^PalatalizeLeft%>õ
!!€0 pȯig0000õ

!! LOWER VOWELS
"ī:ē in tīe 15"
!! **Rule: @RULENAME@**
ī:ē <=> _ (ʼ:) Vow:  %^LowerVows: ;
!!€# tīʼe%^LowerVows%^StodRM%^VowsRM%>šti
!!€0 tē000000šti
 # Cns* _ (ʼ:) ( e: ) Cns: %^ĪE2Ē: ;
! requires 0:e
!!€0 kīel%^ĪE2Ē%^PalatalizeLeft
!!€# kē0ļ00

!! Destressing in second syllable
"ā:õ "
!! **Rule: @RULENAME@**
ā:õ <=>  _ ( %^ConsL: ) %^Stress2to1:0 [ # | %> ] ;
        _ ( %^ConsL: ) %^LongV2Õin2: ;
!!€# käp0ā%^ConsL%^LongV2Õin2
!!€0 käppõ00
     # Cns* [ Vow | ʼ: | :ʼ ]+ [ Cns (0:Cns)| Cns+ ] _ z: %^Stress2to1:0  ;
!# Cns* Vow ( 0: ) :Cns+ _ %^Stress2to1:0 [ # | %> ] ;
!!€  rik0āz%^Stress2to1%^ConsRM
!!€  rikkõ000

"a:õ "
!! **Rule: @RULENAME@**
a:õ <=>  _ ( %^ConsL:0 ) [ %^Stress2to1:0 | %^VowsShU1: ] [ # | %> ] ;
     # Cns* [ Vow: | ʼ: | :ʼ ]+ [ Cns (0:Cns)| Cns+ ] _ [ %^Stress1to2:0  (%^ConsL:) | (%^VowShIn1:) %^A2ÕIn2: ] [ # | %> ]  ;
!!€# tīer0a%^VowShIn1%^A2ÕIn2
!!€0 tierrõ00

"ū:õ "
!! **Rule: @RULENAME@**
ū:õ <=> # Cns* [ Vow | ʼ: | :ʼ ]+ :Cns+ _ %^Stress2to1:0 [ # | %> ] ;

"õ:i"
!! **Rule: @RULENAME@**
 õ:i <=> [ Vow | ʼ: | :ʼ ]+ :Cns+ _ [ n: | l: | z: | ļ:] %^PalatalizeLeft: ;
! # Cns* [ Vow | ʼ: | :ʼ ]+ :Cns+ _ [ n: | l: | z: ] %^PalatalizeLeft: ;
         [ Vow | ʼ: | :ʼ ]+ :Cns+ _ r %^VowsõToi:0  ;
!         # Cns* [ Vow | ʼ: | :ʼ ]+ :Cns+ _ r %^VowsõToi:0  ;
!!€# ǭʼrõn%^PalatalizeLeft
!!€0 ǭʼriņ0
!!€# nȭŗkõz%^PalatalizeLeft%>ist
!!€0 nȭŗkiz00ist


!! VOWEL LOSS

"ā:0"
!! **Rule: @RULENAME@**
ā:0 <=> _ Dummy:* %^VowsRM:0  ;
!# Cns*  [Vow: | ʼ: | :ʼ ]+ [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 | %^Stress1to2: | %^ConsSh: |%^PalatalizeLeft:) %^VowsRM:0 | %^VowsShIn1:0 %^VowsRM:0 | %^ConsL:0 %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
!!€# aʼmmā%^PalatalizeLeft%^ConsSh%^VowsRM%>ži
!!€0 äʼm000000ži
!!€# aʼmā%^PalatalizeLeft%^ConsSh%^VowsRM%>ḑi
!!€# ä0m00000ḑi
!!€# daʼdzā%^Stress1to2%^VowsRM%>õ
!!€0 daʼdz0000õ

! # Cns* Vow ( ʼ: | :ʼ ) [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 ) %^VowsRM:0 | %^VowsShIn1:0 %^VowsRM:0 | %^ConsL:0 %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
       # Cns* [ Vow | ʼ: | :ʼ ]+ [ Cns: | :Cns ]+ _ Cns: %^StodRM: %^VowsRM: %^ConsRM: ;
!!€# maʼigāz%^StodRM%^VowsRM%^ConsRM>īst
!!€0 ma0ig000000īst

"ū:0"
!! **Rule: @RULENAME@**
ū:0 <=>  _ Dummy:* %^VowsRM:0 ;
! # Cns* [Vow | ʼ: | :ʼ ]+ [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 ) %^VowsRM:0 | %^VowsShIn1:0 %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
    # Cns* Vow:+ ( ʼ: | :ʼ ) [ Cns: | :Cns ]+ _ Cns  %^Tense:0  ;
!#Vow: %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 %> d: 

"ī:0"
!! **Rule: @RULENAME@**
ī:0 <=>  _ Dummy:* %^VowsRM: ;
    # Cns* Vow:+ ( ʼ: | :ʼ ) [ Cns: | :Cns ]+ _ Cns  %^Tense:0  ;
    # Cns* _ e %> i ;
!!€# sīe%>i
!!€# s0e0i

"a:0"
!! **Rule: @RULENAME@**
a:0 <=>   _ Dummy:* %^VowsRM: ;
! # Cns* [ Vow:+ ( 0:i )| Vow: Vow: ] [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress1to2:0 ) ( %^ConsL:0 ) | %^VowsShIn1:0 | %^VowsSh1:0 | ( %^Stress1to2:0 ) %^PalatalizeLeft:0 ] %^VowsRM:0 ;
 # Cns* [ Vow:+ ( 0:i )| Vow: Vow: ] [ Cns: | :Cns ]+ _ Cns [ %^VowsShIn1:0  ] %^VowsMRM:0 ;
!!€# jǭr0a%^Stress1to2%^ConsL%^VowsRMõ
!!€0 jarr0000õ
!!€# pūol0a%^Stress1to2%^ConsL%^VowsRMõ
!!€0 pȯ0ll0000õ
     _ (%^VowShIn1:) (%^PalatalizeLeft:) %^VowsRM: ; !%^Vow2Iin2: might also be good
!!€# kǟnga%^VowShIn1%^PalatalizeLeft%^VowsRMi
!!€0 keņg0000i

"e:0"
!! **Rule: @RULENAME@**
 e:0 <=> _ Dummy:* %^VowsRM: ;
!!€ tīʼe%^LowerVows%^StodRM%^VowsRM%>šti
!!€ tē000000šti
      _ Cns: %^ĪE2Ē: ;
!! kēļ+N+Sg+Nom
!!€ kīel%^ĪE2Ē%^PalatalizeLeft
!!€ kē0l00


"õ:0"
!! **Rule: @RULENAME@**
õ:0 <=> _ Dummy:* %^VowsRM: ;
!  [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress1to2:0 ) | %^VowsMetath:0 | ( %^StodRM:0 ) %^ConsSh:0 | %^VowsLI1:0 ] %^VowsRM:0 ;
!!€# kittõ%^ConsSh%^VowsRM%>dõd
!!€0 kit00000tõd
!          Vow %> _ [ d | n | k s | s t ] ;

"i:0 in first syllable"
!! **Rule: @RULENAME@**
i:0 <=>  # Cns* [ e: | ȯ: (0:) ] _ :Cns+ (Vow:) %^VowsLI1:0 ; 
!!€# leiba%^VowsLI1%>st
!!€0 lē0ba00st
 # Cns:* Vow:+ (ʼ:) [ CnsWithStod :0* | i g | Cns:+ Cns: ] _ (%^StodRM:) (%^ConsSh:) %^VowsRM: ;
         # Cns* e: _ %^VowsLI1:0 ;
!!€# tei%^VowsLI1%>ḑi
!!€0 tē000ḑi
         Vow: _ j Vow: %^ConsSh: ;
!!€# lǭija%^ConsSh
!!€0 lǭ0ja0
       # Cns*  _ e Cns:+ (Vow: ) %^VowShIn1:  ;
!!€# liestā%^VowShIn1%^PalatalizeLeft%^VowsRM%>i
!!€0 l0ešt00000i
       # Cns* Vow: _ s: k Vow: %^ConsSh: ;
!!€# ūiska%^ConsSh
!!€0 ū0ška0

"u:0 in second position of first-syllable diphthong"
!! **Rule: @RULENAME@**
u:0 <=>
# Cns:* ǟ: _ Cns:+ Vow: %^PEN: %^U2ZERO: ;
# Cns:* [ õ: | ȭ: | ä: | o: ]  _ Cns:+ ( Vow: ) %^VowsLI1:0 ;
!!€ tǟutõ^PEN^U2ZERO^VowsRM>a>m
!!€ tǟ0t0000>a>m
!!€# ouki%^VowsLI1z
!!€0 ō0ki0z
         Vow: _ v Vow: %^ConsSh: ;
!!€# pǟuva%^ConsSh
!!€0 pǟ0va0

"o:0 in second position of first-syllable diphthong"
!! **Rule: @RULENAME@**
!! ```
o:0 <=> # Cns:* [ ū: ]  _ Cns:+ (0:Cns) (Vow:) [ %^Stress1to2:0 ] ;
!!€# pūol0a%^Stress1to2%^ConsL
!!€# pȯ0llõ00
!! ```

!! ### Zero to vowel


"0:õ in vowel metathesis"
!! **Rule: @RULENAME@**
0:õ <=> [ b | d | t | g ] _ [ l | r ] õ: %^VowsMetath:0 %^VowsRM:0  ;


!! ## Consonant rules

!! ### Consonant loss 
"shorten consonantism between 1st and 2nd vowel center jeʼllõ:jelāb"
!! **Rule: @RULENAME@**
 Cx:0 <=> 
# Cns* Vow: ( ʼ: ) Cx _ Vow: Dummy:* %^ConsSh:0  ;
          where Cx in ( b j l ļ m n ņ r ŗ v ž ) ;
!!€# aʼmmā%^PalatalizeLeft%^ConsSh%^VowsRM%>ži
!!€0 äʼm000000ži

"g:0"
!! **Rule: @RULENAME@**
 g:0 <=>
# Cns* Vow ( ʼ: ) g _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
  _ (%^VowsLI1:|%^StodRM:) (%^VowsRM:) %^ConsRM: ;

"z:0"
!! **Rule: @RULENAME@**
 z:0 <=>
# Cns* Vow ( ʼ: ) z _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
  _ (%^Stress2to1:|%^StodRM:) (%^VowsRM:) %^ConsRM: ;
!!€  rik0āz%^Stress2to1%^ConsRM
!0  rikkõ000

!! ### Consonant lengthening

!! Lengthening consonantism between first and second vowel center
!! simultaneous to reducing vowel of second syllable

! jelāb:jellõ
"lengthen consonantism between 1st and 2nd vowel center jelāb: jellõ"
!! **Rule: @RULENAME@**
 0:Cx <=> 
 # Cns* Vow:+ ( ʼ: ) Cx _ Vow: (Cns:) [ %^VowShIn1: (%^A2ÕIn2: ) | ( %^Stress1to2:0 ) %^ConsL:0 | %^Stress2to1:0 ] ;
! # Cns* Vow: (o:) ( ʼ: ) Cx _ Vow: (Cns:) [ ( %^Stress1to2:0 ) %^ConsL:0 | %^Stress2to1:0 ] ;
 # Cns* Vow ( ʼ: ) Cx _ [ %^ConsL:0 ] ;
! Cx _ Vow: %^ConsL:0  [ # | %> | %^VowsRM:0 %> :Vow ] ;
! Cx _ Vow: %^Stress2to1:0 [ # | %> | %^VowsRM:0 %> :Vow ] ;
          where Cx in ( t k b d g l ļ m n ņ r ŗ š v z ž ) ;
!!€  je0l0ā%^Stress2to1
!0  jeʼllõ0
!!€  ka0l0ā%^Stress2to1
!0  kaʼllõ0
!!€  rik0āz%^Stress2to1%^ConsRM
!0  rikkõ000
!!€ pūol0a^Stress1to2^ConsL
!!€ pȯ0llõ00

"0:p"
!! **Rule: @RULENAME@**
 0:p <=> Vow: p _ Vow: %^ConsL: ;
!!€ käp0ā^ConsL^LongV2Õin2
!!€ käppõ00

"0:s"
!! **Rule: @RULENAME@**
 0:s <=> Vow: s _ (Vow:) %^ConsL: ;
!!€ mis0^ConsL>õst
!!€ miss0>õst

"%{XC%}:Cx"
!! **Rule: @RULENAME@**
 %{XC%}:Cx <=> 
!# Cns* Vow ( ʼ: ) Cx _ Vow: [ ( %^Stress1to2:0 ) %^ConsL:0 | %^Stress2to1:0 ] 
               :Cx _ ;
!  # Cns* [ a | ä | e | õ | u | i ] ( ʼ: ) Cx _ [ %> õ | %> i ] ;
         where Cx in (t b d g l ļ m n ņ r ŗ š v z ) ;

"%{XC%}:p"
!! **@RULENAME@** 2014-02-27
 %{XC%}:p <=> p _ %> ;
!!€# kep%{XC%}>õ
!!€0 kepp0õ

"%{XC%}:s"
!! **@RULENAME@** 2020-10-21
 %{XC%}:s <=> s _ %> ;
!! tas+N+Sg+Ill
!!€ tas{XC}>õ
!!€ tass>õ

"%{XC%}:ž"
!! **@RULENAME@** 2014-02-27
 %{XC%}:ž <=>
  # Cns* [ a | ä | õ | u | i | e ] ( ʼ: ) :ž (%^D2Ž:) _ [ %> õ |  %> i ] ;
! :ž _ %> ;
!!€# veʼd%^D2Ž%{XC%}>i
!!€0 veʼž0ž0i

!"%{XC%}:š"
!!! __@RULENAME@__ 2014-01-18 "%{XC%}:Cx" not working
! %{XC%}:š <=> š _ %> ;

"%{XC%}:k"
!! **@RULENAME@** 2014-02-27
 %{XC%}:k <=> k _ %> ;
!!€# rok%{XC%}>õ
!!€0 rokk0õ

"Stod removal left"
!! **Rule: @RULENAME@**
 ʼ:0 <=> Vow: _ \[ .#. | # ]* %^StodRM:0 ;
 ! comment: 2020-10-21: without surrounding full stops, # is the character used in the stem
!  # Cns* :0* Vow _ [ CnsWithStod Cns:* [ :0* | Vow: z: ]| [ i | u ] g (Vow: Cns:) | o Cns ] ( Vow: | ū: Cns: ) %^StodRM:0 ; 
!!€# daʼdzā%^Stress1to2%^VowsRM%>õ
!!€0 daʼdz0000õ
!! aʼb#sõʼnā+N+Sg+Nom: 
!!€ aʼb#sõʼn%^StodRMā
!!€ aʼb#sõ0n0ā
!"Stod removal right"
!!! __@RULENAME@__  
! ʼ:0 =>  Vow: _ \[ .#. ]* %^StodRM:0 ;
!# Cns* :0* Vow _ [ CnsWithStod Cns:* :0* | [ i | u ] g (Vow: Cns:) | o Cns ] (Vow: | ū: Cns: ) %^StodRM:0 ; 
! 2013-06-26 for looser voikko stød can disappear everywhere 
! _ ; a filter does this removal 2015-06-17

! Vow _ CnsWithStod: ( :CnsWithStod )  Vow: %^Stress2to1:0 [ # | %> | %^VowsRM:0  %> ] ;
!# Cns* :Vow _ :CnsWithStod+  Vow: %^Stress2to1:0 [ # | %> | %^VowsRM:0  %> ] ;
!!€# ka0l0ā%^Stress2to1
!!€0 kaʼllõ0

!# Cns* Vow ( 0: ) :Cns+ _ %^Stress2to1:0 [ # | %> ] ;
!# Cns* :Vow _  CnsStrengthening Vow: %^Stress2to1:0  [ # | %> | %^VowsRM:0  %> ] ;
!         # Cns* :Vow _ [ g: | d: | l: ] Vow: %^Stress2to1:0 %^VowsRM:0  %^ConsRM:0  ;
!         # Cns* :Vow _ [ g: | d: | l: ] Vow: %^Stress2to1:0 %^VowsRM:0 %> [ ḑ i | ž i | š t i ] ;

"z:ž"
!! **Rule: @RULENAME@**
 z:ž <=> _ (Vow:0|t) %^PalatalizeLeft:0 ; !! 
!!€# alīz%^PalatalizeLeft%>i
!!€0 alīž00i
!!€# izā%^PalatalizeLeft%^VowsRM%>ḑi
!!€# iž0000ḑi

"d:ḑ"
!! **Rule: @RULENAME@**
d:ḑ <=>
      Vow: _ (%^ĪE2Ē:) (%^StodRM:) %^PalatalizeLeft:0 ; !! lēʼḑ:līʼed 147

"l:ļ"
!! **Rule: @RULENAME@**
l:ļ <=> _ Vow: (%^Stress2to1:0) %^VowsRM:0 ( %^ConsRM:0 ) %> [ ḑ i | ž i | š t i ] ;
        _ m Vow: (%^Stress1to2:) %^PalatalizeLeft:0 ( %^VowsRM:0 ) [ # | %> ] ;
        _ ( g |%{XC%}: ) (%^VowsLI1:|%^VowShIn1:) (%^StodRM:) %^PalatalizeLeft:0 ( %^VowsRM:0 ) ( %^ConsRM:0 ) [ # | %> ] ;!kiļg:kilg
!! This rule should not require the %^ConsRM:0 trigger, but for now this makes it work.
	_ (%^ĪE2Ē:) (%^StodRM:) %^PalatalizeLeft:0 ; !! kēļ:kēl 215
!!€# äʼddõl%^PalatalizeLeft%>i
!!€0 äʼddiļ00i
!!€# tuʼl%^StodRM%^PalatalizeLeft%>īž
!!€# tu0ļ000īž

"n:ņ palatalization"
!! **Rule: @RULENAME@**
n:ņ <=> _ ( Vow: ) (%^VowsSh1:) %^PalatalizeLeft:0 ( %^VowsRM:0 | %^Vow2Iin2:) [ # | %> ] ;
!!€# Vē0na%^PalatalizeLeft%^VowsRMõ
!!€0 Vein000õ
!!€# ǭʼrõn%^PalatalizeLeft
!!€0 ǭʼriņ0
       _ t s: Vow: %^VowShIn1: %^PalatalizeLeft: ;
!!€# vȱntsa%^VowShIn1%^PalatalizeLeft%^VowsRM%>ti
!!€0 vȯņtš00000ti
        _ %^PalatalizeLeft:0 ; !! sēņ:sēn 220
       _ g ( Vow: ) (%^VowShIn1:) %^PalatalizeLeft: ;
!!€# kǟnga%^VowShIn1%^PalatalizeLeft%^VowsRMi
!!€0 keņg0000i
   

"r:ŗ"
!! **Rule: @RULENAME@**
r:ŗ <=> _ %^PalatalizeLeft:0 ; !! jūŗ:jūr 221
       _ g ( Vow: ) ( %^VowShIn1: ) %^PalatalizeLeft: ;
!!€# ǟrga%^VowShIn1%^PalatalizeLeft%^VowsRMi
!!€0 eŗg0000i


! First syllable short second syllable long
! jellõ: jelāb
!"shorten first syllable jellõ: jelāb"
! Cx:0 <=> Cx _ %^1Sh2L:0 ;
!          Cx _ %^ConsRM:0 ;
!          where Cx in ( d g l ļ n p ŗ ) ;

"d:t"
!! **Rule: @RULENAME@**
d:t <=>
       Vow: ( ʼ: | :ʼ ) [ s k: | d d: | t t: | k k:  | t s | n d | ŗ š t: | r t: | 0:u [ t: | d ] ] (Vow:) ( %^ConsSh:0 |%^Stress1to2:) %^VowsRM:0 ( %^ConsRM:0 ) %> _ õ d ;
!!€# kittõ%^ConsSh%^VowsRM%>dõd
!!€0 kit00000tõd
       [ ž | :z | :s | :š ] %> _ ; !! nominative plural 
       _ (%^PreI:) (%^StodRM:) %^D2T:  ;
!!€# v0eʼd%^PreI%^StodRM%^D2T
!!€0 vie0t

"d:ț"
!! **Rule: @RULENAME@**
d:ț <=> [ ț: ] Vow: ( %^ConsSh:0 ) %^VowsRM:0  ( %^ConsRM:0 ) %> _ õ d ;

"d:ž"
!! **Rule: @RULENAME@**
d:ž <=>
       :Vow ( ʼ: | :ʼ ) Cns:* _ (%^PreI:) (%^StodRM:) (%^VowsLI1:) %^D2Ž:0 ;

!"ž:d "
!!! __@RULENAME@__
! ž:d <=>
!       :Vow Cns:* _ (%^VowsLI1:|%^StodRM) %^Ž2D:0 ;



!! ### Rules for consonant loss

"d:0"
!! **Rule: @RULENAME@**
d:0 <=>
     Vow: (Cns:+)  _ (%^PreI: %^StodRM:|%^VowsLI1:) %^D2ZERO:0 ;  !!= @CODE@ 
      Cns* Vow ( ʼ: ) d _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;

"t:0"
!! **T loss before subsequent morpheme with underlying initial d**  
t:0 <=>
    [ Cns:+ | Vow: 0:u ] _ Vow: ( %^Stress1to2:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;
    # Cns* Vow ( ʼ: ) t _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
!!€# kittõ%^ConsSh%^VowsRM%>dõd
!!€0 kit00000tõd

"k:0"
!! **Rule: @RULENAME@**
k:0 <=> s _ Vow: ( %^ConsSh:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;
       # Cns* Vow ( ʼ: ) k _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
!!€# rikkā%^ConsSh%^Vow2Iin2di
!!€0 rik0ī00di

"ț:0"
!! **Rule: @RULENAME@**
ț:0 <=> k _ Vow: ( %^ConsSh:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;

"s:š palatalization"
!! **Rule: @RULENAME@**
s:š <=> _ %^PalatalizeLeft:0 [ # | %> ] ;
       _ (t) Vow: Dummy:*  %^PalatalizeLeft:0   ;
!!€# liestā%^VowShIn1%^PalatalizeLeft%^VowsRM%>i
!!€0 l0ešt00000i
        # Cns* ȯ:  0: i: _ Cns:* (Vow:) %^VowsLI1:0 ;
!!€# pȯ0is%^VowsLI1õd
!!€0 pūo0š0õd

"ǟ:ē palatalization"
!! **Rule: @RULENAME@**
ǟ:ē <=> _ (ʼ:) %^PalatalizeLeft:0 (%^StodRM:) [ # | %> ] ;

"ǟ:e short and palatalization"
!! **Rule: @RULENAME@**
ǟ:e <=> # Cns* _ Cns:+ ( Vow: ) %^VowShIn1: %^PalatalizeLeft: ;
# Cns* _ ʼ: %^VowShIn1: %^PalatalizeLeft: ;
!!€ kǟnga^VowShIn1^PalatalizeLeft^VowsRMi
!!€ keņg0000i
!!€ nǟʼ^VowShIn1^PalatalizeLeft^StodRM>i>z
!!€ ne000>i>z

"a:ä palatalization"
!! **Rule: @RULENAME@**
a:ä <=> # Cns*  _ ( 0: | ʼ: ) [p:|m:] Cns* [%^PalatalizeLeft:0 | Vow: %^PalatalizeLeft:0 (%^ConsSh:) %^VowsRM: ] [ # | %> ] ;

!!€# aʼmā%^PalatalizeLeft%^ConsSh%^VowsRM%>ḑi
!!€# äʼm00000ḑi

!echo 'a ʼ b # s õ ʼ:0 n ^StodRM:0 ā' | hfst-pair-test src/fst/phonology.compose.hfst
