!! ================================================ !
!! The Livonian morphophonological/twolc rules file !
!! ================================================ !


Alphabet
a b c d e f g h i j k l m n o p q r s t u v w x y z
A B C D E F G H I J K L M N O P Q R S T U V W X Y Z
ḑ ļ ņ ŗ š ț ž
Ḑ Ļ Ņ Ŗ Š Ț Ž
õ ä ö ȯ
Õ Ä Ö Ȯ
ā ē ī ō ū ǟ ǭ ȭ ȱ
Ā Ē Ī Ō Ū Ǟ Ǭ Ȭ Ȱ

!! '
ʼ !! Stød



!! Triggers
! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0

! The ti => si
%^TS:0

! Vowel raising
%^RVows:0
%^VowsSh1:0 !!

!! vowel metathesis
%^VowsMetath:0

%^StodRM:0

!%^ConsSh:0
%^ConsL:0 !! lengthen consonant
%^1Sh2L:0

%^Stress1to2:0
%^Stress2to1:0
%^VowsRM:0
%^ConsRM:0
%^PalatalizeLeft:0 !! laps:läpš

%^VowsL1aToǭ:0

%- !! Hyphen in  constructions 
%> !! morpheme boundary
#   !! Word boundary for both lexicalised and dynamic compounds

;


Sets

VowsBack = a o u ;
VowsFront = ä ö y ü ;
VowsNeutral = e i ; 
VowsNonHigh = a o ä ö e ;
VowsLong  = ā ō ū ǟ ǭ ȭ ȱ ȫ ȳ ǖ ē ī ;
VowsShort = a o u ä ǫ õ ȯ ö y ü e i ;
Vows = VowsLong VowsShort ;

ConsWithStod = b d g j l ļ m ņ r ŗ v z ž ;
Cons = b c č d ḑ f g ģ h j k ķ l ļ m n ņ p q r ŗ s š t ț v z ž ;

Letters = Vows Cons ;


!Definitions
!
!ConsStrengthening = [ b: 0:b | d: 0:d | g: 0:g | j: 0:j | l: 0:l | ļ: 0:ļ | m: 0:m | ņ: 0:ņ | r: 0:r | ŗ: 0:ŗ | v: 0:v | z: 0:z | ž: 0:ž ];

Rules

!! Vowels
!! Shortening in first syllable
"ā:a in first syllable"
ā:a <=> # Cons* _  ( i ) :Cons+ Vows: %^Stress1to2:0  ;

"ǭ:a in first syllable"
ǭ:a <=> # Cons* _  ( i ) :Cons+ Vows: %^Stress1to2:0  ;

"ē:e in first syllable"
ē:e<=> # Cons* _  ( i ) :Cons+ Vows: %^Stress1to2:0  ;


"a:ǭ in first syllable"
a:ǭ <=> _ :Cons+ %^VowsL1aToǭ:0 ;
!€# tam%^VowsL1aToǭõd
!€0 tǭm0õd


!! Destressing in second syllable
"ā:õ "
ā:õ <=>  _ ( %^ConsL:0 ) %^Stress2to1:0 [ # | %> ] ;
!# Cons* Vows ( 0: ) :Cons+ _ %^Stress2to1:0 [ # | %> ] ;

"ū:õ "
ū:õ <=> # Cons* [ Vows | ʼ: | :ʼ ]+ :Cons+ _ %^Stress2to1:0 [ # | %> ] ;

!! Vowel loss
!! 
"ā:0"
ā:0 <=> # Cons* Vows ( ʼ: | :ʼ ) [ Cons: | :Cons ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 ) %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;

"ū:0"
ū:0 <=> # Cons* [Vows | ʼ: | :ʼ ]+ [ Cons: | :Cons ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 ) %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
!#Vows: %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 %> d: 

"a:0"
a:0 <=> # Cons* Vows:+ [ Cons: | :Cons ]+ _ ( %^StodRM:0 ) ( %^Stress1to2:0 ) ( %^ConsL:0 ) %^VowsRM:0 ;
!€# jǭr0a%^Stress1to2%^ConsL%^VowsRMõ
!€0 jarr0000õ
"õ:0"
õ:0 <=>  [ Cons: | :Cons ]+ _ [ ( %^StodRM:0 ) ( %^Stress1to2:0 ) | %^VowsMetath:0 ] %^VowsRM:0 ;
!          Vows %> _ [ d | n | k s | s t ] ;

"0:õ in vowel metathesis"
0:õ <=> [ b | d | t | g ] _ [ l | r ] õ: %^VowsMetath:0 %^VowsRM:0  ;


!! Consonant loss 


! Lengthening consonantism between first and second vowel center
! simultaneous to reducing vowel of second syllable
!! jelāb:jellõ
"lengthen consonantism between 1st and 2nd vowel center jelāb: jellõ"
 0:Cx <=> Cx _ Vows: [ ( %^Stress1to2:0 ) %^ConsL:0 | %^Stress2to1:0 ] ;
! Cx _ Vows: %^Stress2to1:0 [ # | %> | %^VowsRM:0 %> :Vows ] ;
          where Cx in ( t k b d g l ļ m n ņ r ŗ v z ž ) ;
!€  je0l0ā%^Stress2to1
!0  jeʼllõ0

"Stod removal"
 ʼ:0 <=>  # Cons* Vows _ [ :ConsWithStod+ | i g ] Vows: %^StodRM:0 ; 
! Vows _ ConsWithStod: ( :ConsWithStod )  Vows: %^Stress2to1:0 [ # | %> | %^VowsRM:0  %> ] ;
!# Cons* :Vows _ :ConsWithStod+  Vows: %^Stress2to1:0 [ # | %> | %^VowsRM:0  %> ] ;
!€# ka0l0ā%^Stress2to1
!€0 kaʼllõ0

!# Cons* Vows ( 0: ) :Cons+ _ %^Stress2to1:0 [ # | %> ] ;
!# Cons* :Vows _  ConsStrengthening Vows: %^Stress2to1:0  [ # | %> | %^VowsRM:0  %> ] ;
!         # Cons* :Vows _ [ g: | d: | l: ] Vows: %^Stress2to1:0 %^VowsRM:0  %^ConsRM:0  ;
!!         # Cons* :Vows _ [ g: | d: | l: ] Vows: %^Stress2to1:0 %^VowsRM:0 %> [ ḑ i | ž i | š t i ] ;

 
"l:ļ"
l:ļ <=> _ Vows: %^Stress2to1:0 %^VowsRM:0 ( %^ConsRM:0 ) %> [ ḑ i | ž i | š t i ] ;
!! This rule should not require the %^ConsRM:0 trigger, but for now this makes it work.


! First syllable short second syllable long
!! jellõ: jelāb
!"shorten first syllable jellõ: jelāb"
! Cx:0 <=> Cx _ %^1Sh2L:0 ;
!          Cx _ %^ConsRM:0 ;
!          where Cx in ( d g l ļ n p ŗ ) ;

"d:t "
d:t <=>
       :Vows ( ʼ: | :ʼ ) [ s k: | d: | t: | k  | t s ] Vows: ( %^Stress2to1:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> _ õ d ;
       [ :z | :s | :š ] %> _ ; !! nominative plural 

"d:ț "
d:ț <=> [ ț: ] Vows: ( %^Stress2to1:0 ) %^VowsRM:0  ( %^ConsRM:0 ) %> _ õ d ;

"k:0"
k:0 <=> s _ Vows: ( %^Stress2to1:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;


"ț:0"
ț:0 <=> k _ Vows: ( %^Stress2to1:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;

"s:š palatalization"
s:š <=> _ %^PalatalizeLeft:0 [ # | %> ] ;

"ǟ:ē palatalization"
ǟ:ē <=> _ %^PalatalizeLeft:0 [ # | %> ] ;

"a:ä palatalization"
a:ä <=> _ ( 0: ) :Cons+ %^PalatalizeLeft:0 [ # | %> ] ;

