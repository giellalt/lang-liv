
!! !!!The Livonian morphophonological/twolc rules file 


!! !!Alphabet

Alphabet
a b c d e f g h i j k l m n o p q r s t u v w x y z  !!= * @CODE@ 
A B C D E F G H I J K L M N O P Q R S T U V W X Y Z  !!= * @CODE@ 
ḑ ļ ņ ŗ š ț ž									     !!= * @CODE@ 
Ḑ Ļ Ņ Ŗ Š Ț Ž									     !!= * @CODE@ 
õ ä ö ȯ											     !!= * @CODE@ 
Õ Ä Ö Ȯ											     !!= * @CODE@ 
ā ē ī ō ū ǟ ǭ ȭ ȱ								     !!= * @CODE@ 
Ā Ē Ī Ō Ū Ǟ Ǭ Ȭ Ȱ								     !!= * @CODE@ 


ʼ !! ʼ Stød

! loosening the rule to allow ǭ:ō 2013-06-26
ǭ:ō
Ǭ:Ō

!  for consonant lengthening

%{XC%}:p %{XC%}:t %{XC%}:k
%{XC%}:b %{XC%}:d %{XC%}:g
%{XC%}:l %{XC%}:ļ
%{XC%}:m %{XC%}:n %{XC%}:ņ
%{XC%}:r %{XC%}:ŗ
%{XC%}:š %{XC%}:v %{XC%}:z %{XC%}:ž

!! ! Triggers
 %^Tense:0 !!= * @CODE@ = Tense stem will have stød if proper stem type


K1:k       !!= * @CODE@ this k is not effected by gradation
%^NoGrad:0 !!= * @CODE@  This will be placed after a stem to break Gradation


%^WGStem:0 !!= * @CODE@ this weakens the stem ompel to ommel


%^TS:0 !!= * @CODE@ The ti => si

%^D2Ž:0 !!= * @CODE@ The *ti => *si

%^D2ZERO:0 !!= * @CODE@ The d => 0

!! ! Vowel raising

%^RVows:0     !!= * @CODE@ raise vowel
%^VowsSh1:0   !!= * @CODE@ vowel shortening in first syllable
%^VowsShU1:0  !!= * @CODE@ 
%^VowsShI1:0  !!= * @CODE@ 
%^DiphthSh1:0 !!= * @CODE@ 
%^VowsLI1:0   !!= * @CODE@ vowel lengthening that is followed by "i" when short
%^VowsLU1:0   !!= * @CODE@ vowel lengthening that is followed by "u" when short
%^VowsL1:0    !!= * @CODE@ vowel lengthening 
%^DiphthL1:0  !!= * @CODE@ 

%^LongV2Õin2:0   !!= * @CODE@ long vowel to õ in second syllable

%^Vow2Iin2:0   !!= * @CODE@ vowel to i or ī in second syllable

!! vowel metathesis
%^VowsMetath:0



%^StodRM:0

%^ConsSh:0
%^ConsL:0 !!  * @CODE@ lengthen consonant
%^1Sh2L:0


%^Stress1to2:0
%^Stress2to1:0
%^VowsRM:0
%^ConsRM:0
%^PalatalizeLeft:0 !!  * @CODE@ laps:läpš

%^VowsL1aToǭ:0

%- !!  * @CODE@ Hyphen in  constructions 
%> !!  * @CODE@ morpheme boundary
#   !!  * @CODE@ Word boundary for both lexicalised and dynamic compounds

;


!! !!!Sets

Sets

VowBack = a o u A O U ;
VowFront = ä ö y ü Ä Ö Y Ü ;
VowNeutral = e i E I ; 
VowNonHigh = a o ä ö e A O Ä Ö E ;
VowLong  = ā ō ū ǟ ǭ ȭ ȱ ȫ ȳ ǖ ē ī Ā Ō Ū Ǟ Ǭ Ȭ Ȱ Ȫ Ȳ Ǖ Ē Ī ;
VowShort = a o u ä ǫ õ ȯ ö y ü e i A O U Ä Ǫ Õ Ȯ Ö Y Ü E I ;
Vow = VowLong VowShort ;

CnsWithStod = b d g j l ļ m n ņ r ŗ v z ž ;
Cns = b c č d ḑ f g ģ h j k ķ l ļ m n ņ p q r ŗ s š t ț v z ž B C Č D Ḑ F G Ģ H J K Ķ L Ļ M N Ņ P Q R Ŗ S Š T Ț V Z Ž ;

Letters = Vow Cns ;


!Definitions
!
!CnsStrengthening = [ b: 0:b | d: 0:d | g: 0:g | j: 0:j | l: 0:l | ļ: 0:ļ | m: 0:m | ņ: 0:ņ | r: 0:r | ŗ: 0:ŗ | v: 0:v | z: 0:z | ž: 0:ž ];


!! !!!Rule section
Rules

!! !!Vowel rules

!! !Shortening in first syllable

!! 
"ā:a in first syllable"
!! __@RULENAME@__  
ā:a <=> # Cns* _  ( i ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ] ;

"ā:ī in second syllable plural"
!! __@RULENAME@__
ā:ī <=>  _ ( %^ConsSh: ) %^Vow2Iin2: ; 
!# Cns* Vow [ ʼ: | :ʼ ] [ :Cns | Cns: ] _ (%^ConsSh:) %^Vow2Iin2: ;
!€# rikkā%^ConsSh%^Vow2Iin2>di
!€# rik0ī000di

"ǭ:a in first syllable"
!! __@RULENAME@__  
ǭ:a <=> # Cns* _  ( i ) :Cns+ Vow: [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;


"ē:e in first syllable"
!! __@RULENAME@__  
ē:e <=> # Cns* _  ( :i ) :Cns+ Vow: [ %^VowsShI1:0 | %^Stress1to2:0 | %^VowsSh1:0 ] ;
!# Cns* _  ( :i ) :Cns+ Vow: %^Stress1to2:0  ;

"ū:u in first syllable"
!! __@RULENAME@__  
ū:u <=> # Cns* _  ( i ) Cns:+ Vow: [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;

"ū:ȯ in first syllable"
!! __@RULENAME@__  
!! {{{
ū:ȯ <=> # Cns* _   o:  Cns:+ (0:Cns) (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;
!€# pūol0a%^Stress1to2%^ConsL
!€# pȯ0llõ00
!! }}}

"ī:i in first syllable"
!! __@RULENAME@__  
ī:i <=> # Cns* _  ( i ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;

"ȭ:õ in first syllable"
!! __@RULENAME@__  
ȭ:õ <=> # Cns* _  ( i ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;

"ō:o in first syllable"
!! __@RULENAME@__  
ō:o <=> # Cns* _  ( i | 0:u ) Cns:+ (Vow:) [ %^Stress1to2:0 | %^VowsSh1:0 ]  ;

!! lengthen vowels

"e:ē in first syllable"
!! __@RULENAME@__  
e:ē<=> # Cns* _  ( i: ) :Cns+ Vow: %^VowsLI1:0 ;

"u:ū in first syllable"
!! __@RULENAME@__  
u:ū <=> # Cns* _  ( i: ) :Cns+ (Vow:) %^VowsLI1:0 ;
!€# ul%^VowsLI1>õd
!€0 ūl00õd

"õ:ȭ in first syllable"
!! __@RULENAME@__  
õ:ȭ <=> # Cns* _  ( u: ) :Cns+ Vow: %^VowsLI1:0 ;

"ȯ:ū in first syllable"
!! __@RULENAME@__  
ȯ:ū <=> # Cns* _  0: i:  Cns:+ (Vow:) %^VowsLI1:0  ;

"ä:ǟ in first syllable"
!! __@RULENAME@__  
ä:ǟ <=> # Cns:* _  ( u: ) Cns:+ (Vow:) %^VowsLI1:0 ;

"0:i after shortened vowel or diphthong"
!! __@RULENAME@__  
0:i <=> # Cns* [ ē: | ū: o: ] _ :Cns+ Vow: %^VowsShI1:0 ;

"0:u after shortened vowel or diphthong"
!! __@RULENAME@__  
0:u <=> # Cns* [ ō: ] _ Cns:+ (Vow:) [ %^Stress1to2:0 ] ;

"0:o after shortened vowel or diphthong"
!! __@RULENAME@__  
0:o <=> # Cns*  ȯ: _ (i:) Cns:+ (Vow:) [ %^VowsLI1:0 ] ;



"a:ǭ in first syllable"
!! __@RULENAME@__  
a:ǭ <=> _ :Cns+ %^VowsL1aToǭ:0 ;
!€# tam%^VowsL1aToǭõd
!€0 tǭm0õd

"a:ā in first syllable"
!! __@RULENAME@__  
a:ā <=> _ [i g | :Cns+] (Vow:) %^VowsLI1:0 ;
!€# kand%^VowsLI1õd
!€0 kāndõd

"o:ō in first syllable"
!! __@RULENAME@__  
o:ō <=> _ [i g | (u:) :Cns+] (Vow:) %^VowsLI1:0 ;
!€# ouki%^VowsLI1z
!€0 ō0ki0z

!! raise vowels
"ē:ī in kēļ 215"
!! __@RULENAME@__  
ē:ī <=> # Cns* _ ( :e ) Cns %^RVows:0 ;
! requires 0:e

!! Destressing in second syllable
"ā:õ "
!! __@RULENAME@__  
ā:õ <=>  _ ( %^ConsL:0 ) %^Stress2to1:0 [ # | %> ] ;
     # Cns* [ Vow | ʼ: | :ʼ ]+ [ Cns (0:Cns)| Cns+ ] _ z: %^Stress2to1:0  ;
!# Cns* Vow ( 0: ) :Cns+ _ %^Stress2to1:0 [ # | %> ] ;
!€  rik0āz%^Stress2to1%^ConsRM
!0  rikkõ000

"a:õ "
!! __@RULENAME@__
a:õ <=>  _ ( %^ConsL:0 ) %^Stress2to1:0 [ # | %> ] ;
     # Cns* [ Vow: | ʼ: | :ʼ ]+ [ Cns (0:Cns)| Cns+ ] _ [ %^Stress1to2:0  (%^ConsL:) ] [ # | %> ]  ;

"ū:õ "
!! __@RULENAME@__  
ū:õ <=> # Cns* [ Vow | ʼ: | :ʼ ]+ :Cns+ _ %^Stress2to1:0 [ # | %> ] ;

"õ:i"
!! __@RULENAME@__
 õ:i <=> # Cns* [ Vow | ʼ: | :ʼ ]+ :Cns+ _ [ n: ] %^PalatalizeLeft: ;

!! Vowel loss

"ā:0"
!! __@RULENAME@__  
ā:0 <=> # Cns* [ Vow | ʼ: | :ʼ ]+ [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 | %^ConsSh:) %^VowsRM:0 | %^VowsShI1:0 %^VowsRM:0 | %^ConsL:0 %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
! # Cns* Vow ( ʼ: | :ʼ ) [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 ) %^VowsRM:0 | %^VowsShI1:0 %^VowsRM:0 | %^ConsL:0 %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
       # Cns* [ Vow | ʼ: | :ʼ ]+ [ Cns: | :Cns ]+ _ Cns: %^StodRM: %^VowsRM: %^ConsRM: ;
!€# maʼigāz%^StodRM%^VowsRM%^ConsRM>īst
!€0 ma0ig000000īst

"ū:0"
!! __@RULENAME@__  
ū:0 <=> # Cns* [Vow | ʼ: | :ʼ ]+ [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress2to1:0 ) %^VowsRM:0 | %^VowsShI1:0 %^VowsRM:0 | %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 ] [ # | %> ] ;
    # Cns* Vow:+ ( ʼ: | :ʼ ) [ Cns: | :Cns ]+ _ Cns  %^Tense:0  ;
!#Vow: %^Stress2to1:0 %^VowsRM:0 %^ConsRM:0 %> d: 

"ī:0"
!! __@RULENAME@__  
ī:0 <=> _ %^VowsRM:0 ;
    # Cns* Vow:+ ( ʼ: | :ʼ ) [ Cns: | :Cns ]+ _ Cns  %^Tense:0  ;

"a:0"
!! __@RULENAME@__  
a:0 <=> # Cns* [ Vow:+ ( 0:i )| Vow: Vow: ] [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress1to2:0 ) ( %^ConsL:0 ) | %^VowsShI1:0 | %^VowsSh1:0 | ( %^Stress1to2:0 ) %^PalatalizeLeft:0 ] %^VowsRM:0 ;
!€# jǭr0a%^Stress1to2%^ConsL%^VowsRMõ
!€0 jarr0000õ
!€# pūol0a%^Stress1to2%^ConsL%^VowsRMõ
!€# pȯ0ll0000õ


"õ:0"
!! __@RULENAME@__  
õ:0 <=>  [ Cns: | :Cns ]+ _ [ ( %^StodRM:0 ) ( %^Stress1to2:0 ) | %^VowsMetath:0 | ( %^StodRM:0 ) %^ConsSh:0 | %^VowsLI1:0 ] %^VowsRM:0 ;
!          Vow %> _ [ d | n | k s | s t ] ;

"i:0 in first syllable"
!! __@RULENAME@__  
i:0 <=>  # Cns* [ e: | ȯ: (0:) ] _ :Cns+ (Vow:) %^VowsLI1:0 ; 
 # Cns:* Vow:+ (ʼ:) [ CnsWithStod :0* | i g ] _ (%^StodRM:) (%^ConsSh:) %^VowsRM: ;


"u:0 in second position of first-syllable diphthong"
!! __@RULENAME@__  
u:0 <=> # Cns:* [ õ: | ä: | o: ]  _ Cns:+ (Vow:) %^VowsLI1:0 ;
!€# ouki%^VowsLI1z
!€0 ō0ki0z

"o:0 in second position of first-syllable diphthong"
!! __@RULENAME@__  
!! {{{
o:0 <=> # Cns:* [ ū: ]  _ Cns:+ (0:Cns) (Vow:) [ %^Stress1to2:0 ] ;
!€# pūol0a%^Stress1to2%^ConsL
!€# pȯ0llõ00
!! }}}

!! !Zero to vowel

"0:e in vowel raising kēļ"
!! __@RULENAME@__  
0:e <=> Cns* ē: _ Cns %^RVows:0 ;

"0:õ in vowel metathesis"
!! __@RULENAME@__  
0:õ <=> [ b | d | t | g ] _ [ l | r ] õ: %^VowsMetath:0 %^VowsRM:0  ;


!! !!Consonant rules

!! !Consonant loss 
"shorten consonantism between 1st and 2nd vowel center jeʼllõ:jelāb"
!! __@RULENAME@__  
 Cx:0 <=> 
# Cns* Vow ( ʼ: ) Cx _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
          where Cx in ( b g l ļ m n ņ r ŗ v ž ) ;

"z:0"
!! __@RULENAME@__
 z:0 <=>
# Cns* Vow ( ʼ: ) z _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
  _ (%^Stress2to1:|%^StodRM:) (%^VowsRM:) %^ConsRM: ;
!€  rik0āz%^Stress2to1%^ConsRM
!0  rikkõ000

!! !Consonant lengthening

!! Lengthening consonantism between first and second vowel center
!! simultaneous to reducing vowel of second syllable

! jelāb:jellõ
"lengthen consonantism between 1st and 2nd vowel center jelāb: jellõ"
!! __@RULENAME@__  
 0:Cx <=> 
 # Cns* Vow: (o:) ( ʼ: ) Cx _ Vow: (Cns:) [ ( %^Stress1to2:0 ) %^ConsL:0 | %^Stress2to1:0 ] ;
 # Cns* Vow ( ʼ: ) Cx _ [ %^ConsL:0 ] ;
! Cx _ Vow: %^ConsL:0  [ # | %> | %^VowsRM:0 %> :Vow ] ;
! Cx _ Vow: %^Stress2to1:0 [ # | %> | %^VowsRM:0 %> :Vow ] ;
          where Cx in ( t k b d g l ļ m n ņ r ŗ š v z ž ) ;
!€  je0l0ā%^Stress2to1
!0  jeʼllõ0
!€  ka0l0ā%^Stress2to1
!0  kaʼllõ0
!€  rik0āz%^Stress2to1%^ConsRM
!0  rikkõ000
!€# pūol0a%^Stress1to2%^ConsL
!€# pȯ0llõ00





"%{XC%}:Cx"
!! __@RULENAME@__
 %{XC%}:Cx <=> 
!# Cns* Vow ( ʼ: ) Cx _ Vow: [ ( %^Stress1to2:0 ) %^ConsL:0 | %^Stress2to1:0 ] 
  # Cns* [ a | ä | õ | u | i ] ( ʼ: ) Cx _ [ %> õ | %> i ] ;
         where Cx in (p t k b d g l ļ m n ņ r ŗ š v z ž ) ;

!"%{XC%}:š"
!!! __@RULENAME@__ 2014-01-18 "%{XC%}:Cx" not working
! %{XC%}:š <=> š _ %> ;

"Stod removal left"
!! __@RULENAME@__  
 ʼ:0 <=  # Cns* Vow _ [ CnsWithStod :0* | i g ] Vow: %^StodRM:0 ; 

"Stod removal right"
!! __@RULENAME@__  
 ʼ:0 =>  # Cns* Vow _ [ CnsWithStod :0* | i g ] Vow: %^StodRM:0 ; 
! 2013-06-26 for looser voikko stød can disappear everywhere
 _ ;
! Vow _ CnsWithStod: ( :CnsWithStod )  Vow: %^Stress2to1:0 [ # | %> | %^VowsRM:0  %> ] ;
!# Cns* :Vow _ :CnsWithStod+  Vow: %^Stress2to1:0 [ # | %> | %^VowsRM:0  %> ] ;
!€# ka0l0ā%^Stress2to1
!€0 kaʼllõ0

!# Cns* Vow ( 0: ) :Cns+ _ %^Stress2to1:0 [ # | %> ] ;
!# Cns* :Vow _  CnsStrengthening Vow: %^Stress2to1:0  [ # | %> | %^VowsRM:0  %> ] ;
!         # Cns* :Vow _ [ g: | d: | l: ] Vow: %^Stress2to1:0 %^VowsRM:0  %^ConsRM:0  ;
!         # Cns* :Vow _ [ g: | d: | l: ] Vow: %^Stress2to1:0 %^VowsRM:0 %> [ ḑ i | ž i | š t i ] ;

 
"l:ļ"
!! __@RULENAME@__  
l:ļ <=> _ Vow: (%^Stress2to1:0) %^VowsRM:0 ( %^ConsRM:0 ) %> [ ḑ i | ž i | š t i ] ;
        _ m Vow: (%^Stress1to2:) %^PalatalizeLeft:0 ( %^VowsRM:0 ) [ # | %> ] ;
	_ %^PalatalizeLeft:0 ; !! kēļ:kēl 215
!! This rule should not require the %^ConsRM:0 trigger, but for now this makes it work.

"n:ņ palatalization"
!! __@RULENAME@__  
n:ņ <=> _ ( Vow: ) %^PalatalizeLeft:0 ( %^VowsRM:0 ) [ # | %> ] ;
!€# Vē0na%^PalatalizeLeft%^VowsRMõ
!€0 Vein000õ
        _ %^PalatalizeLeft:0 ; !! sēņ:sēn 220

"r:ŗ"
!! __@RULENAME@__  
r:ŗ <=> _ %^PalatalizeLeft:0 ; !! jūŗ:jūr 221


! First syllable short second syllable long
! jellõ: jelāb
!"shorten first syllable jellõ: jelāb"
! Cx:0 <=> Cx _ %^1Sh2L:0 ;
!          Cx _ %^ConsRM:0 ;
!          where Cx in ( d g l ļ n p ŗ ) ;

"d:t "
!! __@RULENAME@__  
d:t <=>
       Vow: ( ʼ: | :ʼ ) [ s k: | d d: | t t: | k k:  | t s | n d | ŗ š t: | r t: | 0:u [ t: | d ] ] (Vow:) ( %^ConsSh:0 |%^Stress1to2:) %^VowsRM:0 ( %^ConsRM:0 ) %> _ õ d ;
       [ ž | :z | :s | :š ] %> _ ; !! nominative plural 

"d:ț "
!! __@RULENAME@__  
d:ț <=> [ ț: ] Vow: ( %^ConsSh:0 ) %^VowsRM:0  ( %^ConsRM:0 ) %> _ õ d ;

"d:ž "
!! __@RULENAME@__  
d:ž <=>
       :Vow Cns:* _ (%^VowsLI1:) %^D2Ž:0 ;

!! !Consonant loss

"d:0"
!! __@RULENAME@__  
d:0 <=>
     Vow: (Cns:+)  _ (%^VowsLI1:) %^D2ZERO:0 ;  !!= @CODE@ 
      Cns* Vow ( ʼ: ) d _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;

"t:0"
!! __T loss before subsequent morpheme with underlying initial d__  
t:0 <=>
    [ Cns:+ | Vow: 0:u ] _ Vow: ( %^Stress1to2:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;
    # Cns* Vow ( ʼ: ) t _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;

"k:0"
!! __@RULENAME@__  
k:0 <=> s _ Vow: ( %^ConsSh:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;
       # Cns* Vow ( ʼ: ) k _ Vow: ( %^StodRM:0 ) %^ConsSh:0  ;
!€# rikkā%^ConsSh%^Vow2Iin2di
!€# rik0ī00di

"ț:0"
!! __@RULENAME@__  
ț:0 <=> k _ Vow: ( %^ConsSh:0 ) %^VowsRM:0 ( %^ConsRM:0 ) %> d: õ d ;

"s:š palatalization"
!! __@RULENAME@__  
!! {{{
s:š <=> _ %^PalatalizeLeft:0 [ # | %> ] ;
        # Cns* ȯ:  0: i: _ Cns:* (Vow:) %^VowsLI1:0 ;
!€# pȯ0is%^VowsLI1õd
!€0 pūo0š0õd
!! }}}

"ǟ:ē palatalization"
!! __@RULENAME@__  
ǟ:ē <=> _ %^PalatalizeLeft:0 [ # | %> ] ;

"a:ä palatalization"
!! __@RULENAME@__  
a:ä <=> # Cns*  _ ( 0: ) :Cns+ %^PalatalizeLeft:0 [ # | %> ] ;

