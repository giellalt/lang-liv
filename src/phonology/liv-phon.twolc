!! =================================== !
!! The Livonian morphophonological/twolc rules file !
!! =================================== !


Alphabet
a b d ḑ e f g h i j k l ļ m n ņ o p r ŗ s š t ț u v z ž õ ä ö
A B D Ḑ E F G H I J K L Ļ M N Ņ O P R Ŗ S Š T Ț U V Z Ž Õ Ä Ö
ā ē ī ō ū ǟ ǭ ȭ ȯ ȱ
Ā Ē Ī Ō Ū Ǟ Ǭ Ȭ Ȯ Ȱ

' ! Stød

!This far 2012-12-21
! Vowel harmony with "(t)a/ä"
AÄ1:a
AÄ1:ä
AÄ1:0

! Vowel harmony with "loi/löi" also kalatOÖ1in
OÖ1:o
OÖ1:ö

! Vowel harmony with "ttu/tty"
UY1:u
UY1:y

! this appears in the illative
V1:a
V1:e
V1:i
V1:o
V1:u
V1:y
V1:ä
V1:ö

! These appear with the inessive and adessive
V2:a
V2:e
V2:i
V2:o
V2:u
V2:y
V2:ä
V2:ö

! These reduplicate the preceding vowel if it in turn is preceded by a consonant
V3:a
V3:e
V3:i
V3:o
V3:u
V3:y
V3:ä
V3:ö
V3:0

! this k is not effected by gradation
K1:k
%^NoGrad:0 ! This will be placed after a stem to break Gradation

! this weakens the stem _ompel_ to _ommel_
%^WGStem:0

! The ti => si
%^TS:0

! Vowel raising
%^RVows:0

%- !Hyphen in  constructions 
%>:0
#:0   ! Word boundary for both lexicalised and dynamic compounds

Cx
Cy
X
Y

%^LVows:0
%^LCons:0
%^WCons:0
%^AtoO:0
%^ÄtoÖ:0
%^OddSyll:0
%^StretchSyll2:0
%^SyllBr:0
%^E1:0

;


Sets

VowsBack = a o u ;
VowsFront = ä ö y ü ;
VowsNeutral = e i ь ; 
VowsNonHigh = a o ä ö e ;
Vows = a o u ä ö y ü e i ь ;

Cons = b d f g h j k l m n p r s ş š t v z ƶ ž ;

Letters = Vows Cons ;



Definitions

! Front Trigger
FT =  [ #: Cons* [ VowsFront | VowsNeutral | i: ] | :VowsFront ] \[ #: | VowsBack: | :VowsBack ]*  ;


! Back Trigger 
BT =   ( #: Cons* ( [ VowsNeutral | i: ] \[ #: | VowsFront: | :VowsFront ]* ) ) [ VowsBack: | :VowsBack ]  \[ #: | VowsFront: | :VowsFront ]* ;

! Gradation strong to weak
!GS2W = [ p:v | m p:m | t:0 | l t:l | r t:r | n t:n | s t:s | k:0 | l k:0 | r k:0 | n k:g | s k:0 ] ;

! Short vowel
SV = [ [ #: | Cons+ ] :Vows ] ;

 ! If a vowel is preceeded by this, is can be long
!  in both the inessive and the adessive
! Inessive lengthening of vowel
IL = [ #: Cons* Vows : Cons:+ | Cons* Vows+ Cons:+  Vows:+ Cons:+  ] ;

!! Right context for gradation
GRC =  [ [ ( %>:0 ) AÄ1: | OÖ1: | UY1: | Vows: | :Vows ]  ( %>:0 i ) |  %>:0  i ] %>:0 %^WCons:0  ;

! oma:ommaa, 
! Subsequent Long Vowel
! preceeded by "Cons _"
! 2012-10-11 SLV =  [ :Vows [ ( %>:0 %^LCons:0 %^LVows:0 | %>:0 %^LCons:0 | %>:0 i %>:0 | %>:0 ) [ V1:Vows | AÄ1: ] |  0:Vows %>:0 ( %^LCons:0 | %^WCons: ) %^LVows: | %>:0 %^LCons:0  ] ] ;

Rules

! Vowel reduction


! Stem vowel loss before plural "i"
"StemVowLoss"
 X:Y <=> X _  %>:0 i  %>:0 ;
!( %>:0  %^WCons:0 |  %>:0  %^LCons:0 ) %>:0 [ i | i:j ] ;
          where X in (e o ö)
                Y in (0 0 0) matched ;

! A loss
"StemALoss"
 X:Y <=>  _   ( %>:0 %^LCons:0 ) %>:0 i: %>:0 ;
          where X in (a ä)
                Y in (0 0) matched ;

!€ osa%>^WCons%>i%>st
!€ os0000i0st

!%>%^LConsi%>V1
! AÄ1 loss
"StemAÄLoss"
 AÄ1:0 <=> [ AÄ1: | Cons: | :Cons ] _ ( %>:0 %^LCons:0 ) %>:0 i %>:0 ;


! U loss [u:0 | y:0]
"StemULoss"
 X:Y <=> X _ %>:0  i  %>:0 ;
! ( %>:0  %^WCons:0 |  %>:0  %^LCons:0 ) %>:0 i %>:0 ;
         [ e | ä | a | o | ö ] _  [ t: | k: ] 0:v ;
          where X in (u y ü)
                Y in (0 0 0) matched ;
!Vows:0 %>:0  %^WCons:0 %>:0 i: %>:0 

!€ maa%>i%>n
!€ ma00i0n

! Stem vowel "i" loss before plural "i"
"i:Zero"
 i:0 <=> i _ %>:0 i  %>:0 ;

! iToj between vowels
"i:j"
 i:j <=> :Vows ( %>:0  %^LCons:0 ) %>:0 _ %>:0 :Vows ;
         Vows _ Cons:0 Vows: ;
!veitte%>%^WConsttUY1%>%^LCons%>i%>AÄ1
!€ em0o0i%>%^LConsAÄ1
!€ emmooj00a

! a to e in comparatives
"a:e"
 a:e <=> _ %>:0 %^WCons:0 %^E1:0 ;

! ä to e in comparatives
"ä:e"
 ä:e <=> _ %>:0 %^WCons:0 %^E1:0 ;


!
! Vowel harmony suffixes Front
"AÄ1:ä"
 AÄ1:ä <=> FT _  ( %>:0  %^WCons:0 ( %^LVows:0 ) |  %^LVows:0  |  %>:0  %^LCons:0 ( %^LVows:0 ) ) [ ( %>:0 ) [ Cons | %>:0 %^WCons:0 [ Cons ] | [[ Vows: ] - [ i | i:j ]]] | #: | AÄ1: | %^StretchSyll2:0 | i | %>:0 ( %^WCons:0 | %^LCons:0 ) [ V1: | V3: ] ]  ;

! mato : mattooa
! Vowel harmony suffixes Back
"AÄ1:a"
 AÄ1:a <=> BT _   ( %>:0  %^WCons:0 ( %^LVows:0 ) |  %^LVows:0  |  %>:0  %^LCons:0 ( %^LVows:0 ) ) [ ( %>:0 ) [ Cons | %>:0 %^WCons:0 [ Cons ] | [[ Vows: ] - [ i | i:j ]]] | #: | AÄ1: | %^StretchSyll2:0 | i | %>:0 ( %^WCons:0 | %^LCons:0 ) [ V1: | V3: ] ]  ;

!
! Vowel harmony suffixes Front
"OÖ1:ö"
 OÖ1:ö <=>  FT _ ;

! Vowel harmony suffixes Back
"OÖ1:o"
 OÖ1:o <=>  BT _ ;

!
! Vowel harmony suffixes Front
"UY1:y"
 UY1:y <=>  FT _ ;

! Vowel harmony suffixes Back
"UY1:u"
 UY1:u <=>  BT _ ;

! Vows %>:0 %^LCons: V1:
! Vowel agreement in illative 
"V1:aeouüäö"
 V1:Vx <=> :Vx %>:0 [ h | ( %^LCons: %>:0 ) ] _ ;
        where Vx in (a o u ä ö ü y) ;

! Vowel agreement in illative e
"V1:e"
 V1:e <=> [ e | i ] %>:0  h  _ ;
          e ( %>:0 %^LCons: ) %>:0  _ ;

! Illative i with no intermittent _h_
"V1:i"
 V1:i <=> i ( %>:0 %^LCons: ) %>:0 _ ;

!  _mato:maool_ not allowed
! Used with patsas type nominals
"V2:aeiouüäö"
 V2:Vx <=>  Vx ( h ( :h ) ) %>:0  ( %^LCons:0 )  _ [ V2: | ( %>:0 ([ %^WCons:0 | %^LCons:0 ] ( V3: ) ) )  Cons ] ;
            Vx ( h ( :h ) ) %>:0  ( %^LCons:0 | %^WCons:0 ) V2: _ [ ( %>:0 )  Cons ]  ;
        where Vx in (a o u ä ö ü e i y) ;


"V2:Zero"
 V2:0 <=> Vows %>:0 _ %>:0 i: %>:0 ;

!!!FROM HERE

! Vowel lengthening for the inesive and adessive with V3
!! This deals with secondary or perhaps pertary vowel lengthening
! e.g. gen plural, ine, ade
"V3:a"
 V3:a <=> :Cons :0* :a :0* _ ;

"V3:e"
 V3:e <=> :Cons :0* :e :0* _ ;

"V3:i"
 V3:i <=> :Cons :0* :i :0* _ ;

"V3:o"
 V3:o <=> :Cons :0* :o :0* _ ;

"V3:u"
 V3:u <=> :Cons :0* :u :0* _ ;

"V3:ä"
 V3:ä <=> :Cons :0* :ä :0* _ ;

"V3:ö"
 V3:ö <=> :Cons :0* :ö :0* _ ;

"V3:y"
 V3:y <=> :Cons :0* :y :0* _ ;

"V3:Zero"
 V3:0 <=> :Vows :0* :Vows :0* _ ;

! ! Vowel Lengthening
! mato : mattoon,
 "Zero:Vx"
 0:Vx <=> ( Vows+ :Cons+ ) Vx _ %>:0 ( %^LCons: | %^WCons:0 ) %^LVows: ;
          #: Cons* Vows [ Cons 0: | 0:i j ] Vx _ Cons ( Cons:0 ) :Vows %^StretchSyll2:0 ; ! ommeena<omena
! 
         [ #: ] Cons* Vows Cons 0: Vx _ Cons :0 Vows [ Cons | [ #: ] ] ;
       where Vx in (a o u ä ö y e) ; 


! Secondary Consonant lengthening
! mato : mattoo, näytettävä : näytettävvää
!"even-syllable" This should be secondary
"consonant lengthening"
 0:Cx <=> 
         [ #: Cons* Vows+ Cons:+ Vows+ :0* Cons+ :Vows ( %>:0 %^LCons:0 ) | #: Cons*  Vows | %^OddSyll:0 ] Cx _ ( [ Vows: | :Vows ] ( :Vows ) ) %>:0 %^LCons:0 ; !
         #: Cons* Vows Cx _ [ Vows 0: Cons ( Cons:0 ) :Vows | Vows %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen
           where Cx in (m p t s n r l k g h) ;
!
! oja: oija
"j lengthening"
0:i <=> [ #: ] Cons* Vows _ j Vows ( 0: ) %>:0 %^LCons:0  ;
          [ [ :Cons | :Cons Vows:0 ] ( %>:0 ) | Cons: ] i _ %>:0 ( %^LCons: | %^WCons:0 ) %^LVows: ;
         [ #: ] Cons* Vows Cons 0: i _ Cons :0 Vows [ Cons | [ #: ] ] ;
         #: Cons* Vows _ j [ Vows 0: Cons ( Cons:0 ) :Vows | Vows %^SyllBr:0 e ] %^StretchSyll2:0 ; ! ommeena<omena ; venät:vennäen, kaijaava


!!!TO HERE
!!!!!!!!!!!!!!!!!!!!!!!!!
!! Consonant weakening !!
!!!!!!!!!!!!!!!!!!!!!!!!!
! t:0, k:0 (suku:suun), p:v, ht:h, hk:h, lt:ll, rt:rr
! mp:mm, nt:nn, st:ss,
! it:ij, lk:l, rk:r, nk:ng, sk:s, tk:t, 
! kk:k, tt:t, pp:p
! ut:vv, yt:vv (uut:uuvv), uk:vv
! aika:ajan, poika:pojan, reikä:reijän, liika:liijan
! lp:lv, rp:rv



"Ut:vv Uk:vv weakening"
 Cx:Cy <=> [ #: ] Cons* Vows [ u: | y: ] _ 0:v [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;
              where Cx in (k t)
                    Cy in (v v) matched ;

"0:v Lengthening with Ut:vv weakening"
 0:v <=> [ #: ] Cons* Vows [ u: | y: ] [ t: | k: ]  _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;
         [ #: ][ Cons* Vows | Cons* Vows+ Cons:+ Vows+ :0* Cons+ :Vows ] v _  ( :Vows | AÄ1: ) [ 0:Vows %>:0 %^LCons: | %>:0 %^LCons: :0* :Vows ] ; ! arvattava:arvattavvaa
        #: Cons* Vows v _ Vows [ Vows 0: Cons ( Cons:0 ) :Vows | Vows %^SyllBr:0 e ] %^StretchSyll2:0 ; ! kevät:kevväen, tavvaara




! viis: viijes
"it:j"
 t:j <=> [ i | o ] i _ [ e | o ] %>: %^WCons:0 ;

"st:ss weakening"
t:s <=> Vows+ s _ ( %^TS:0 ) GRC ;
! last-minute verbs
! "ti:si in verbs"
   [ n | l | r | Vows Vows ] _ %^TS:0 ; !  pittää: piti 


"rt:rr weakening"
t:r <=>  Vows r _ GRC ;

"lt:ll weakening"
t:l <=>  Vows l _ GRC ;

"nt:nn weakening"
t:n <=>  Vows ( %>:0 %^WCons:0 ) n _ GRC ;

!! p

"mp:mm weakening"
p:m <=> [ :Vows ( %>:0 %^WCons:0 ) | [ a: | ä: ] %>:0 %^WCons:0 %^E1:0 | AÄ1: | Vows+ :Cons+ Vows  %^SyllBr:0 e %^StretchSyll2:0 ] m _ [ GRC | AÄ1: %>:0 %^WCons:0 ( V3: ) Cons | %^TS:0 i %>:0 %^WCons:0 ] ;
        m _ Vows l %^WGStem:0 ; ! ompel- to ommel-
! [ ( %>:0 ) :Vows ( 0: ) %>:0 %^WCons:0 | Vows:0 %>:0 i: %>:0 %^WCons:0 | Vows:0 %>:0  %^WCons:0 %>:0 i: %>:0 ] ;
!%> %^WCons %^E1 mp
!!

"nk:ng weakening"
k:g <=> [ #: ] Cons* Vows n _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;


! aika: ajan
"kToZero"
k:0 <=> [  #:  Cons* Vows (( r | i | VowsNonHigh ) k | t | h | l | r | s ) | Vows :Cons+ :Vows+ k ] _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;
![ [ #: ] Cons* Vows (( r | i ) k | t | h | l | r | s ) | Vows :Cons+ :Vows+ k ] _ [ Vows: | :Vows | ( Vows ) %>:0 i ] ( %>:0 i: | %^StretchSyll2:0 ) %>:0 %^WCons:0 ;
        [ #: ] Cons* VowsBack i: _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;

! liika:liijan, reikä:reijän
"kToj"
 k:j <=> [ #: ] Cons* [ VowsFront | VowsNeutral ] i: _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;

! mato : maot, ! mato : maool
! tToZero in weak
"Double stop:weakens tToZero"
t:0 <=> [ #: Cons* Vows | #: Cons* Vows [ Vows - [ i | u | y ] ] | Vows h [ y | u ] ] _  GRC ;
        [ #: Cons*  Vows+ [ h | (r) t ] | Vows ( %>:0 %^WCons:0 ) t | Cons* Vows+ Cons+ Vows+ ( n ) t | Vows t ] _  [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;
        t _ Vows l %^WGStem:0 ; ! ottel- to otel-

! lupa : luvan
"p:v"
p:v <=> [ #: ] Cons* Vows ( r | l | Vows ) _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;

"double pp to p"
p:0 <=> [ #: Cons* Vows ( m ) p | #: Cons* Vows Vows p | Cons* Vows+ Cons+ Vows+ p ] _ [ GRC | %^TS:0 i %>:0 %^WCons:0 ] ;

!! Vowel raising
"o:u ö:y in Inf"
 Vx:Vy <=> _ Vx: %>:0 v v AÄ1: %^RVows:0 ;
           Vx: _ %>:0 v v AÄ1: %^RVows:0 ;
          where Vx in (o ö)
                Vy in (u y) matched ;

!!
!"Disallow WLoss after non-v"
!WLoss:0 /<= [[ :* Letters ] - [ :* [ в л | в О1: л ] ] ] %>:0 _ ;

!"Disallow V3 after two consecutive realized vowels"
! V3: /<= :Vows :0* :Vows :0* _ ;

