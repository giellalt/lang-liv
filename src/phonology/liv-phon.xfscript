! This file is a script for the Xerox xfst interface.
! It gives the morphophonological rules for Livonian.
! 2012-09-07 Only the vowels have been worked on

! In this file, testcases are marked as follows:

! space exclmark euro space upper:lower
! Please add tests for eventual new rules or problems

! To test: in kt/vep/src, do:
! xfst -e "source xfst-vep.txt"
! 
! save s
! quit
! cat xfst-vep.txt | grep €|cut -d" " -f3|cut -d":" -f1|lookup s


clear stack


! Definition section !
! ================== !

echo << THE DEFININTIONS >>

define Cns [ b | c | č | d | f | g | h | j | k | l 
           | B | C | Č | D | F | G | H | J | K | L 
           | m | n | p | r | s | š | t | v | z | ž
           | M | N | P | R | S | Š | T | V | Z | Ž ] ;

define VwsLessi [ a | e | o | u | ü | ä | ö | õ | ȯ | ē | ȱ | ǭ 
                | A | E | O | U | Ü | Ä | Ö | Õ | Ȯ | Ē | Ȱ | Ǭ ] ;

define Vws [ i | I | VwsLessi ] ;

define Stod [ ’ ] ; ! soft sign

define Lts [ Cns | Vws | Stosd ] ; !

define Dummy %> ; 



! Rule section !
! ============ !

echo << THE RULES >>


define VowelCorrelation [ V1 -> а || [ QAO1 ]  %> h _ ,,
                          V1 -> e || [ i ] %> h _ ,,
                          V1 -> e || [ QEQ1 ]  %> h _  ,,
                          V1 -> o || o %> h _   ] ; ! Vowel harmony

define iToj [ i -> j || Vws _ [ QAQ1 | QAO1 ] %> ,,
             i -> 0 || Vws %- _ k ] ;

define SoftLossBeforeVowel [ ' -> 0 || _ QEQ1 %>  ] ;

define StemFinalVowelLoss [ QAO1 -> 0 , QEQ1 -> 0 , QAQ1 -> 0 , QÄQ1 -> 0  || _  [ .#. | %- ]  ] ;
 !€ marj>QAO1:marj

define VowelLossBeforePli [ QEQ1 -> 0 , QAQ1 -> 0 , QÄQ1 -> 0  || _ %> i ] ;

define QAO [ QAO1 -> a , QAQ1 -> a || _ %> [ n | d | t a | k s | l | h V1 | n n o | d m e | s ] ,,
            QAO1 -> o || _ %> i ] ;

define QÄQ1 [ QÄQ1 -> ä , QEQ1 -> e || _ %> [ Cns | VwsLessi ] ] ;

define D1E1 [ D1E1 -> d e || _ %> [ Cns | VwsLessi ] ] ;
! This should have voz' > vodel

define S2SH     [ s -> š || i %> _ ] ;

define VOICING     [ t -> d , k -> g || [ Vws | n | l | r ] %> _ [ Vws ] ,,
                     z -> s || Vws _ %> [ t | k ] ] ; ! in verb stems
 !€ sugi>ta:sugida
 !€ vali^DEVOICE>ta:valita

define DEVOICE [ %^DEVOICE -> 0 ||  _ ] ;

define VowelStemVowelLoss [ e -> 0 || _ %> i ( n| d | m | t ) ] ;

define I1Variation [ I1 -> 0 || Vws  %- _ k ,, I1 -> i || Cns :0* %- _ k ] ;

define DummyDeletion Dummy -> 0 ;



read regex [

    VowelCorrelation
.o. iToj
.o. SoftLossBeforeVowel
.o. StemFinalVowelLoss
.o. VowelLossBeforePli
.o. QAO
.o. QÄQ1
.o. D1E1
.o. S2SH
.o. VOICING
.o. DEVOICE
.o. VowelStemVowelLoss
.o. I1Variation
.o. DummyDeletion
] ;


