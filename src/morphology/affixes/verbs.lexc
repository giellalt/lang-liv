!! Verb inflection
!! ---------------
!! The UNDEFINED language verbs inflect in persons.

LEXICON V_JELLO
!! je’llõ:jelā
+V: V-01_JELLO ;

LEXICON V-01_JELLO
!! 13 je’llõ, 18 astõ, 19 võttõ, 24 vie’ddõ, 25 maksõ, 26 tappõ
 GER-õs     ;
:%^Stress2to1 INF-õ      ;
 SUP        ;
:   INDPRSSG1    ;
:   INDPRSSG2    ;
:   INDPRSSG3    ;
:%^Stress2to1   INDPRSPL1	 ;
:%^Stress2to1   INDPRSPL2	 ;
:%^Stress2to1   INDPRSPL3	 ;
:%^ConsSh   INDCONNEGSG    ;
: INDCONNEGPL1 ;
: INDCONNEGPL2 ;
: INDCONNEGPL3 ;
:ī INDPRT-z ;
!+Ind+Sg+ConNeg:	K    ;

!+Cond+Sg3:õks	K    ;
 CONDPRS ;

!+Imprt+Sg2:  	K    ;
!+Imprt+ConNeg:	K    ;
!+Imprt+Pl2:õgid	K    ;
!+Jus+Sg3:õg		K    ;
!+Quo+Sg:iji		K    ;
!+Act+PrfPrc+Sg:õn	K    ;
:%^Stress2to1%^VowsRM%^ConsRM	PSSPRFPRCSG    ;

!! 29-48 follow same pattern
LEXICON V_LUGGO
!! lu’ggõ:lugū 29
+V:	V-01_LUGGO ;

LEXICON V_MUDZO
!! mu’džõ:mudžū 30
+V:	V-01_LUGGO ;

LEXICON V_VAKTO
!! vakțõ:vakțū 31
+V:	V-01_LUGGO	;

LEXICON V_KITTO
!! kittõ:kitū 32
+V:	V-01_LUGGO ;

LEXICON V_RIDDO
!! riddõ:ridū 33
+V:	V-01_LUGGO ;

LEXICON V_KUTSO
!! kutsõ:kutsū 34
+V:	V-01_LUGGO ;

LEXICON V_LASKO
!! laskõ:laskū 35
+V:	V-01_LUGGO ;

LEXICON V_AKKO
!! akkõ:akū 36
+V:	V-01_LUGGO ;

LEXICON	V-01_LUGGO
!! This is mutual for 29-36: 
!! luggõ, mudžõ, vakțõ, kittõ, riddõ, kutsõ, laskõ, akkõ
:%^Stress2to1 GER-õs   ;
:%^Stress2to1 INF-õ	  ;
:%^Stress2to1 SUP	  ; 
: INDPRSSG1    ;
: INDPRSSG2    ;
: INDPRSSG3    ;
:%^Stress2to1   INDPRSPL1    ;
:%^Stress2to1   INDPRSPL2    ;
:%^Stress2to1   INDPRSPL3    ;
:%^Stress2to1%^VowsRM%^ConsRM	INDCONNEGSG    ;
:%^Stress2to1 INDCONNEGPL1 ;
:%^Stress2to1 INDCONNEGPL2 ;
:%^Stress2to1 INDCONNEGPL3 ;
!! Prt
:%^VowsRMī INDPRT-z    ;
! Cond
 CONDPRS ;
!! Imprt
:%^Stress2to1%^VowsRM%^ConsRM	IMPRTPRSSG2    ;
:%^Stress2to1 IMPRTPRSPL1	;
:%^Stress2to1 IMPRTPRSPL2	;
:%^Stress2to1 IMPRTPRSCONNEG ;

+Imprt+Prs+Pl2:%^Stress2to1%>gid	K    ;
!! Jus
:%^Stress2to1 JUSPRS	;
!! Kvo
:%^Stress2to1%^VowsRM QUOPRS	;

!! participles
:%^Stress2to1 ACTPRSPRC ;
:%^Stress2to1 ACTPRFPRC ;
:%^Stress2to1%^VowsRM%^ConsRM PSSPRSPRC ;
:%^Stress2to1%^VowsRM%^ConsRM	PSSPRFPRCSG    ;


LEXICON V_AIGO
!! aigõ:āigõ 37
+V:	V-01_AIGO ;


LEXICON V_KUORRO
!! kuoŗŗõ:kūoŗõ 38
+V:	V-01_AIGO	;


LEXICON V_VANNO
!! vannõ:vǭnõ 39
+V:	V-01_AIGO	;


LEXICON V_POLLO
!! pȯļļõ:pūoļõ 40
+V:	V-01_AIGO	;


LEXICON V_POIMO
!! pȯimõ:pūoimõ 41
+V:	V-01_AIGO	;


LEXICON V_OURO
!! ouŗõ:ōŗõ 42
+V:	V-01_AIGO	;


LEXICON V_KEIJO
!! keijõ:kējõ 43
+V:	V-01_AIGO	;


LEXICON V_ARSTO
!! aŗštõ:āŗštõ 44
+V:	V-01_AIGO	;


LEXICON V_PORTO
!! pȯrtõ:pūortõ 45
+V:	V-01_AIGO	;


LEXICON V_OUTO
!! outõ:ōtõ 46
+V:	V-01_AIGO	;


LEXICON V_TUNDO
!! tundõ:tūndõ 47
+V:	V-01_AIGO	;


LEXICON V_OUDO
!! oudõ:ōdõ 48
+V:	V-01_AIGO	;

LEXICON V-01_AIGO
!! This is mutual for 37-48
:%^Stress1to2 GER-õs   ;
:%^Stress1to2 INF-õ	  ;
:%^Stress1to2 SUP	  ; 
 INDPRSSG1    ;
: INDPRSSG2    ;
: INDPRSSG3    ;
:%^Stress1to2   INDPRSPL1    ;
:%^Stress1to2   INDPRSPL2    ;
:%^Stress1to2   INDPRSPL3    ;
:%^Stress1to2%^VowsRM	INDCONNEGSG    ;
:%^Stress1to2 INDCONNEGPL1 ;
:%^Stress1to2 INDCONNEGPL2 ;
:%^Stress1to2 INDCONNEGPL3 ;
!! Prt
:%^VowsRMi INDPRT-z    ;

!! participles
:%^Stress1to2 ACTPRSPRC ;
:%^Stress1to2 ACTPRFPRC ;
:%^Stress1to2%^VowsRM%^ConsRM PSSPRSPRC ;
:%^Stress1to2%^VowsRM%^ConsRM	PSSPRFPRCSG    ;

:%^Stress1to2 CONDPRS ;
:%^Stress1to2%^VowsRM IMPRTPRSSG2 ;
:%^Stress1to2 IMPRTPRSPL1 ;
:%^Stress1to2 IMPRTPRSPL2 ;
 IMPRTPRSCONNEG ;
:%^Stress1to2 JUSPRS	;
:%^Stress1to2%^VowsRM QUOPRS	;		


LEXICON V-kūlõ
!kūlõ:kūl 49
+V: V-kūlõ_01 ;

LEXICON V-arrõ
!arrõ:arr 50
+V: V-kūlõ_01 ;

LEXICON V-ailõ
!ailõ:ail 51
+V: V-kūlõ_01 ;

LEXICON V-svarrõ
! svarrõ:svarr 52
+V: V-kūlõ_01 ;

LEXICON V-kītõ
!kītõ:kīt 53
+V: V-kūlõ_01 ;

LEXICON V-äbțõ
!äbțõ:äbț 54
+V: V-kūlõ_01 ;

LEXICON V-kūldõ
!kūldõ:kūld 55
+V: V-kūlõ_01 ;

LEXICON V-kīskõ
!kīskõ:kīsk 56
+V: V-kūlõ_01 ;

LEXICON V-īedõ
!īedõ:īed 57
+V: V-kūlõ_01 ;

LEXICON V-kūlõ_01
! This is mutual for 49-57
 GER-õs   ;
 INF-õ	  ;
 SUP	  ; 
:õ INDPRSSG1    ;
:õ INDPRSSG2    ;
:õ INDPRSSG3    ;
:õ   INDPRSPL1    ;
:õ   INDPRSPL2    ;
:õ   INDPRSPL3    ;
:%^ConsSh	INDCONNEGSG    ;
:õ INDCONNEGPL1 ;
:õ INDCONNEGPL2 ;
:õ INDCONNEGPL3 ;
!! Prt
:i INDPRT-z    ;



LEXICON V-umblõ
+V: VTag ;

LEXICON VTag
	Persons	;

LEXICON Persons
+V+1sg:en   #   ;
+V+2sg:et   #   ;
+V+3sg:e    #   ;
+V+1pl:ien  #   ;
+V+2pl:iet  #   ;
+V+3pl:iet  #   ;
+V+inf:0    #   ;
!#:0   Nouns   ;

!! Nonfinites
LEXICON GER-õs
+Ger+Ine:%>s   K ;

LEXICON GER-sõ
+Ger+Ine:%>sõ   K ;

LEXICON INF-dõ
+Inf:%>dõ    K   ;

LEXICON INF-õ
+Inf:    K   ;

LEXICON SUP
+Sup+Ill:%>m K   ;

LEXICON ACTPRSPRC
+Act+PrsPrc+Sg:%>b	K    ;
+Act+PrsPrc+Pl:%>bõd	K    ;

LEXICON	ACTPRFPRC
+Act+PrfPrc+Sg:%>n	K    ;
+Act+PrfPrc+Pl:%>nd	K    ;

LEXICON PSSPRSPRC
+Pss+PrsPrc+Sg:%>dõb        K    ;
+Pss+PrsPrc+Pl:%>dõbõd      K    ;

LEXICON PSSPRFPRCSG
+Pss+PrfPrc+Sg:%>dõd    K    ;

!! Finites

LEXICON INDPRT-z
!! Indicative preterite in z
 INDPRTSG1-z    ;
 INDPRTSG2-z    ;
 INDPRTSG3-z    ;
 INDPRTPL1-z    ;
 INDPRTPL2-z    ;
 INDPRTPL3-z    ;


LEXICON CONDPRS
!! Conditional present
  CONDPRSSG1	;
  CONDPRSSG2	;
  CONDPRSSG3	;
  CONDPRSPL1	;
  CONDPRSPL2	;
  CONDPRSPL3	;


 ! Indicative present
LEXICON INDPRSSG1
 +Ind+Prs+Sg1:%>b    K	;

LEXICON INDPRSSG2
 +Ind+Prs+Sg2:%>d    K	;

LEXICON INDPRSSG3
 +Ind+Prs+Sg3:%>b    K	;

LEXICON INDPRSPL1
 +Ind+Prs+Pl1:%>m    K	;

LEXICON INDPRSPL2
 +Ind+Prs+Pl2:%>t    K	;

LEXICON INDPRSPL3
 +Ind+Prs+Pl3:%>bõd    K	;

 ! Indicative preterite
LEXICON INDPRTSG1-z
 +Ind+Prt+Sg1:%>z    K	;

LEXICON INDPRTSG2-z
 +Ind+Prt+Sg2:%>zt    K	;

LEXICON INDPRTSG3-z
 +Ind+Prt+Sg3:%>z    K	;

LEXICON INDPRTPL1-z
 +Ind+Prt+Pl1:%>zmõ    K	;

LEXICON INDPRTPL2-z
 +Ind+Prt+Pl2:%>zt    K	;
 +Ind+Prt+Pl2:%>ztõ    K	;

LEXICON INDPRTPL3-z
 +Ind+Prt+Pl3:%>zt    K	;
 +Ind+Prt+Pl3:%>ztõ    K	;


LEXICON INDCONNEGSG
+Ind+ConNeg+Sg:    K	;

LEXICON INDCONNEGPL1
 +Ind+ConNeg+Pl1:%>m    K	;

LEXICON INDCONNEGPL2
 +Ind+ConNeg+Pl2:%>t    K	;

LEXICON INDCONNEGPL3
 +Ind+ConNeg+Pl3:%>t    K	;

!! Conditional
LEXICON CONDPRSSG1
 +Cond+Prs+Sg1:%>ks    K	;

LEXICON CONDPRSSG2
 +Cond+Prs+Sg2:%>kst    K	;

LEXICON CONDPRSSG3
 +Cond+Prs+Sg3:%>ks    K	;

LEXICON CONDPRSPL1
 +Cond+Prs+Pl1:%>ksmõ    K	;

LEXICON CONDPRSPL2
 +Cond+Prs+Pl2:%>kstõ    K	;

LEXICON CONDPRSPL3
 +Cond+Prs+Pl3:%>kstõ    K	;

!! Imperative
LEXICON IMPRTPRSSG2
+Imprt+Prs+Sg2: K ;

LEXICON IMPRTPRSPL1
+Imprt+Prs+Pl1:%>gõm K ;

LEXICON IMPRTPRSPL2
+Imprt+Prs+Pl2:%>gid K ;

LEXICON IMPRTPRSCONNEG
+Imprt+Prs+ConNeg:      K    ;

!! Jussative
LEXICON JUSPRS
+Jus+Prs+Sg:%>g             K    ;
+Jus+Prs+Pl:%>gõd             K    ;

!! Quotative
LEXICON QUOPRS
+Quo+Prs+Sg:%>iji           K    ;
+Quo+Prs+Pl:%>ijid           K    ;

! vim: set ft=xfst-lexc:

