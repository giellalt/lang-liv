!Distributed under the terms of the GNU General Public License version 2 
! or any later version.

! =================================== !
! File containing numerals to cyphers !
! =================================== !

! Lexica for adding tags and periods
! ----------------------------------

LEXICON Root
 CARDINAL-NUMERALS ;
 ORDINAL-NUMERALS ;

LEXICON CARDINAL-NUMERALS
HUNDREDSM ;		     ! 200M
sadā:1 HUNDREDM ; ! 100M
TENSM ;		         ! 20-99M
TEENSM ;	         ! 10-19M
1MILJON ;            ! 1M
ONESM ;		         ! 1-9M
1GIGA ;            	 ! 1G
ONESG ;		         ! 1-9G
HUNDREDST ;	         ! 200000-999999
sadā:1 HUNDREDT ; ! 100000-100999
TENST ;			     ! 20000-99999,10000-10999
TEENST ;		     ! 11000-19999
ONEST ;			     ! 2000-9999
tūontõ:1 THOUSAND ; ! 1000-1999

UNDERTHOUSAND ;		 ! 100-999
TENS ;			     ! 20-99
TEENS ;			     ! 10-19
ONES ;			     ! 1-9

LEXICON ONESG
ikš% : 1GIGA ;
kakš% :2 GIGA ;
kuolm% :3 GIGA ;
nēļa% :4 GIGA ;
vīž% :5 GIGA ;
kūž% :6 GIGA ;
seis% :7 GIGA ;
kǭdõks% :8 GIGA ;
īdõks% :9 GIGA ;

LEXICON 1GIGA
miljard% :1 OVERMILLIONS ;

LEXICON GIGA
miljard% : OVERMILLIONS ;

LEXICON HUNDREDSM

kakš:2 CUODIM ;
kuolm:3 CUODIM ;
nēļa:4 CUODIM ;
vīž:5 CUODIM ;
kūž:6 CUODIM ;
seis:7 CUODIM ;
kǭdõks:8 CUODIM ;
īdõks:9 CUODIM ;

LEXICON CUODIM
sadā: HUNDREDM ; !200, not 100

LEXICON HUNDREDM
TENSM ;
TEENSM ;
:%0 ONESM ;
% :%0%0 MILJON ;

LEXICON TEENSM
:1 TEENM ;

LEXICON TEENM
ikš:1 LOHKAIM ;
kakš:2 LOHKAIM ;
kuolm:3 LOHKAIM ;
nēļa:4 LOHKAIM ;
vīž:5 LOHKAIM ;
kūž:6 LOHKAIM ;
seis:7 LOHKAIM ;
kǭdõks:8 LOHKAIM ;
īdõks:9 LOHKAIM ;

LEXICON LOHKAIM
tuoistõn% : MILJON ;

LEXICON TENSM
kim% :1%0 MILJON ;

kakš:2 LÅGEVM ;
kuolm:3 LÅGEVM ;
nēļa:4 LÅGEVM ;
vīž:5 LÅGEVM ;
kūž:6 LÅGEVM ;
seis:7 LÅGEVM ;
kǭdõks:8 LÅGEVM ;
īdõks:9 LÅGEVM ;

LEXICON LÅGEVM
kimdõ% :%0 MILJON ;
kimdõ: ONESM ;

LEXICON ONESM
ikš% :  1MILJON ;

kakš% :2 MILJON ;
kuolm% :3 MILJON ;
nēļa% :4 MILJON ;
vīž% :5 MILJON ;
kūž% :6 MILJON ;
seis% :7 MILJON ;
kǭdõks% :8 MILJON ;
īdõks% :9 MILJON ;

LEXICON 1MILJON
miljon% :1 OVERTHOUSANDS ;

LEXICON MILJON
miljon% : OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON OVERMILLIONS
HUNDREDSM ;
sadā:1 HUNDREDM ;
% :%0 TENSM ;
:%0 TEENSM ;
:%0%0 1MILJON ;
:%0%0 ONESM ;
:%0%0%0 HUNDREDST ;		        ! x00.000.000
sadā:%0%0%01 HUNDREDT ;    ! 100.000.000
:%0%0%0%0 TENST ;		        !  20.000.000
:%0%0%0%0 TEENST ;		    !  10.000.000
:%0%0%0%0%0 ONEST ;		    !   2.000.000
tūontõ:%0%0%0%0%01 THOUSAND ;	!   1.000.000
:%0%0%0%0%0%0 UNDERTHOUSAND ;	
% :%0%0%0%0%0%0%0 TENS ;
:%0%0%0%0%0%0%0 TEENS ;
:%0%0%0%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0%0%0%0 # ;	     !  x000000

LEXICON OVERTHOUSANDS
HUNDREDST ;		        ! x00.000.000
sadā:1 HUNDREDT ;    ! 100.000.000
:%0 TENST ;		        !  20.000.000
:%0 TEENST ;		    !  10.000.000
:%0%0 ONEST ;		    !   2.000.000
tūontõ:%0%01 THOUSAND ;	!   1.000.000
:%0%0%0 UNDERTHOUSAND ;	
% :%0%0%0%0 TENS ;
:%0%0%0%0 TEENS ;
:%0%0%0%0%0 ONES ;
:%0%0%0%0%0%0 # ;	     !  x000000

LEXICON HUNDREDST

kakš:2 CUODIT ;
kuolm:3 CUODIT ; ! 300000-399999
nēļa:4 CUODIT ; ! 400000-499999
vīž:5 CUODIT ; ! 500000-599999
kūž:6 CUODIT ; ! 600000-699999
seis:7 CUODIT ; ! 
kǭdõks:8 CUODIT ; ! 800000-899999
īdõks:9 CUODIT ; ! 900000-999999

LEXICON CUODIT
sadā% : HUNDREDT ;	! 200000-199999

LEXICON HUNDREDT	     ! X = 1-9, Y = 0-9
TENST ;			     ! X2XYYY, X10YYY
TEENST ;		     ! X1XYYY
ikš% :%01 THOUSANDS ;
:%0 ONEST ;		     ! XX0YYY
% :%0%0 THOUSANDS ;	 ! X00YYY

LEXICON TEENST
:1 TEENT ;

LEXICON TEENT
kim% :%0 THOUSANDS ;
ikš:1 LOHKAIT ;
kakš:2 LOHKAIT ;
kuolm:3 LOHKAIT ;
nēļa:4 LOHKAIT ;
vīž:5 LOHKAIT ;
kūž:6 LOHKAIT ;
seis:7 LOHKAIT ;
kǭdõks:8 LOHKAIT ;
īdõks:9 LOHKAIT ;

LEXICON LOHKAIT
tuoistõn% : THOUSANDS ;

LEXICON TENST
ikš:1 LÅGEVT ;
kakš:2 LÅGEVT ;
kuolm:3 LÅGEVT ;
nēļa:4 LÅGEVT ;
vīž:5 LÅGEVT ;
kūž:6 LÅGEVT ;
seis:7 LÅGEVT ;
kǭdõks:8 LÅGEVT ;
īdõks:9 LÅGEVT ;

LEXICON LÅGEVT
kimdõ% :%0 THOUSANDS ;
!tūontõ:1 THOUSAND ;
kimdõ% : ONEST ;

LEXICON ONEST
tūontõ:1 THOUSAND ;
kakš% :2 THOUSANDS ;
kuolm% :3 THOUSANDS ;
nēļa% :4 THOUSANDS ;
vīž% :5 THOUSANDS ;
kūž% :6 THOUSANDS ;
seis% :7 THOUSANDS ;
kǭdõks% :8 THOUSANDS ;
īdõks% :9 THOUSANDS ;

LEXICON THOUSANDS      ! x > 1
!% tūontõd: THOUSAND ;
tūontõ: THOUSAND ;


LEXICON THOUSAND
% : UNDERTHOUSAND ;	   ! 1100-1999
% :%0 TENS ;	   ! 1020-1099
% :%0 TEENS ;	   ! 1010-1019
% :%0%0 ONES ;	   ! 1001-1009
:%0%0%0 # ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON UNDERTHOUSAND
HUNDREDS ;
sadā:1 HUNDRED ;


LEXICON HUNDREDS
kakš:2 CUODI ;
kuolm:3 CUODI ;
nēļa:4 CUODI ;
vīž:5 CUODI ;
kūž:6 CUODI ;
seis:7 CUODI ;
kǭdõks:8 CUODI ;
īdõks:9 CUODI ;

LEXICON CUODI
!% saddõ: HUNDRED ;
sadā: HUNDRED ;

LEXICON HUNDRED
% : TENS ;
TEENS ;
:%0 ONES ;
:%0%0 # ;

LEXICON TEENS
ikš:11 LOHKAI ;
kakš:12 LOHKAI ;
kuolm:13 LOHKAI ;
nēļa:14 LOHKAI ;
vīž :15 LOHKAI ;
kūž:16 LOHKAI ;
seis:17 LOHKAI ;
kǭdõks:18 LOHKAI ;
īdõks:19 LOHKAI ;


LEXICON LOHKAI
tuoistõn: # ;

LEXICON TENS
kim:1%0 # ;
kakš:2 LÅGEV ;
kuolm:3 LÅGEV ;
nēļa:4 LÅGEV ;
vīž:5 LÅGEV ;
kūž:6 LÅGEV ;
seis:7 LÅGEV ;
kǭdõks:8 LÅGEV ;
īdõks:9 LÅGEV ;

LEXICON LÅGEV
kimdõ:%0 # ;
kimdõ% : ONES ;


LEXICON ONES
ikš:1 # ;
kakš:2 # ;
kuolm:3 # ;
nēļa:4 # ;
vīž:5 # ;
kūž:6 # ;
seis:7 # ;
kǭdõks:8 # ;
īdõks:9 # ;


!! Starting work with ordinals
LEXICON ORDINAL-NUMERALS
ORD-HUNDREDSM ;		     ! 200M
sadāz:1 ORD-HUNDREDM ; ! 100M
ORD-TENSM ;		         ! 20-99M
ORD-TEENSM ;	         ! 10-19M
ORD-1MILJON ;            ! 1M
ORD-ONESM ;		         ! 1-9M
ORD-1GIGA ;            	 ! 1G
ORD-ONESG ;		         ! 1-9G
ORD-HUNDREDST ;	         ! 200000-999999
sadāz:1 ORD-HUNDREDT ; ! 100000-100999
ORD-TENST ;			     ! 20000-99999,10000-10999
ORD-TEENST ;		     ! 11000-19999
ORD-ONEST ;			     ! 2000-9999
tūontõz:1 ORD-THOUSAND ; ! 1000-1999

ORD-UNDERTHOUSAND ;		 ! 100-999
ORD-TENS ;			     ! 20-99
ORD-TEENS ;			     ! 10-19
ORD-ONES ;			     ! 1-9

LEXICON ORD-ONESG
īʼdõz% : ORD-1GIGA ;
kǭʼdõz% :2 ORD-GIGA ;
kuolmõz% :3 ORD-GIGA ;
nēļļõz% :4 ORD-GIGA ;
vīdõz% :5 ORD-GIGA ;
kūdõz% :6 ORD-GIGA ;
seismõz% :7 ORD-GIGA ;
kǭdõksmõz% :8 ORD-GIGA ;
īdõksmõz% :9 ORD-GIGA ;

LEXICON ORD-1GIGA
miljardõz% :1 ORD-OVERMILLIONS ;

LEXICON ORD-GIGA
miljardõz% : ORD-OVERMILLIONS ;

LEXICON ORD-HUNDREDSM

kǭʼd:2 ORD-CUODIM ;
kuolm:3 ORD-CUODIM ;
nēļa:4 ORD-CUODIM ;
vīd:5 ORD-CUODIM ;
kūd:6 ORD-CUODIM ;
seis:7 ORD-CUODIM ;
kǭdõks:8 ORD-CUODIM ;
īdõks:9 ORD-CUODIM ;

LEXICON ORD-CUODIM
sadāz: ORD-HUNDREDM ; !200, not 100

LEXICON ORD-HUNDREDM
% : ORD-TENSM ;
ORD-TEENSM ;
:%0 ORD-ONESM ;
% :%0%0 ORD-MILJON ;

LEXICON ORD-TEENSM
:1 ORD-TEENM ;

LEXICON ORD-TEENM
ikš:1 ORD-LOHKAIM ;
īʼd:1 ORD-LOHKAIM ;
kǭʼd:2 ORD-LOHKAIM ;
kuolm:3 ORD-LOHKAIM ;
nēļa:4 ORD-LOHKAIM ;
vīd:5 ORD-LOHKAIM ;
kūd:6 ORD-LOHKAIM ;
seis:7 ORD-LOHKAIM ;
kǭdõks:8 ORD-LOHKAIM ;
īdõks:9 ORD-LOHKAIM ;

LEXICON ORD-LOHKAIM
tuoistõnz% : ORD-MILJON ;

LEXICON ORD-TENSM
kimmõz% :1%0 ORD-MILJON ;

kǭʼd:2 ORD-LÅGEVM ;
kuolm:3 ORD-LÅGEVM ;
nēļa:4 ORD-LÅGEVM ;
vīd:5 ORD-LÅGEVM ;
kūd:6 ORD-LÅGEVM ;
seis:7 ORD-LÅGEVM ;
kǭdõks:8 ORD-LÅGEVM ;
īdõks:9 ORD-LÅGEVM ;

LEXICON ORD-LÅGEVM
kimdõz% :%0 ORD-MILJON ;
kimdõz: ORD-ONESM ;

LEXICON ORD-ONESM
īʼdõz% :  ORD-1MILJON ;

kǭʼd% :2 ORD-MILJON ;
kuolm% :3 ORD-MILJON ;
nēļa% :4 ORD-MILJON ;
vīd% :5 ORD-MILJON ;
kūd% :6 ORD-MILJON ;
seis% :7 ORD-MILJON ;
kǭdõks% :8 ORD-MILJON ;
īdõks% :9 ORD-MILJON ;

LEXICON ORD-1MILJON
miljonõz% :1 ORD-OVERTHOUSANDS ;

LEXICON ORD-MILJON
miljonõz% : ORD-OVERTHOUSANDS ;

! =================
! Under the million
! =================

LEXICON ORD-OVERMILLIONS
ORD-HUNDREDSM ;
sadāz:1 ORD-HUNDREDM ;
% :%0 ORD-TENSM ;
:%0 ORD-TEENSM ;
:%0%0 ORD-1MILJON ;
:%0%0 ORD-ONESM ;
:%0%0%0 ORD-HUNDREDST ;		        ! x00.000.000
sadāz:%0%0%01 ORD-HUNDREDT ;    ! 100.000.000
:%0%0%0%0 ORD-TENST ;		        !  20.000.000
:%0%0%0%0 ORD-TEENST ;		    !  10.000.000
:%0%0%0%0%0 ORD-ONEST ;		    !   2.000.000
tūʼontõz:%0%0%0%0%01 ORD-THOUSAND ;	!   1.000.000
:%0%0%0%0%0%0 ORD-UNDERTHOUSAND ;	
% :%0%0%0%0%0%0%0 ORD-TENS ;
:%0%0%0%0%0%0%0 ORD-TEENS ;
:%0%0%0%0%0%0%0%0 ORD-ONES ;
:%0%0%0%0%0%0%0%0%0 # ;	     !  x000000

LEXICON ORD-OVERTHOUSANDS
ORD-HUNDREDST ;		        ! x00.000.000
sadāz:1 ORD-HUNDREDT ;    ! 100.000.000
:%0 ORD-TENST ;		        !  20.000.000
:%0 ORD-TEENST ;		    !  10.000.000
:%0%0 ORD-ONEST ;		    !   2.000.000
tūʼontõz:%0%01 ORD-THOUSAND ;	!   1.000.000
:%0%0%0 ORD-UNDERTHOUSAND ;	
% :%0%0%0%0 ORD-TENS ;
:%0%0%0%0 ORD-TEENS ;
:%0%0%0%0%0 ORD-ONES ;
:%0%0%0%0%0%0 # ;	     !  x000000

LEXICON ORD-HUNDREDST

kǭʼd:2 ORD-CUODIT ;
kuolm:3 ORD-CUODIT ; ! 300000-399999
nēļa:4 ORD-CUODIT ; ! 400000-499999
vīd:5 ORD-CUODIT ; ! 500000-599999
kūd:6 ORD-CUODIT ; ! 600000-699999
seis:7 ORD-CUODIT ; ! 
kǭdõks:8 ORD-CUODIT ; ! 800000-899999
īdõks:9 ORD-CUODIT ; ! 900000-999999

LEXICON ORD-CUODIT
sadāz% : ORD-HUNDREDT ;	! 200000-199999

LEXICON ORD-HUNDREDT	     ! X = 1-9, Y = 0-9
ORD-TENST ;			     ! X2XYYY, X10YYY
ORD-TEENST ;		     ! X1XYYY
ikš% :%01 ORD-THOUSANDS ;
:%0 ORD-ONEST ;		     ! XX0YYY
% :%0%0 ORD-THOUSANDS ;	 ! X00YYY

LEXICON ORD-TEENST
:1 ORD-TEENT ;

LEXICON ORD-TEENT
kimmõz% :%0 ORD-THOUSANDS ;
ikš:1 ORD-LOHKAIT ;
īʼd:1 ORD-LOHKAIT ;
kǭʼd:2 ORD-LOHKAIT ;
kuolm:3 ORD-LOHKAIT ;
nēļa:4 ORD-LOHKAIT ;
vīd:5 ORD-LOHKAIT ;
kūd:6 ORD-LOHKAIT ;
seis:7 ORD-LOHKAIT ;
kǭdõks:8 ORD-LOHKAIT ;
īdõks:9 ORD-LOHKAIT ;

LEXICON ORD-LOHKAIT
tuoistõnz% : ORD-THOUSANDS ;

LEXICON ORD-TENST
ikš:1 ORD-LÅGEVT ;
kǭʼd:2 ORD-LÅGEVT ;
kuolm:3 ORD-LÅGEVT ;
nēļa:4 ORD-LÅGEVT ;
vīd:5 ORD-LÅGEVT ;
kūd:6 ORD-LÅGEVT ;
seis:7 ORD-LÅGEVT ;
kǭdõks:8 ORD-LÅGEVT ;
īdõks:9 ORD-LÅGEVT ;

LEXICON ORD-LÅGEVT
kimdõz% :%0 ORD-THOUSANDS ;
kimdõz% : ORD-ONEST ;

LEXICON ORD-ONEST
tūʼontõz% :1 ORD-THOUSAND ;
kǭʼdõz% :2 ORD-THOUSANDS ;
kuolmõz% :3 ORD-THOUSANDS ;
nēļļõz% :4 ORD-THOUSANDS ;
vīdõz% :5 ORD-THOUSANDS ;
kūdõz% :6 ORD-THOUSANDS ;
seismõz% :7 ORD-THOUSANDS ;
kǭdõksmõz% :8 ORD-THOUSANDS ;
īdõksmõz% :9 ORD-THOUSANDS ;

LEXICON ORD-THOUSANDS      ! x > 1
tūʼontõz% : ORD-THOUSAND ;


LEXICON ORD-THOUSAND
: ORD-UNDERTHOUSAND ;	   ! 1100-1999
:%0 ORD-TENS ;	   ! 1020-1099
:%0 ORD-TEENS ;	   ! 1010-1019
:%0%0 ORD-ONES ;	   ! 1001-1009
:%0%0%0 ORD-END ;	   ! 1000

!===========================
!Here starts the 999 numbers
!===========================

LEXICON ORD-UNDERTHOUSAND
ORD-HUNDREDS ;
sadāz:1 ORD-HUNDRED ;


LEXICON ORD-HUNDREDS
kǭʼd:2 ORD-CUODI ;
kuolm:3 ORD-CUODI ;
nēļa:4 ORD-CUODI ;
vīd:5 ORD-CUODI ;
kūd:6 ORD-CUODI ;
seis:7 ORD-CUODI ;
kǭdõks:8 ORD-CUODI ;
īdõks:9 ORD-CUODI ;

LEXICON ORD-CUODI
sadāz% : ORD-HUNDRED ;

LEXICON ORD-HUNDRED
ORD-TENS ;
ORD-TEENS ;
:%0 ORD-ONES ;
:%0%0 ORD-END ;

LEXICON ORD-TEENS
ikš:11 ORD-LOHKAI ;
īʼd:11 ORD-LOHKAI ;
kǭʼd:12 ORD-LOHKAI ;
kuolm:13 ORD-LOHKAI ;
nēļa:14 ORD-LOHKAI ;
vīd:15 ORD-LOHKAI ;
kūd:16 ORD-LOHKAI ;
seismõz:17 ORD-LOHKAI ;
seis:17 ORD-LOHKAI ;
kǭʼdõks:18 ORD-LOHKAI ;
īʼdõks:19 ORD-LOHKAI ;


LEXICON ORD-LOHKAI
tuoistõnz: ORD-END ;

LEXICON ORD-TENS
kimmõz:1%0 # ;
kǭʼd:2 ORD-LÅGEV ;
kuolm:3 ORD-LÅGEV ;
nēļa:4 ORD-LÅGEV ;
vīd:5 ORD-LÅGEV ;
kūd:6 ORD-LÅGEV ;
seis:7 ORD-LÅGEV ;
kǭdõks:8 ORD-LÅGEV ;
īdõks:9 ORD-LÅGEV ;

LEXICON ORD-LÅGEV
kimdõz:%0 ORD-END ;
kimdõz% : ORD-ONES ;


LEXICON ORD-ONES
eʼžmi:1 ORD-END ;
tuoi:2 ORD-END ;
kuolmõz:3 ORD-END ;
kuolmi:3 ORD-END ;
neļļõz:4 ORD-END ;
vīdõz:5 ORD-END ;
kūdõz:6 ORD-END ;
seismõz:7 ORD-END ;
seismi:7 ORD-END ;
kǭʼdõksmõz:8 ORD-END ;
īʼdõksmõz:9 ORD-END ;
!


LEXICON ORD-END
:%. # ;
